?macro <? if_var:block ( ?r:opt<? & ?> ?m:opt<? mut ?> ?var_name:ident : ?e:expr ) ?b:block ?>
->
<?
	with( & ?m<? mut ?> ??e_result : ?e )
	{
		if( !??e_result.empty() )
		{
			with( ?r<? & ?> ?m<? mut ?> ?var_name : ??e_result.try_deref() )
			?b
		}
	}
?>

?macro <? foreach:block ( ?r:opt<? & ?> ?m:opt<? mut ?> ?var_name:ident : ?e:expr ) ?b:block ?>
->
<?
	with( & ?m<? mut ?> ??e_result : ?e )
	{
		static_if( typeinfo</ typeof(??e_result) />.is_array )
		{
			for( var size_type mut ??i(0u); ??i < typeinfo</ typeof(??e_result) />.element_count; ++ ??i )
			{
				auto ?r<? & ?> ?m<? mut ?> ?var_name= ??e_result[??i];
				safe{ ?b }
			}
		}
		else if( typeinfo</ typeof(??e_result) />.is_tuple )
		{
			for( ?r<? & ?> ?m<? mut ?> ?var_name : ??e_result )
			?b
		}
		else
		{
			unsafe
			{
				for( auto mut ??r= ??e_result.range(); !??r.empty(); ??r.drop_front_unchecked() )
				{
					with( ?r<? & ?> ?m<? mut ?> ?var_name : ??r.front_unchecked() )
					{
						safe{ ?b }
					}
				}
			}
		}
	}
?>
