import "syntax_elements.uh"

namespace U1
{

namespace Synt
{

fn GetSrcLoc( Initializer& initializer ) : SrcLoc
{
	variant_visit( &el : initializer )
	{
		type ElementT= typeof(el);
		static_if( ust::is_same_type</ ElementT, EmptyVariant />() )
		{
			return SrcLoc();
		}
		else if( ust::is_same_type</ ElementT, Expression />() )
		{
			return GetSrcLoc(el);
		}
		else
		{
			return el.src_loc;
		}
	}
	halt;
}

fn GetSrcLoc( Expression& expression ) : SrcLoc
{
	variant_visit( &el : expression )
	{
		type ElementT= typeof(el);
		static_if( ust::is_same_type</ ElementT, EmptyVariant />() )
		{
			return SrcLoc();
		}
		else if( ust::is_same_type</ ElementT, ust::box</ArrayType/> />() )
		{
			return el.deref().src_loc;
		}
		else if( ust::is_same_type</ ElementT, ust::box</FunctionType/> />() )
		{
			return el.deref().src_loc;
		}
		else if( ust::is_same_type</ ElementT, ust::box</TupleType/> />() )
		{
			return el.deref().src_loc;
		}
		else if( ust::is_same_type</ ElementT, ust::box</RawPointerType/> />() )
		{
			return el.deref().src_loc;
		}
		else if( ust::is_same_type</ ElementT, ust::box</GeneratorType/> />() )
		{
			return el.deref().src_loc;
		}
		else
		{
			return el.src_loc;
		}
	}
	halt;
}

fn GetSrcLoc( BlockElement& block_element ) : SrcLoc
{
	variant_visit( &el : block_element )
	{
		type ElementT= typeof(el);
		static_if( ust::is_same_type</ ElementT, Expression />() )
		{
			return GetSrcLoc(el);
		}
		else if( ust::is_same_type</ ElementT, ust::box</ScopeBlock/> />() )
		{
			return el.deref().src_loc;
		}
		else
		{
			return el.src_loc;
		}
	}
	halt;
}

} // namespace Synt

} // namespace U1
