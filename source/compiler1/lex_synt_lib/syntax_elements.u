import "/keywords.uh"
import "syntax_elements.uh"

namespace U1
{

namespace Synt
{

fn FunctionType::IsAutoReturn( this ) : bool
{
	if_var( &r : return_type )
	{
		if_var( &n : r.get</ComplexName/>() )
		{
			if_var( &l : n.get</NameLookup/>() )
			{
				return l.name == KeywordToString( Keyword::auto_ );
			}
		}
	}
	return false;
}

fn GetSrcLoc( Initializer& initializer ) : SrcLoc
{
	variant_visit( &el : initializer )
	{
		type ElementT= typeof(el);
		static_if( same_type</ ElementT, EmptyVariant /> )
		{
			return SrcLoc();
		}
		else static_if( same_type</ ElementT, Expression /> )
		{
			return GetSrcLoc(el);
		}
		else
		{
			return el.src_loc;
		}
	}
	halt;
}

fn GetSrcLoc( ComplexName& complex_name ) : SrcLoc
{
	variant_visit( &el : complex_name )
	{
		return el.src_loc;
	}
	halt;
}

fn GetSrcLoc( Expression& expression ) : SrcLoc
{
	variant_visit( &el : expression )
	{
		type ElementT= typeof(el);
		static_if( same_type</ ElementT, EmptyVariant /> )
		{
			return SrcLoc();
		}
		else static_if( same_type</ ElementT, ust::box</ArrayType/> /> )
		{
			return el.deref().src_loc;
		}
		else static_if( same_type</ ElementT, ust::box</FunctionType/> /> )
		{
			return el.deref().src_loc;
		}
		else static_if( same_type</ ElementT, ust::box</TupleType/> /> )
		{
			return el.deref().src_loc;
		}
		else static_if( same_type</ ElementT, ust::box</RawPointerType/> /> )
		{
			return el.deref().src_loc;
		}
		else static_if( same_type</ ElementT, ust::box</CoroutineType/> /> )
		{
			return el.deref().src_loc;
		}
		else static_if( same_type</ ElementT, ust::shared_ptr_final</Mixin/> /> )
		{
			return el.deref().src_loc;
		}
		else static_if( same_type</ ElementT, ComplexName /> )
		{
			return GetSrcLoc( el );
		}
		else
		{
			return el.src_loc;
		}
	}
	halt;
}

fn GetSrcLoc( BlockElement& block_element ) : SrcLoc
{
	variant_visit( &el : block_element )
	{
		type ElementT= typeof(el);
		static_if( same_type</ ElementT, Expression /> )
		{
			return GetSrcLoc(el);
		}
		else static_if( same_type</ ElementT, ust::box</ScopeBlock/> /> )
		{
			return el.deref().src_loc;
		}
		else static_if( same_type</ ElementT, ust::shared_ptr_final</TypeAlias/> /> )
		{
			return el.deref().src_loc;
		}
		else static_if( same_type</ ElementT, ust::shared_ptr_final</Mixin/> /> )
		{
			return el.deref().src_loc;
		}
		else
		{
			return el.src_loc;
		}
	}
	halt;
}

} // namespace Synt

} // namespace U1
