import "/string_conversions.u"
import "code_builder.uh"

namespace U1
{

fn CodeBuilder::CreateFunctionDebugInfo( this, FunctionVariable& func_variable )
{
	if( !build_debug_info_ )
	{
		return;
	}

	var Synt::Function& syntax_element= func_variable.body_syntax_element.get_ref();
	var FilePos& file_pos= syntax_element.file_pos;
	var ust::string8& name= syntax_element.name.back();

	unsafe
	{
		var size_type mut linkage_name_size= 0s;
		var char8& linkage_name_start= LLVMGetValueName2( func_variable.llvm_function, linkage_name_size );

		var LLVMMetadataRef file= debug_info_.source_file_entries[ file_pos.GetFileIndex() ];

		auto di_function= LLVMDIBuilderCreateFunction(
			debug_info_.builder,
			file,
			name.front_unsafe(), name.size(),
			linkage_name_start, linkage_name_size,
			file,
			file_pos.GetLine(),
			CreateDIType( func_variable.t ),
			LLVMBool::False, // IsLocalToUnit
			LLVMBool::True, // IsDefinition
			file_pos.GetLine(),
			LLVMDIFlags(0),
			LLVMBool::False /* TODO - pass optimized flag*/ );

		LLVMSetSubprogram( func_variable.llvm_function, di_function );
	}
}

fn CodeBuilder::SetCurrentDebugLocation( this, FunctionContext& function_context, FilePos& file_pos )
{
	if( !build_debug_info_ )
	{
		return;
	}

	unsafe
	{
		auto debug_location= LLVMDIBuilderCreateDebugLocation(
			llvm_context_,
			file_pos.GetLine(),
			file_pos.GetColumn(),
			function_context.current_debug_info_scope,
			LLVMMetadataRef::Null );
		LLVMSetCurrentDebugLocation2( function_context.llvm_ir_builder, debug_location );
	}
}

fn CodeBuilder::DebugInfoStartBlock( this, FunctionContext &mut function_context, FilePos& file_pos ) : LLVMMetadataRef
{
	if( !build_debug_info_ )
	{
		return LLVMMetadataRef::Null;
	}

	auto prev= function_context.current_debug_info_scope;
	unsafe
	{
		function_context.current_debug_info_scope= LLVMDIBuilderCreateLexicalBlock(
			debug_info_.builder,
			function_context.current_debug_info_scope,
			debug_info_.source_file_entries[ file_pos.GetFileIndex() ],
			file_pos.GetLine(),
			file_pos.GetColumn() );
	}
	return prev;
}

fn CodeBuilder::DebugInfoEndBlock( this, FunctionContext &mut function_context, LLVMMetadataRef prev_block )
{
	if( !build_debug_info_ )
	{
		return;
	}

	unsafe
	{
		function_context.current_debug_info_scope= prev_block;
	}
}

fn CodeBuilder::CreateVariableDebugInfo( this, FunctionContext& function_context, NamesScopeVariable& variable, ust::string8& name, FilePos& file_pos )
{
	if( !build_debug_info_ )
	{
		return;
	}

	unsafe
	{
		auto di_variable= LLVMDIBuilderCreateAutoVariable(
			debug_info_.builder,
			function_context.current_debug_info_scope,
			name.front_unsafe(), name.size(),
			debug_info_.source_file_entries[ file_pos.GetFileIndex() ],
			file_pos.GetLine(),
			CreateDIType( variable.t ),
			LLVMBool::False, // AlwaysPreserve
			LLVMDIFlags(0),
			LLVMABIAlignmentOfType( data_layout_, variable.t.GetLLVMType() ) * 8u32 );

		auto debug_location= LLVMDIBuilderCreateDebugLocation(
			llvm_context_,
			file_pos.GetLine(),
			file_pos.GetColumn(),
			function_context.current_debug_info_scope,
			LLVMMetadataRef::Null );

		LLVMDIBuilderInsertDeclareAtEnd(
			debug_info_.builder,
			variable.llvm_value,
			di_variable,
			LLVMDIBuilderCreateExpression( debug_info_.builder, 0i64, 0s ),
			debug_location,
			LLVMGetInsertBlock(function_context.llvm_ir_builder) );
	}
}

fn CodeBuilder::CreateReferenceVariableDebugInfo( this, FunctionContext& function_context, NamesScopeVariable& variable, ust::string8& name, FilePos& file_pos )
{
	if( !build_debug_info_ )
	{
		return;
	}
	unsafe
	{
		auto pointer_llvm_type= LLVMPointerType( variable.t.GetLLVMType(), 0u );
		auto di_type= LLVMDIBuilderCreatePointerType(
			debug_info_.builder,
			CreateDIType( variable.t ),
			LLVMABISizeOfType( data_layout_, pointer_llvm_type ) * 8u64,
			LLVMABIAlignmentOfType( data_layout_, pointer_llvm_type ) * 8u32,
			0u, // address space
			"\0"c8, 0s );

		auto di_variable= LLVMDIBuilderCreateAutoVariable(
			debug_info_.builder,
			function_context.current_debug_info_scope,
			name.front_unsafe(), name.size(),
			debug_info_.source_file_entries[ file_pos.GetFileIndex() ],
			file_pos.GetLine(),
			di_type,
			LLVMBool::False, // AlwaysPreserve
			LLVMDIFlags(0),
			LLVMABIAlignmentOfType( data_layout_, pointer_llvm_type ) * 8u32 );

		// We needs address for reference, so, move it into stack variable.
		auto address_for_ref= LLVMBuildAlloca( function_context.alloca_ir_builder, pointer_llvm_type, "\0"[0] );
		LLVMBuildStore( function_context.llvm_ir_builder, variable.llvm_value, address_for_ref );

		auto debug_location= LLVMDIBuilderCreateDebugLocation(
			llvm_context_,
			file_pos.GetLine(),
			file_pos.GetColumn(),
			function_context.current_debug_info_scope,
			LLVMMetadataRef::Null );

		LLVMDIBuilderInsertDeclareAtEnd(
			debug_info_.builder,
			address_for_ref,
			di_variable,
			LLVMDIBuilderCreateExpression( debug_info_.builder, 0i64, 0s ),
			debug_location,
			LLVMGetInsertBlock(function_context.llvm_ir_builder) );
	}
}

fn CodeBuilder::CreateDIType( this, Type& t ) : LLVMMetadataRef
{
	if( IsTypeComplete( t ) )
	{
		if_var( &tt : t.GetFundamentalType() )
		{
			return CreateDITypeImpl( tt );
		}
		if_var( &tt : t.GetFunctionType() )
		{
			return CreateDITypeImpl( tt );
		}
		if_var( &tt : t.GetFunctionPointerType() )
		{
			return CreateDITypeImpl( tt );
		}
		if_var( &tt : t.GetArrayType() )
		{
			return CreateDITypeImpl( tt );
		}
		if_var( &tt : t.GetTupleType() )
		{
			return CreateDITypeImpl( tt );
		}
		if_var( &tt : t.GetClassType() )
		{
			return CreateDITypeImpl( tt );
		}
	}

	var ust::array_view_imut</char8/> stub_type_name= "_stub_debug_type";

	unsafe
	{
		return LLVMDIBuilderCreateBasicType(
			debug_info_.builder,
			stub_type_name.front(), stub_type_name.size(),
			8u,
			LLVMDWARFTypeEncoding::unsigned,
			0u );
	}
}

fn CodeBuilder::CreateDITypeImpl( this, FundamentalType& t ) : LLVMMetadataRef
{
	auto type_name= GetFundamentalTypeName( t.fundamental_type );

	var LLVMDWARFTypeEncoding mut encoding= LLVMDWARFTypeEncoding::unsigned;
	if( t.fundamental_type == U_FundamentalType::bool_ ){  encoding= LLVMDWARFTypeEncoding::boolean;        }
	else if( IsFloatingPoint( t.fundamental_type )     ){  encoding= LLVMDWARFTypeEncoding::float;          }
	else if( IsChar( t.fundamental_type )              ){  encoding= LLVMDWARFTypeEncoding::unsigned_char;  }
	else if( IsSignedInteger( t.fundamental_type )     ){  encoding= LLVMDWARFTypeEncoding::signed;         }
	else if( IsUnsignedInteger( t.fundamental_type )   ){  encoding= LLVMDWARFTypeEncoding::unsigned;       }

	unsafe
	{
		return LLVMDIBuilderCreateBasicType(
			debug_info_.builder,
			type_name.front_unsafe(), type_name.size(),
			u32( t.GetSize() * 8s ),
			encoding,
			0u );
	}
}

fn CodeBuilder::CreateDITypeImpl( this, ArrayType& t ) : LLVMMetadataRef
{
	unsafe
	{
		return LLVMDIBuilderCreateArrayType(
			debug_info_.builder,
			u32( LLVMABISizeOfType( data_layout_, t.llvm_type ) ) * 8u,
			LLVMABIAlignmentOfType( data_layout_, t.llvm_type ) * 8u,
			CreateDIType( t.element_type ),
			LLVMDIBuilderGetOrCreateSubrange( debug_info_.builder, 0i64, i64(t.element_count) ), 1u );
	}
}

fn CodeBuilder::CreateDITypeImpl( this, TupleType& t ) : LLVMMetadataRef
{
	var ust::vector</LLVMMetadataRef/> mut elements;

	auto di_file= debug_info_.source_file_entries.front();

	for( auto mut i= 0s; i < t.element_types.size(); ++i )
	{
		var ust::string8 name= ust::to_string8(i);
		var Type& element_type= t.element_types[i];
		var LLVMMetadataRef element_di_type= CreateDIType( element_type );
		var LLVMTypeRef llvm_element_type= element_type.GetLLVMType();

		unsafe
		{
			elements.push_back(
				LLVMDIBuilderCreateMemberType(
					debug_info_.builder,
					di_file,
					name.front_unsafe(), name.size(),
					di_file,
					0u, // Line
					LLVMABISizeOfType( data_layout_, llvm_element_type ) * 8u64,
					LLVMABIAlignmentOfType( data_layout_, llvm_element_type ) * 8u32,
					LLVMOffsetOfElement( data_layout_, t.llvm_type, u32(i) ) * 8u64,
					LLVMDIFlags(0),
					element_di_type) );
		}
	}

	var ust::string8 name= Type(t).ToString();

	unsafe
	{
		return LLVMDIBuilderCreateStructType(
			debug_info_.builder,
			di_file,
			name.front_unsafe(), name.size(),
			di_file,
			0u, // Line
			LLVMABISizeOfType( data_layout_, t.llvm_type ) * 8u64,
			LLVMABIAlignmentOfType( data_layout_, t.llvm_type ) * 8u32,
			LLVMDIFlags(0),
			LLVMMetadataRef::Null, // DerivedFrom
			cast_imut(elements).front_unsafe(), u32(elements.size()),
			0u, // RuntimeLang
			LLVMMetadataRef::Null,// VTableHolder
			"\0"c8, 0s);
	}
}

fn CodeBuilder::CreateDITypeImpl( this, FunctionType& t ) : LLVMMetadataRef
{
	var ust::vector</LLVMMetadataRef/> mut di_types;
	{
		auto mut di_type= CreateDIType( t.return_type );
		if( t.return_value_is_reference )
		{
			unsafe
			{
				auto pointer_llvm_type= LLVMPointerType( t.return_type.GetLLVMType(), 0u );
				di_type= LLVMDIBuilderCreatePointerType(
					debug_info_.builder,
					di_type,
					LLVMABISizeOfType( data_layout_, pointer_llvm_type ) * 8u64,
					LLVMABIAlignmentOfType( data_layout_, pointer_llvm_type ) * 8u32,
					0u, // address space
					"\0"c8, 0s );
			}
		}

		di_types.push_back( di_type );
	}

	foreach( &param : t.params )
	{
		auto mut di_type= CreateDIType( param.t );
		if( param.is_reference )
		{
			unsafe
			{
				auto pointer_llvm_type= LLVMPointerType( param.t.GetLLVMType(), 0u );
				di_type= LLVMDIBuilderCreatePointerType(
					debug_info_.builder,
					di_type,
					LLVMABISizeOfType( data_layout_, pointer_llvm_type ) * 8u64,
					LLVMABIAlignmentOfType( data_layout_, pointer_llvm_type ) * 8u32,
					0u, // address space
					"\0"c8, 0s );
			}
		}
		di_types.push_back( di_type );
	}

	unsafe
	{
		return LLVMDIBuilderCreateSubroutineType(
			debug_info_.builder,
			debug_info_.source_file_entries.front(),
			cast_imut(di_types).front_unsafe(), u32( di_types.size() ),
			LLVMDIFlags(0) );
	}
}

fn CodeBuilder::CreateDITypeImpl( this, FunctionPointerType& t ) : LLVMMetadataRef
{
	unsafe
	{
		return LLVMDIBuilderCreatePointerType(
			debug_info_.builder,
			CreateDITypeImpl( t.function_type ),
			LLVMABISizeOfType( data_layout_, t.llvm_type ) * 8u64,
			LLVMABIAlignmentOfType( data_layout_, t.llvm_type ) * 8u32,
			0u, // address space
			"\0"c8, 0s );
	}
}

fn CodeBuilder::CreateDITypeImpl( this, ClassTypePtr& t ) : LLVMMetadataRef
{
	auto class_lock= t.lock_imut();
	var ClassType& class_type= class_lock.get_ref();
	var FilePos mut file_pos;
	if( !class_type.syntax_element.empty() )
	{
		class_type.syntax_element.get_ref().file_pos;
	}

	auto di_file= debug_info_.source_file_entries[ file_pos.GetFileIndex() ];

	var ust::vector</LLVMMetadataRef/> mut elements;

	if( class_type.typeinfo_src_type_.empty() ) // Skip typeinfo struucts because of potential dependency loops.
	{
		auto class_members_lock= class_type.members.lock_imut();
		var NamesScope& class_members= class_members_lock.get_ref();
		foreach( &field_name : class_type.fields_order )
		{
			auto field_ptr= class_members.GetThisScopeValue(field_name).get_ref().Get</ClassField/>();
			auto field_lock= field_ptr.lock_imut();
			var ClassField& field= field_lock.get_ref();

			if( field.index == ~0s ){ continue; } // May be in case of error

			var LLVMMetadataRef mut element_di_type= CreateDIType( field.t );
			var LLVMTypeRef mut llvm_element_type= field.t.GetLLVMType();

			unsafe
			{
				if( field.is_reference )
				{
					auto pointer_llvm_type= LLVMPointerType( llvm_element_type, 0u );
					element_di_type= LLVMDIBuilderCreatePointerType(
						debug_info_.builder,
						element_di_type,
						LLVMABISizeOfType( data_layout_, pointer_llvm_type ) * 8u64,
						LLVMABIAlignmentOfType( data_layout_, pointer_llvm_type ) * 8u32,
						0u, // address space
						"\0"c8, 0s );
				}

				elements.push_back(
					LLVMDIBuilderCreateMemberType(
						debug_info_.builder,
						di_file,
						field_name.front_unsafe(), field_name.size(),
						di_file,
						0u, // Line
						LLVMABISizeOfType( data_layout_, llvm_element_type ) * 8u64,
						LLVMABIAlignmentOfType( data_layout_, llvm_element_type ) * 8u32,
						LLVMOffsetOfElement( data_layout_, class_type.llvm_type, u32(field.index) ) * 8u64,
						LLVMDIFlags(0),
						element_di_type) );
			}
		}

		foreach( &parent : class_type.parents )
		{
			var LLVMMetadataRef parent_di_type= CreateDIType( parent.class_ );
			var LLVMTypeRef llvm_element_type= parent.class_.lock_imut().get_ref().llvm_type;
			var ust::string8 name= Type(parent.class_).ToString();

			unsafe
			{
				elements.push_back(
					LLVMDIBuilderCreateMemberType(
						debug_info_.builder,
						di_file,
						name.front_unsafe(), name.size(),
						di_file,
						0u, // Line
						LLVMABISizeOfType( data_layout_, llvm_element_type ) * 8u64,
						LLVMABIAlignmentOfType( data_layout_, llvm_element_type ) * 8u32,
						LLVMOffsetOfElement( data_layout_, class_type.llvm_type, parent.field_number ) * 8u64,
						LLVMDIFlags(0),
						parent_di_type ) );
			}
		}
	}

	var ust::string8 name= Type(t).ToString();

	unsafe
	{
		return LLVMDIBuilderCreateStructType(
			debug_info_.builder,
			di_file,
			name.front_unsafe(), name.size(),
			di_file,
			file_pos.GetLine(),
			LLVMABISizeOfType( data_layout_, class_type.llvm_type ) * 8u64,
			LLVMABIAlignmentOfType( data_layout_, class_type.llvm_type ) * 8u32,
			LLVMDIFlags(0),
			LLVMMetadataRef::Null, // DerivedFrom
			cast_imut(elements).front_unsafe(), u32(elements.size()),
			0u, // RuntimeLang
			LLVMMetadataRef::Null,// VTableHolder
			"\0"c8, 0s);
	}
}

fn CodeBuilder::CreateDITypeImpl( this, EnumTypePtr& t ) : LLVMMetadataRef
{
	// TODO
	return LLVMMetadataRef::Null;
}

} // namespace U1
