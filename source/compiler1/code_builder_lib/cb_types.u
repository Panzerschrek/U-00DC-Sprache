import "../lex_synt_lib/keywords.uh"
import "error_reporting.uh"
import "code_builder.uh"

namespace U
{

fn CodeBuilder::PrepareType( mut this, NamesScopePtr& names_scope, FunctionContext &mut function_context, Synt::TypeName& type_name ) : Type
{
	variant_visit( &t : type_name )
	{
		return PrepareType( names_scope, function_context, t );
	}

	halt;
}

fn CodeBuilder::PrepareType( mut this, NamesScopePtr& names_scope, FunctionContext &mut function_context, Synt::ComplexName& named_type ) : Type
{
	var Value resolved_value= ResolveValue( names_scope, function_context, named_type );
	if_var( & t : resolved_value.get</Type/>() )
	{
		return t;
	}

	REPORT_ERROR( NameIsNotTypeName, names_scope, named_type.file_pos, named_type.name )
	return Type( FundamentalType( U_FundamentalType::InvalidType, fundamental_llvm_types_.invalid_type_ ) );
}

fn CodeBuilder::PrepareType( mut this, NamesScopePtr& names_scope, FunctionContext &mut function_context, ust::box</Synt::ArrayType/>& array_type ) : Type
{
	return PrepareType( names_scope, function_context, array_type.get_ref() );
}

fn CodeBuilder::PrepareType( mut this, NamesScopePtr& names_scope, FunctionContext &mut function_context, Synt::ArrayType& array_type ) : Type
{
	var ArrayType mut out_array_type
	{
		.element_type( PrepareType( names_scope, function_context, array_type.element_type ) )
	};

	var Variable mut index_variable;
	{
		var VariablesFrameHolder temp_variables_frame_hodler(function_context);
		auto& mut function_context= temp_variables_frame_hodler.GetFunctionContext();

		auto instructions_state= SaveInstructionsState( function_context );
		index_variable= BuildExpressionCodeEnsureVariable( names_scope, function_context, array_type.element_count );
		RestoreInstructionsState( function_context, instructions_state );
	}

	if( index_variable.constexpr_value != LLVMValueRef::Null )
	{
		if( !index_variable.t.GetFundamentalType().empty() && IsInteger( index_variable.t.GetFundamentalType().get_ref().fundamental_type ) )
		{
			if( IsSignedInteger( index_variable.t.GetFundamentalType().get_ref().fundamental_type ) )
			{
				auto mut s= 0i64;
				unsafe{  s= LLVMConstIntGetSExtValue( index_variable.constexpr_value );  }
				if( s >= 0i64 )
				{
					out_array_type.element_count= u64(s);
				}
				else
				{
					out_array_type.element_count= 0u64;
					REPORT_ERROR( ArraySizeIsNegative, names_scope,  array_type.file_pos  )
				}
			}
			else
			{
				unsafe{  out_array_type.element_count= LLVMConstIntGetZExtValue( index_variable.constexpr_value );  }
			}
		}
		else
		{
			REPORT_ERROR( ArraySizeIsNotInteger, names_scope, array_type.file_pos )
		}
	}
	else
	{
		REPORT_ERROR( ExpectedConstantExpression, names_scope, array_type.file_pos )
	}

	unsafe{ out_array_type.llvm_type= LLVMArrayType( out_array_type.element_type.GetLLVMType(), u32(out_array_type.element_count) ); }

	return Type( move(out_array_type) );
}

fn CodeBuilder::PrepareType( mut this, NamesScopePtr& names_scope, FunctionContext &mut function_context, ust::box</Synt::TupleType/>& tuple_type ) : Type
{
	return PrepareType( names_scope, function_context, tuple_type.get_ref() );
}

fn CodeBuilder::PrepareType( mut this, NamesScopePtr& names_scope, FunctionContext &mut function_context, Synt::TupleType& tuple_type ) : Type
{
	var TupleType mut out_tuple_type;

	var ust::vector</ LLVMTypeRef /> mut element_llvm_types;
	foreach( & element : tuple_type.elements )
	{
		var Type mut t= PrepareType( names_scope, function_context, element );
		element_llvm_types.push_back( t.GetLLVMType() );
		out_tuple_type.element_types.push_back( move(t) );
	}

	unsafe
	{
		out_tuple_type.llvm_type= LLVMStructTypeInContext( llvm_context_, cast_imut(element_llvm_types).front_unsafe(), u32(element_llvm_types.size()), LLVMBool::False );
	}

	return move(out_tuple_type);
}

fn CodeBuilder::PrepareType( mut this, NamesScopePtr& names_scope, FunctionContext &mut function_context, ust::box</Synt::FunctionType/>& function_type_ptr ) : Type
{
	return PrepareType( names_scope, function_context, function_type_ptr.get_ref() );
}

fn CodeBuilder::PrepareType( mut this, NamesScopePtr& names_scope, FunctionContext &mut function_context, Synt::FunctionType& function_type ) : Type
{
	// For function type inside type name generate function pointer type.
	var FunctionPointerType mut function_pointer_type
	{
		.function_type= PrepareFunctionType( names_scope, function_context, function_type, ust::shared_ptr_nullable_mut</ClassType/>() )
	};

	unsafe{  function_pointer_type.llvm_type= LLVMPointerType( function_pointer_type.function_type.llvm_type, 0u );  }

	return move(function_pointer_type);
}

fn CodeBuilder::PrepareFunctionType( mut this, NamesScopePtr& names_scope, FunctionContext &mut function_context, Synt::FunctionType& function_type, ust::shared_ptr_nullable_mut</ClassType/>& class_type ) : FunctionType
{
	var FunctionType mut res
	{
		.return_type(
			select(
				function_type.return_type.empty()
					? Type( FundamentalType( U_FundamentalType::void_, fundamental_llvm_types_.void_for_ret_ ) )
					: PrepareType( names_scope, function_context, function_type.return_type.get_ref() ) ) )
		.return_value_is_reference= function_type.return_value_reference_modifier == Synt::ReferenceModifier::Reference,
		.return_value_is_mutable= function_type.return_value_mutability_modifier == Synt::MutabilityModifier::Mutable,
		.is_unsafe= function_type.is_unsafe,
	};

	foreach( &param : function_type.params )
	{
		var FunctionType::Param mut out_param
		{
			.t( invalid_type_ ),
			.is_reference= param.reference_modifier == Synt::ReferenceModifier::Reference,
			.is_mutable= param.mutability_modifier == Synt::MutabilityModifier::Mutable,
		};

		if( param.name == KeywordToString( Keyword::this_ ) )
		{
			// TODO - make "this" mutable in constructors and destructors.
			if( class_type.empty() )
			{
				REPORT_ERROR( ThisInNonclassFunction, names_scope, param.file_pos, ust::string8("TODO - func name") )
			}
			else if( ust::ref_to_int(param) != ust::ref_to_int(function_type.params.front() ) )
			{
				REPORT_ERROR( UsingKeywordAsName, names_scope, param.file_pos )
			}
			else
			{
				auto mut param_ok= false;
				if_var( &param_type_complex_name : param.t.get</Synt::ComplexName/>() )
				{
					param_ok= param_type_complex_name.name.empty() && param_type_complex_name.tail.empty();
				}
				if( !param_ok )
				{
					REPORT_ERROR( UsingKeywordAsName, names_scope, param.file_pos )
				}

				out_param.is_reference= true;
				out_param.t= ust::to_non_nullable( class_type );
			}
		}
		else
		{
			if( IsKeyword( param.name ) )
			{
				REPORT_ERROR( UsingKeywordAsName, names_scope, param.file_pos )
			}

			out_param.t= PrepareType( names_scope, function_context, param.t );
		}

		res.params.push_back( move(out_param) );
	}

	var ust::vector</LLVMTypeRef/> mut llvm_param_types;

	// s_ret
	auto is_s_ret= !res.return_value_is_reference && ( !res.return_type.GetClassType().empty() || !res.return_type.GetTupleType().empty() );
	if( is_s_ret )
	{
		unsafe{  llvm_param_types.push_back( LLVMPointerType( res.return_type.GetLLVMType(), 0u ) );  }
	}

	foreach( &param : res.params )
	{
		auto mut llvm_type= param.t.GetLLVMType();
		if( param.is_reference )
		{
			unsafe{  llvm_type= LLVMPointerType( llvm_type, 0u );  }
		}
		else
		{
			// Pass aggregate types by-reference.
			if( !param.t.GetClassType().empty() || !param.t.GetTupleType().empty() )
			{
				unsafe{  llvm_type= LLVMPointerType( llvm_type, 0u );  }
			}
		}
		llvm_param_types.push_back( llvm_type );
	}

	unsafe
	{
		auto mut ret_llvm_type= res.return_type.GetLLVMType();
		if( res.return_value_is_reference )
		{
			ret_llvm_type= LLVMPointerType( ret_llvm_type, 0u );
		}
		else if( is_s_ret )
		{
			ret_llvm_type= fundamental_llvm_types_.void_for_ret_;
		}

		res.llvm_type= LLVMFunctionType( ret_llvm_type, cast_imut(llvm_param_types).front_unsafe(), u32(llvm_param_types.size()), LLVMBool::False );
	}

	return move(res);
}

} // namespace U
