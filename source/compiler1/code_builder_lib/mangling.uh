import "/string.u"
import "structs_combined.uh"

namespace U1
{


class ManglerState
{
public:
	fn Push( mut this, char8 c );
	fn Push( mut this, ust::array_view_imut</char8/> name );
	fn PushLengthPrefixed( mut this, ust::array_view_imut</char8/> name );
	fn PushDecimalNumber( mut this, u64 n );

	fn TakeResult( mut this ) : ust::string8;

public:
	class NodeHolder
	{
	public:
		fn constructor( mut this'a', ManglerState &'b mut state ) ' a <- b '
			( state_(state), start_= state_.GetCurrentPos(), compressed_start_= state_.GetCurrentCompressedPos() )
		{}

		fn destructor()
		{
			state_.FinalizePart( start_, compressed_start_ );
		}

		fn GetManglerState( this'x' ) : ManglerState &'x mut
		{
			return state_;
		}

	private:
		ManglerState &mut state_;
		size_type imut start_;
		size_type imut compressed_start_;
	}

private:
	fn GetCurrentPos( this ) : size_type;
	fn GetCurrentCompressedPos( this ) : size_type;
	fn FinalizePart( mut this, size_type start, size_type compressed_start );

private:
	type LenType= u16;
	struct Substitution
	{
		LenType start;
		LenType size;
	}

private:
	ust::vector</Substitution/> substitutions_;
	ust::string8 result_full_;
	ust::string8 result_compressed_;
}

class Mangler
{
public:
	// All methods return null-terminated string.

	fn MangleFunction(
		mut this,
		NamesScope& parent_scope,
		ust::string8& name,
		FunctionType& function_type ) : ust::string8;

	fn MangleFunction(
		mut this,
		NamesScope& parent_scope,
		ust::string8& name,
		FunctionType& function_type,
		TemplateArgsFinished& template_args ) : ust::string8;

	fn MangleGlobalVariable( mut this, NamesScope& parent_scope, ust::string8& variable_name ) : ust::string8;
	fn MangleType( mut this, Type& t ) : ust::string8;
	fn MangleTemplateArgs( mut this, TemplateArgsFinished& template_args ) : ust::string8;
	fn MangleVirtualTable( mut this, Type& t ) : ust::string8;

private:
	fn MangleFunctionImpl(
		mut this,
		NamesScope& parent_scope,
		ust::string8& name,
		FunctionType& function_type,
		ust::optional_ref_imut</TemplateArgsFinished/> template_args ) : ust::string8;

private:
	ManglerState state_;
}

} // namespace U1
