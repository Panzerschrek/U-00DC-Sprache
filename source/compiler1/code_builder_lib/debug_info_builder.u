import "/string_conversions.u"
import "debug_info_builder.uh"

namespace U1
{

fn DebugInfoBuilder::constructor(
	LLVMContextRef llvm_context,
	LLVMTargetDataRef data_layout,
	SourceGraph& source_graph,
	LLVMModuleRef mod,
	bool build_debug_info )
	( llvm_context_(llvm_context), data_layout_(data_layout) )
{
	if( !build_debug_info )
	{
		return;
	}

	builder_= unsafe( LLVMCreateDIBuilder( mod ) );

	foreach( & node : source_graph.nodes )
	{
		source_file_entries_.push_back(
			unsafe( LLVMDIBuilderCreateFile(
				builder_,
				cast_mut(node.file_path_normalized).data(), node.file_path_normalized.size(),
				ust::nullptr</char8/>(), 0s ) ) );
	}

	// TODO - fix this. LLVM C interface doesn't provide any way to specify custom language, so, use language id for "C".
	var LLVMDWARFSourceLanguage c_dwarf_language_id= 1u;

	compile_unit_=
		unsafe( LLVMDIBuilderCreateCompileUnit(
			builder_,
			c_dwarf_language_id,
			source_file_entries_.front(),
			ust::nullptr</char8/>(), 0s, // TODO - set producer
			LLVMBool::False, // TODO - pass "optimized' flag
			ust::nullptr</char8/>(), 0s, // Flags
			0u, // Runtime version
			ust::nullptr</char8/>(), 0s, // SplitName
			LLVMDWARFEmissionKind::Full,
			0u, // DWOId
			LLVMBool::True, // SplitDebugInlining
			LLVMBool::False, // DebugInfoForProfiling
			ust::nullptr</char8/>(), 0s, // SysRoot
			ust::nullptr</char8/>(), 0s  // SDK
		) );

	var ust::string_view8 stub_type_name= "_stub_debug_type";
	stub_type_= unsafe( LLVMDIBuilderCreateBasicType(
		builder_,
		stub_type_name.data(), stub_type_name.size(),
		8u64,
		LLVMDWARFTypeEncoding::unsigned,
		0u ) );
}

fn DebugInfoBuilder::destructor()
{
	if( builder_ == LLVMDIBuilderRef::Null )
	{
		return;
	}

	// Build full debug info for classes, because at this moment all classes should be complete.
	// Use separate list since we can't iterate over classes_di_types_ and adding new elements simultaniously.
	// Use counter-based loop, since this container may be modified during iteration.
	for( var size_type mut i= 0s; i < classes_order_.size(); ++i )
	{
		auto class_type= classes_order_[i];
		BuildClassTypeFullDebugInfo( class_type );
	}

	unsafe
	{
		LLVMDIBuilderFinalize( builder_ );
		LLVMDisposeDIBuilder( builder_ );
	}
}

fn DebugInfoBuilder::CreateFunctionInfo( mut this, FunctionVariable& func_variable )
{
	if( builder_ == LLVMDIBuilderRef::Null )
	{
		return;
	}

	var Synt::Function& syntax_element= func_variable.body_syntax_element.try_deref();
	var SrcLoc& src_loc= syntax_element.src_loc;
	var ust::string8& name= syntax_element.name.back();

	with( &lazy_function : func_variable.llvm_function.lock_imut().deref() )
	{
		var LLVMMetadataRef file= source_file_entries_[ src_loc.GetFileIndex() ];

		var ust::string8& linkage_name= lazy_function.name_mangled;

		unsafe
		{
			auto di_function= LLVMDIBuilderCreateFunction(
				builder_,
				file,
				cast_mut(name).data(), name.size(),
				cast_mut(linkage_name).data(), linkage_name.size(),
				file,
				src_loc.GetLine(),
				CreateFunctionDIType( func_variable.t ),
				LLVMBool::False, // IsLocalToUnit
				LLVMBool::True, // IsDefinition
				src_loc.GetLine(),
				LLVMDIFlags(0),
				LLVMBool::False /* TODO - pass optimized flag*/ );

			LLVMSetSubprogram( lazy_function.function, di_function );
		}
	}
}

fn DebugInfoBuilder::SetCurrentLocation( mut this, FunctionContext& function_context, SrcLoc& src_loc )
{
	if( builder_ == LLVMDIBuilderRef::Null )
	{
		return;
	}

	unsafe
	{
		auto debug_location= LLVMDIBuilderCreateDebugLocation(
			llvm_context_,
			src_loc.GetLine(),
			src_loc.GetColumn(),
			function_context.current_debug_info_scope,
			LLVMMetadataRef::Null );
		LLVMSetCurrentDebugLocation2( function_context.llvm_ir_builder, debug_location );
	}
}

fn DebugInfoBuilder::StartBlock( mut this, FunctionContext &mut function_context, SrcLoc& src_loc ) : LLVMMetadataRef
{
	if( builder_ == LLVMDIBuilderRef::Null )
	{
		return LLVMMetadataRef::Null;
	}

	auto prev= function_context.current_debug_info_scope;
	function_context.current_debug_info_scope= unsafe( LLVMDIBuilderCreateLexicalBlock(
		builder_,
		function_context.current_debug_info_scope,
		source_file_entries_[ src_loc.GetFileIndex() ],
		src_loc.GetLine(),
		src_loc.GetColumn() ) );

	return prev;
}

fn DebugInfoBuilder::EndBlock( mut this, FunctionContext &mut function_context, LLVMMetadataRef prev_block )
{
	if( builder_ == LLVMDIBuilderRef::Null )
	{
		return;
	}

	function_context.current_debug_info_scope= prev_block;
}

fn DebugInfoBuilder::CreateVariableInfo( mut this, FunctionContext& function_context, Variable& variable, ust::string8& name, SrcLoc& src_loc )
{
	if( builder_ == LLVMDIBuilderRef::Null )
	{
		return;
	}

	unsafe
	{
		auto di_variable= LLVMDIBuilderCreateAutoVariable(
			builder_,
			function_context.current_debug_info_scope,
			cast_mut(name).data(), name.size(),
			source_file_entries_[ src_loc.GetFileIndex() ],
			src_loc.GetLine(),
			CreateDIType( variable.t ),
			LLVMBool::False, // AlwaysPreserve
			LLVMDIFlags(0),
			LLVMABIAlignmentOfType( data_layout_, variable.t.GetLLVMType() ) * 8u32 );

		auto debug_location= LLVMDIBuilderCreateDebugLocation(
			llvm_context_,
			src_loc.GetLine(),
			src_loc.GetColumn(),
			function_context.current_debug_info_scope,
			LLVMMetadataRef::Null );

		LLVMDIBuilderInsertDeclareAtEnd(
			builder_,
			variable.llvm_value,
			di_variable,
			LLVMDIBuilderCreateExpression( builder_, 0i64, 0s ),
			debug_location,
			LLVMGetInsertBlock(function_context.llvm_ir_builder) );
	}
}

fn DebugInfoBuilder::CreateReferenceVariableInfo( mut this, FunctionContext& function_context, Variable& variable, ust::string8& name, SrcLoc& src_loc )
{
	if( builder_ == LLVMDIBuilderRef::Null )
	{
		return;
	}
	unsafe
	{
		auto pointer_llvm_type= LLVMPointerType( variable.t.GetLLVMType(), 0u );
		auto di_type= LLVMDIBuilderCreatePointerType(
			builder_,
			CreateDIType( variable.t ),
			LLVMABISizeOfType( data_layout_, pointer_llvm_type ) * 8u64,
			LLVMABIAlignmentOfType( data_layout_, pointer_llvm_type ) * 8u32,
			0u, // address space
			ust::nullptr</char8/>(), 0s );

		auto di_variable= LLVMDIBuilderCreateAutoVariable(
			builder_,
			function_context.current_debug_info_scope,
			cast_mut(name).data(), name.size(),
			source_file_entries_[ src_loc.GetFileIndex() ],
			src_loc.GetLine(),
			di_type,
			LLVMBool::False, // AlwaysPreserve
			LLVMDIFlags(0),
			LLVMABIAlignmentOfType( data_layout_, pointer_llvm_type ) * 8u32 );

		// We needs address for reference, so, move it into stack variable.
		auto address_for_ref= LLVMBuildAlloca( function_context.alloca_ir_builder, pointer_llvm_type, g_null_string );
		LLVMBuildStore( function_context.llvm_ir_builder, variable.llvm_value, address_for_ref );

		auto debug_location= LLVMDIBuilderCreateDebugLocation(
			llvm_context_,
			src_loc.GetLine(),
			src_loc.GetColumn(),
			function_context.current_debug_info_scope,
			LLVMMetadataRef::Null );

		LLVMDIBuilderInsertDeclareAtEnd(
			builder_,
			address_for_ref,
			di_variable,
			LLVMDIBuilderCreateExpression( builder_, 0i64, 0s ),
			debug_location,
			LLVMGetInsertBlock(function_context.llvm_ir_builder) );
	}
}

fn DebugInfoBuilder::CreateDIType( mut this, Type& t ) : LLVMMetadataRef
{
	if_var( &tt : t.GetFundamentalType() )
	{
		return CreateDITypeImpl( tt );
	}
	if_var( &tt : t.GetRawPointerType() )
	{
		return CreateDITypeImpl( tt );
	}
	if_var( &tt : t.GetFunctionPointerType() )
	{
		return CreateDITypeImpl( tt );
	}
	if_var( &tt : t.GetArrayType() )
	{
		return CreateDITypeImpl( tt );
	}
	if_var( &tt : t.GetTupleType() )
	{
		return CreateDITypeImpl( tt );
	}
	if_var( &tt : t.GetClassType() )
	{
		return CreateDITypeImpl( tt );
	}
	if_var( &tt : t.GetEnumType() )
	{
		return CreateDITypeImpl( tt );
	}

	return stub_type_;
}

fn DebugInfoBuilder::CreateDITypeImpl( mut this, FundamentalType& t ) : LLVMMetadataRef
{
	auto type_name= GetFundamentalTypeName( t.fundamental_type );

	if( t.fundamental_type == U_FundamentalType::void_ )
	{
		// Internal representation of void type is llvm struct with zero elements.
		auto di_file= source_file_entries_.front();

		return unsafe( LLVMDIBuilderCreateStructType(
			builder_,
			di_file,
			type_name.data(), type_name.size(),
			di_file,
			0u, // Line
			LLVMABISizeOfType( data_layout_, t.llvm_type ) * 8u64,
			LLVMABIAlignmentOfType( data_layout_, t.llvm_type ) * 8u32,
			LLVMDIFlags(0),
			LLVMMetadataRef::Null, // DerivedFrom
			ust::nullptr</LLVMMetadataRef/>(), 0u, // elements
			0u, // RuntimeLang
			LLVMMetadataRef::Null,// VTableHolder
			ust::nullptr</char8/>(), 0s ) );
	}

	var LLVMDWARFTypeEncoding mut encoding= LLVMDWARFTypeEncoding::unsigned;
	if( t.fundamental_type == U_FundamentalType::bool_ ){  encoding= LLVMDWARFTypeEncoding::boolean;        }
	else if( IsFloatingPoint( t.fundamental_type )     ){  encoding= LLVMDWARFTypeEncoding::float;          }
	else if( IsChar( t.fundamental_type )              ){  encoding= LLVMDWARFTypeEncoding::unsigned_char;  }
	else if( IsByte( t.fundamental_type )              ){  encoding= LLVMDWARFTypeEncoding::unsigned;       }
	else if( IsSignedInteger( t.fundamental_type )     ){  encoding= LLVMDWARFTypeEncoding::signed;         }
	else if( IsUnsignedInteger( t.fundamental_type )   ){  encoding= LLVMDWARFTypeEncoding::unsigned;       }

	return unsafe( LLVMDIBuilderCreateBasicType(
		builder_,
		type_name.data(), type_name.size(),
		u64( t.GetSize() * 8u ),
		encoding,
		0u ) );
}

fn DebugInfoBuilder::CreateDITypeImpl( mut this, ArrayType& t ) : LLVMMetadataRef
{
	if( unsafe( LLVMTypeIsSized( t.llvm_type ) ) == LLVMBool::False )
	{
		return stub_type_;
	}

	unsafe
	{
		auto mut subrange= LLVMDIBuilderGetOrCreateSubrange( builder_, 0i64, i64(t.element_count) );
		return LLVMDIBuilderCreateArrayType(
			builder_,
			LLVMABISizeOfType( data_layout_, t.llvm_type ) * 8u64,
			LLVMABIAlignmentOfType( data_layout_, t.llvm_type ) * 8u,
			CreateDIType( t.element_type ),
			$<(subrange), 1u );
	}
}

fn DebugInfoBuilder::CreateDITypeImpl( mut this, TupleType& t ) : LLVMMetadataRef
{
	if( unsafe( LLVMTypeIsSized( t.llvm_type ) ) == LLVMBool::False )
	{
		return stub_type_;
	}

	var ust::vector</LLVMMetadataRef/> mut elements;

	auto di_file= source_file_entries_.front();

	for( auto mut i= 0s; i < t.element_types.size(); ++i )
	{
		var ust::string8 name= ust::to_string8(i);
		var Type& element_type= t.element_types[i];
		var LLVMMetadataRef element_di_type= CreateDIType( element_type );
		var LLVMTypeRef llvm_element_type= element_type.GetLLVMType();

		elements.push_back(
			unsafe( LLVMDIBuilderCreateMemberType(
				builder_,
				di_file,
				cast_mut(name).data(), name.size(),
				di_file,
				0u, // Line
				LLVMABISizeOfType( data_layout_, llvm_element_type ) * 8u64,
				LLVMABIAlignmentOfType( data_layout_, llvm_element_type ) * 8u32,
				LLVMOffsetOfElement( data_layout_, t.llvm_type, u32(i) ) * 8u64,
				LLVMDIFlags(0),
				element_di_type) ) );
	}

	var ust::string8 name= Type(t).ToString();

	return unsafe( LLVMDIBuilderCreateStructType(
		builder_,
		di_file,
		cast_mut(name).data(), name.size(),
		di_file,
		0u, // Line
		LLVMABISizeOfType( data_layout_, t.llvm_type ) * 8u64,
		LLVMABIAlignmentOfType( data_layout_, t.llvm_type ) * 8u32,
		LLVMDIFlags(0),
		LLVMMetadataRef::Null, // DerivedFrom
		elements.data(), u32(elements.size()),
		0u, // RuntimeLang
		LLVMMetadataRef::Null,// VTableHolder
		ust::nullptr</char8/>(), 0s ) );
}

fn DebugInfoBuilder::CreateDITypeImpl( mut this, RawPointerType& t ) : LLVMMetadataRef
{
	return unsafe( LLVMDIBuilderCreatePointerType(
		builder_,
		CreateDIType( t.element_type ),
		LLVMABISizeOfType( data_layout_, t.llvm_type ) * 8u64,
		LLVMABIAlignmentOfType( data_layout_, t.llvm_type ) * 8u32,
		0u, // address space
		ust::nullptr</char8/>(), 0s ) );
}

fn DebugInfoBuilder::CreateDITypeImpl( mut this, FunctionPointerType& t ) : LLVMMetadataRef
{
	return unsafe( LLVMDIBuilderCreatePointerType(
		builder_,
		CreateFunctionDIType( t.function_type ),
		LLVMABISizeOfType( data_layout_, t.llvm_type ) * 8u64,
		LLVMABIAlignmentOfType( data_layout_, t.llvm_type ) * 8u32,
		0u, // address space
		ust::nullptr</char8/>(), 0s ) );
}

fn DebugInfoBuilder::CreateDITypeImpl( mut this, ClassTypePtr& t ) : LLVMMetadataRef
{
	if_var( cache_value : classes_di_types_.find( t ) )
	{
		return cache_value;
	}

	auto class_lock= t.lock_imut();
	var ClassType& class_type= class_lock.deref();
	var SrcLoc mut src_loc;
	if_var( &syntax_element : class_type.syntax_element )
	{
		src_loc= syntax_element.src_loc;
	}

	auto di_file= source_file_entries_[ src_loc.GetFileIndex() ];

	var ust::string8 name= Type(t).ToString();

	auto forward_declaration=
		unsafe( LLVMDIBuilderCreateReplaceableCompositeType(
			builder_,
			2u, // DW_TAG_class_type
			cast_mut(name).data(), name.size(),
			di_file,
			di_file,
			src_loc.GetLine(),
			0u, // RuntimeLang
			0u64, // Size
			0u, // Alignment
			0u, // Flags
			ust::nullptr</char8/>(), 0s ) );

	// Insert stub first to prevent loops.
	classes_di_types_.insert( t, forward_declaration );
	classes_order_.push_back( t );
	return forward_declaration;
}

fn DebugInfoBuilder::CreateDITypeImpl( mut this, EnumTypePtr& t ) : LLVMMetadataRef
{
	if_var( cache_value : enums_di_types_.find( t ) )
	{
		return cache_value;
	}

	auto enum_lock= t.lock_imut();
	var Enum& enum_= enum_lock.deref();

	var bool is_unsigned= IsUnsignedInteger( enum_.underlaying_type.fundamental_type );

	var ust::vector</LLVMMetadataRef/> mut elements;
	foreach( &element_pair : enum_.elements )
	{
		var ust::string8& name= element_pair.key();
		var LLVMValueRef constexpr_value= element_pair.value().constexpr_value;

		elements.push_back(
			unsafe( LLVMDIBuilderCreateEnumerator(
				builder_,
				cast_mut(name).data(), name.size(),
				select( is_unsigned ? i64(LLVMConstIntGetZExtValue(constexpr_value)) : LLVMConstIntGetSExtValue(constexpr_value) ),
				select( is_unsigned ? LLVMBool::True : LLVMBool::False ) ) ) );
	}

	var SrcLoc& src_loc= enum_.syntax_element.deref().src_loc;
	auto di_file= source_file_entries_[ src_loc.GetFileIndex() ];

	var ust::string8 name= Type(t).ToString();

	auto di_type= unsafe( LLVMDIBuilderCreateEnumerationType(
		builder_,
		di_file,
		cast_mut(name).data(), name.size(),
		di_file,
		src_loc.GetLine(),
		LLVMABISizeOfType( data_layout_, enum_.underlaying_type.llvm_type ) * 8u64,
		LLVMABIAlignmentOfType( data_layout_, enum_.underlaying_type.llvm_type ) * 8u32,
		elements.data(), u32(elements.size()),
		CreateDITypeImpl(enum_.underlaying_type) ) );

	enums_di_types_.insert( t, di_type );
	return di_type;
}

fn DebugInfoBuilder::CreateFunctionDIType( mut this, FunctionType& t ) : LLVMMetadataRef
{
	var ust::vector</LLVMMetadataRef/> mut di_types;
	{
		auto mut di_type= CreateDIType( t.return_type );
		if( t.return_value_type != ValueType::Value )
		{
			unsafe
			{
				auto pointer_llvm_type= LLVMPointerType( t.return_type.GetLLVMType(), 0u );
				di_type= LLVMDIBuilderCreatePointerType(
					builder_,
					di_type,
					LLVMABISizeOfType( data_layout_, pointer_llvm_type ) * 8u64,
					LLVMABIAlignmentOfType( data_layout_, pointer_llvm_type ) * 8u32,
					0u, // address space
					ust::nullptr</char8/>(), 0s );
			}
		}

		di_types.push_back( di_type );
	}

	foreach( &param : t.params )
	{
		auto mut di_type= CreateDIType( param.t );
		if( param.value_type != ValueType::Value )
		{
			unsafe
			{
				auto pointer_llvm_type= LLVMPointerType( param.t.GetLLVMType(), 0u );
				di_type= LLVMDIBuilderCreatePointerType(
					builder_,
					di_type,
					LLVMABISizeOfType( data_layout_, pointer_llvm_type ) * 8u64,
					LLVMABIAlignmentOfType( data_layout_, pointer_llvm_type ) * 8u32,
					0u, // address space
					ust::nullptr</char8/>(), 0s );
			}
		}
		di_types.push_back( di_type );
	}

	return unsafe( LLVMDIBuilderCreateSubroutineType(
		builder_,
		source_file_entries_.front(),
		di_types.data(), u32( di_types.size() ),
		LLVMDIFlags(0) ) );
}

fn DebugInfoBuilder::BuildClassTypeFullDebugInfo( mut this, ClassTypePtr& class_ )
{
	// Create stub first.
	// It is important, since we need to create cache value for this class, even if it was not referenced previously,
	// because we need to build proper debug info for it in case if it will be requested by BuildClassTypeFullDebugInfo call for other class,
	// (that for example contains this class).
	CreateDITypeImpl( class_ );

	auto class_lock= class_.lock_imut();
	var ClassType& class_type= class_lock.deref();
	var SrcLoc mut src_loc;
	if_var( &syntax_element : class_type.syntax_element )
	{
		src_loc= syntax_element.src_loc;
	}

	auto di_file= source_file_entries_[ src_loc.GetFileIndex() ];

	var ust::vector</LLVMMetadataRef/> mut elements;
	var u64 mut size_in_bits(1);
	var u32 mut alignment_in_bits(1);

	if( class_type.is_complete && // Build proper info for complete clases.
		class_type.generated_class_data.get</ ClassType::TypeinfoClassDescription />().empty() ) // Skip typeinfo structs because of potential dependency loops.
	{
		foreach( &field_pair : class_type.fields_order )
		{
			var ust::string8& field_name= field_pair[0];
			auto field_lock= field_pair[1].lock_imut();
			var ClassField& field= field_lock.deref();

			if( field.index == ~0u ){ continue; } // May be in case of error

			var LLVMMetadataRef mut element_di_type= CreateDIType( field.t );
			var LLVMTypeRef mut llvm_element_type= field.t.GetLLVMType();

			if( field.is_reference )
			{
				auto pointer_llvm_type= unsafe( LLVMPointerType( llvm_element_type, 0u ) );
				element_di_type= unsafe( LLVMDIBuilderCreatePointerType(
					builder_,
					element_di_type,
					LLVMABISizeOfType( data_layout_, pointer_llvm_type ) * 8u64,
					LLVMABIAlignmentOfType( data_layout_, pointer_llvm_type ) * 8u32,
					0u, // address space
					ust::nullptr</char8/>(), 0s ) );
			}

			elements.push_back(
				unsafe( LLVMDIBuilderCreateMemberType(
					builder_,
					di_file,
					cast_mut(field_name).data(), field_name.size(),
					di_file,
					0u, // Line
					LLVMABISizeOfType( data_layout_, llvm_element_type ) * 8u64,
					LLVMABIAlignmentOfType( data_layout_, llvm_element_type ) * 8u32,
					LLVMOffsetOfElement( data_layout_, class_type.llvm_type, u32(field.index) ) * 8u64,
					LLVMDIFlags(0),
					element_di_type) ) );
		}

		foreach( &parent : class_type.parents )
		{
			var LLVMMetadataRef parent_di_type= CreateDIType( parent.class_ );
			var LLVMTypeRef llvm_element_type= parent.class_.lock_imut().deref().llvm_type;
			var ust::string8 name= Type(parent.class_).ToString();

			elements.push_back(
				unsafe( LLVMDIBuilderCreateMemberType(
					builder_,
					di_file,
					cast_mut(name).data(), name.size(),
					di_file,
					0u, // Line
					LLVMABISizeOfType( data_layout_, llvm_element_type ) * 8u64,
					LLVMABIAlignmentOfType( data_layout_, llvm_element_type ) * 8u32,
					LLVMOffsetOfElement( data_layout_, class_type.llvm_type, parent.field_number ) * 8u64,
					LLVMDIFlags(0),
					parent_di_type ) ) );
		}

		size_in_bits= unsafe( LLVMABISizeOfType( data_layout_, class_type.llvm_type ) ) * 8u64;
		alignment_in_bits= unsafe( LLVMABIAlignmentOfType( data_layout_, class_type.llvm_type ) ) * 8u32;
	}
	else
	{
		// Leave fields list empty and use stub size/alignment for incomplete classes (in case of error).
	}

	var ust::string8 name= Type(class_).ToString();

	auto di_type= unsafe( LLVMDIBuilderCreateStructType(
		builder_,
		di_file,
		cast_mut(name).data(), name.size(),
		di_file,
		src_loc.GetLine(),
		size_in_bits,
		alignment_in_bits,
		LLVMDIFlags(0),
		LLVMMetadataRef::Null, // DerivedFrom
		elements.data(), u32(elements.size()),
		0u, // RuntimeLang
		LLVMMetadataRef::Null,// VTableHolder
		ust::nullptr</char8/>(), 0s ) );

	// Replace temporary forward declaration with correct node.
	var LLVMMetadataRef &mut cache_value= classes_di_types_.find( class_ ).try_deref(); // Should be non-empty.
	unsafe( LLVMMetadataReplaceAllUsesWith( cache_value, di_type ) );
	cache_value= di_type;
}

} // namespace U1
