import "error_reporting.uh"
import "code_builder.uh"

namespace U
{

// "Class" of function argument in terms of overloading.
enum ArgOverloadingClass
{
	// Value-args (both mutable and immutable), immutable references.
	ImmutableReference,
	// Mutable references.
	MutalbeReference,
}

fn GetArgOverloadingClass( bool is_reference, bool is_mutable ) : ArgOverloadingClass
{
	return select(
		is_reference && is_mutable
			? ArgOverloadingClass::MutalbeReference
			: ArgOverloadingClass::ImmutableReference );
}

fn GetArgOverloadingClass( FunctionType::Arg& arg ) : ArgOverloadingClass
{
	return GetArgOverloadingClass( arg.is_reference, arg.is_mutable );
}

enum ConversionsCompareResult
{
	Same,
	LeftIsBetter,
	RightIsBetter,
	Incomparable
}

fn ComplareConversionsType(
	Type& src,
	Type& dst_left,
	Type& dst_right ) : ConversionsCompareResult
{
	if( dst_left == dst_right )
	{
		return ConversionsCompareResult::Same;
	}
	if( src == dst_left )
	{
		return ConversionsCompareResult::LeftIsBetter;
	}
	if( src == dst_right )
	{
		return ConversionsCompareResult::RightIsBetter;
	}

	if( src.ReferenceIsConvertibleTo( dst_left ) && src.ReferenceIsConvertibleTo( dst_right ) )
	{
		var Type void_type( FundamentalType( U_FundamentalType::void_, LLVMTypeRef::Null ) );

		// If one of types are void - other is better (because it is not void ).
		if( dst_left  == void_type )
		{
			return ConversionsCompareResult::RightIsBetter;
		}
		if( dst_right == void_type )
		{
			return ConversionsCompareResult::LeftIsBetter;
		}

		// SPRACHE_TODO - select more relevant compare function.

		//const Class&   src_class= *src.GetClassType();
		//const Class&  left_class= *src.GetClassType();
		//const Class& right_class= *src.GetClassType();

		if( dst_right.ReferenceIsConvertibleTo( dst_left  ) )
		{
			return ConversionsCompareResult::RightIsBetter;
		}
		if( dst_left .ReferenceIsConvertibleTo( dst_right ) )
		{
			return ConversionsCompareResult::LeftIsBetter;
		}
	}

	return ConversionsCompareResult::Incomparable;
}

fn CompareConversionsMutability(
	FunctionType::Arg& src,
	FunctionType::Arg& dst_left,
	FunctionType::Arg& dst_right ) : ConversionsCompareResult
{
	auto  left_overloding_class= GetArgOverloadingClass(dst_left );
	auto right_overloding_class= GetArgOverloadingClass(dst_right);

	if( left_overloding_class == right_overloding_class )
	{
		return ConversionsCompareResult::Same;
	}
	if( left_overloding_class == ArgOverloadingClass::MutalbeReference )
	{
		return ConversionsCompareResult::LeftIsBetter;
	}
	if( right_overloding_class == ArgOverloadingClass::MutalbeReference )
	{
		return ConversionsCompareResult::RightIsBetter;
	}

	return ConversionsCompareResult::Incomparable;
}

fn CompareConversions(
	FunctionType::Arg& src,
	FunctionType::Arg& dst_left,
	FunctionType::Arg& dst_right ) : ConversionsCompareResult
{
	auto types_compare= ComplareConversionsType( src.arg_type, dst_left.arg_type, dst_right.arg_type );
	auto mutability_compare= CompareConversionsMutability( src, dst_left, dst_right );

	if( types_compare == mutability_compare )
	{
		return types_compare;
	}

	if( types_compare == ConversionsCompareResult::Incomparable || mutability_compare == ConversionsCompareResult::Incomparable )
	{
		return ConversionsCompareResult::Incomparable;
	}
	if( types_compare == ConversionsCompareResult::Same )
	{
		return mutability_compare;
	}
	if( mutability_compare == ConversionsCompareResult::Same )
	{
		return types_compare;
	}

	return ConversionsCompareResult::Incomparable;
}

fn CompareConversions(
	FunctionType::Arg& src,
	FunctionType::Arg& dst_left,
	FunctionType::Arg& dst_right,
	TemplateSignatureParam& template_param_l,
	TemplateSignatureParam& template_param_r ) : ConversionsCompareResult
{
	// conversions result have more preority. Compare template specialization only if conversions comare results are same.

	auto conversions_compare= CompareConversions( src, dst_left, dst_right );
	auto template_specialization_compare= CompareTemplateSpecialization( template_param_l, template_param_r );

	if( conversions_compare == ConversionsCompareResult::Incomparable )
	{
		return ConversionsCompareResult::Incomparable;
	}

	if( conversions_compare == ConversionsCompareResult::LeftIsBetter ||
		conversions_compare == ConversionsCompareResult::RightIsBetter )
	{
		return conversions_compare;
	}

	return template_specialization_compare;
}

fn CompareTemplateSpecialization( TemplateSignatureParam& l, TemplateSignatureParam& r ) : ConversionsCompareResult
{
	if( !l.GetTemplateParam().empty() )
	{
		if( !r.GetTemplateParam().empty() )
		{
			return ConversionsCompareResult::Same;
		}
		if( !r.GetTypeParam().empty() ||
			!r.GetVariableParam().empty() ||
			!r.GetArrayTypeParam().empty() ||
			!r.GetFunctionTypeParam().empty() ||
			!r.GetSpecializedTemplateParam().empty() )
		{
			return ConversionsCompareResult::RightIsBetter;
		}
	}
	if( !l.GetTypeParam().empty() )
	{
		if( !r.GetTemplateParam().empty() )
		{
			return ConversionsCompareResult::LeftIsBetter; // Concrete type is better, then template parameter.
		}
		if( !r.GetTypeParam().empty() )
		{
			return ConversionsCompareResult::Same;
		}
		if( !r.GetArrayTypeParam().empty() ||
			!r.GetFunctionTypeParam().empty() ||
			!r.GetSpecializedTemplateParam().empty() )
		{
			return ConversionsCompareResult::LeftIsBetter;
		}
	}
	if( !l.GetVariableParam().empty() )
	{
		if( !r.GetTemplateParam().empty() )
		{
			return ConversionsCompareResult::LeftIsBetter; // Concrete variable is better, then template parameter.
		}
		if( !r.GetVariableParam().empty() )
		{
			return ConversionsCompareResult::Same;
		}
	}
	if_var( &l_array_type : l.GetArrayTypeParam() )
	{
		if( !r.GetTemplateParam().empty() )
		{
			return ConversionsCompareResult::LeftIsBetter; // Array type is more specialized, then template parameter.
		}
		if( !r.GetTypeParam().empty() )
		{
			return ConversionsCompareResult::RightIsBetter; // Concrete type is better, then array type.
		}
		if_var( &r_array_type : r.GetArrayTypeParam() )
		{
			auto type_compare_result= CompareTemplateSpecialization( l_array_type.element_type, r_array_type.element_type );
			auto count_compare_result= CompareTemplateSpecialization( l_array_type.element_count, r_array_type.element_count );
			if( type_compare_result == ConversionsCompareResult::Incomparable || count_compare_result == ConversionsCompareResult::Incomparable )
			{
				return ConversionsCompareResult::Incomparable;
			}
			if( type_compare_result == ConversionsCompareResult::Same )
			{
				return count_compare_result;
			}
			if( count_compare_result == ConversionsCompareResult::Same )
			{
				return type_compare_result;
			}
			if( type_compare_result == count_compare_result )
			{
				return type_compare_result;
			}
			return ConversionsCompareResult::Incomparable;
		}
	}
	if_var( &l_function_type : l.GetFunctionTypeParam() )
	{
		if( !r.GetTemplateParam().empty() )
		{
			return ConversionsCompareResult::LeftIsBetter; // Function type is more specialized, then template parameter.
		}
		if( !r.GetTypeParam().empty() )
		{
			return ConversionsCompareResult::RightIsBetter; // Concrete type is better, then function type.
		}
		if_var( &r_function_type : r.GetFunctionTypeParam() )
		{
			halt if( l_function_type.params.size() != r_function_type.params.size() );

			auto mut result= CompareTemplateSpecialization( l_function_type.return_type, r_function_type.return_type );
			for( auto mut i= 0s; i < l_function_type.params.size(); ++i )
			{
				auto param_result= CompareTemplateSpecialization( l_function_type.params[i].t, r_function_type.params[i].t );
				if( param_result == ConversionsCompareResult::Incomparable )
				{
					return ConversionsCompareResult::Incomparable;
				}

				if( param_result == ConversionsCompareResult::Same )
				{}
				else if( result == ConversionsCompareResult::Same )
				{
					result= param_result;
				}
				else if( result != param_result )
				{
					return ConversionsCompareResult::Incomparable;
				}
			}
			return result;
		}
	}
	if_var( &l_specialized_template : l.GetSpecializedTemplateParam() )
	{
		if( !r.GetTemplateParam().empty() )
		{
			return ConversionsCompareResult::LeftIsBetter; // Specialized template is more specialized, then template parameter.
		}
		if( !r.GetTypeParam().empty() )
		{
			return ConversionsCompareResult::RightIsBetter; // Concrete type is better, then Specialized template.
		}
		if_var( &r_specialized_template : r.GetSpecializedTemplateParam() )
		{
			if( l_specialized_template.params.size() != r_specialized_template.params.size() )
			{
				return ConversionsCompareResult::Incomparable;
			}

			auto mut result= ConversionsCompareResult::Same;
			for( auto mut i= 0s; i < l_specialized_template.params.size(); ++i )
			{
				auto param_result= CompareTemplateSpecialization( l_specialized_template.params[i], r_specialized_template.params[i] );
				if( param_result == ConversionsCompareResult::Incomparable )
				{
					return ConversionsCompareResult::Incomparable;
				}

				if( param_result == ConversionsCompareResult::Same )
				{}
				else if( result == ConversionsCompareResult::Same )
				{
					result= param_result;
				}
				else if( result != param_result )
				{
					return ConversionsCompareResult::Incomparable;
				}
			}
			return result;
		}
	}

	halt; // Unhandled combination of template signature parameter kinds.
}

fn CodeBuilder::ApplyOverloadedFunction(
	FunctionsSet &mut functions_set,
	FunctionVariable mut function,
	NamesScopePtr& names_scope,
	FilePos& file_pos ) : bool
{
	if( functions_set.functions.empty() )
	{
		functions_set.functions.push_back(function);
		return true;
	}

	{
		auto& function_type= function.t;

		/*
		Algorithm for overloading applying:
		If parameter count differs - overload function.
		If "ArgOverloadingClass" of one or more arguments differs - overload function.
		*/
		foreach( & set_function : functions_set.functions )
		{
			if( function.no_mangle || set_function.no_mangle )
			{
				REPORT_ERROR( CouldNotOverloadFunction, names_scope, file_pos )
				return false;
			}

			auto& set_function_type= set_function.t;

			// If argument count differs - allow overloading.
			// SPRACHE_TODO - handle default arguments.
			if( function_type.args.size() != set_function_type.args.size() )
			{
				continue;
			}

			auto mut arg_is_same_count= 0s;
			for( auto mut i= 0s; i < function_type.args.size(); ++i )
			{
				auto& arg= function_type.args[i];
				auto& set_arg= set_function_type.args[i];
				if( arg.arg_type == set_arg.arg_type && GetArgOverloadingClass( arg ) == GetArgOverloadingClass( set_arg ) )
				{
					++arg_is_same_count;
				}
			}

			if( arg_is_same_count == function_type.args.size() )
			{
				REPORT_ERROR( CouldNotOverloadFunction, names_scope, file_pos )
				return false;
			}
		} // For functions in set.
	}

	// No error - add function to set.
	functions_set.functions.push_back( move(function) );
	return true;
}

// TODO - pass array of "Variable" instead of "FunctionType::Arg"
fn CodeBuilder::GetOverloadedFunction(
	mut this,
	FunctionsSet &'x functions_set,
	ust::array_view_imut</FunctionType::Arg/> actual_args,
	bool first_actual_arg_is_this,
	NamesScopePtr& names_scope,
	FilePos& file_pos,
	bool produce_errors ) : ust::optional</FunctionVariable/>
{
	var ust::vector</FunctionVariable/> mut match_functions;
	foreach( & function : functions_set.functions )
	{
		var FunctionType& function_type= function.t;

		auto mut actual_args_corrected= actual_args;
		if( first_actual_arg_is_this && !function.is_this_call )
		{
			actual_args_corrected.drop_front();
		}

		// TODO - fix foreach and use continue here.
		if( function_type.args.size() == actual_args_corrected.size() )
		{
			auto mut all_args_is_compatible= true;

			for( auto mut arg_n= 0s; arg_n < actual_args_corrected.size(); ++arg_n )
			{
				auto arg_overloading_class= GetArgOverloadingClass( actual_args_corrected[arg_n] );
				auto parameter_overloading_class= GetArgOverloadingClass( function_type.args[arg_n] );

				auto& src_type= actual_args_corrected[arg_n].arg_type;
				auto& dst_type= function_type.args[arg_n].arg_type;
				if( src_type == dst_type )
				{} // ok
				else
				{
					if( src_type != void_type_ && dst_type != void_type_ &&
						!( EnsureTypeComplete( src_type ) && EnsureTypeComplete( dst_type ) ) )
					{
						if( produce_errors )
						{
							REPORT_ERROR( CouldNotSelectOverloadedFunction, names_scope, file_pos )
						}
						all_args_is_compatible= false;
						break;
					}

					if( src_type.ReferenceIsConvertibleTo( dst_type ) )
					{} // Ok - convert references.
					else
					{
						// TODO - apply here type/reference conversions
						all_args_is_compatible= false;
						break;
					}
				}

				if( arg_overloading_class == parameter_overloading_class )
				{} // All ok, exact match
				else if( parameter_overloading_class == ArgOverloadingClass::MutalbeReference &&
					arg_overloading_class != ArgOverloadingClass::MutalbeReference )
				{
					// We can only bind nonconst-reference arg to nonconst-reference parameter.
					all_args_is_compatible= false;
					break;
				}
				else if( parameter_overloading_class == ArgOverloadingClass::ImmutableReference &&
					arg_overloading_class == ArgOverloadingClass::MutalbeReference )
				{} // Ok, mut to imut conversion.
				else{ halt; }
			}

			if( all_args_is_compatible )
			{
				match_functions.push_back(function);
			}
		}
	}

	foreach( &function_template : functions_set.function_templates )
	{
		if_var( mut generated_function : GenTemplateFunction( function_template, actual_args, first_actual_arg_is_this, file_pos ) )
		{
			match_functions.push_back( move(generated_function) );
		}
	}

	foreach( &parametrized_function_template : functions_set.parametrized_function_templates )
	{
		auto generated_function=
			GenTemplateFunction(
				parametrized_function_template.base_template,
				parametrized_function_template.known_args,
				actual_args,
				first_actual_arg_is_this,
				file_pos );
		if( !generated_function.empty() )
		{
			match_functions.push_back( generated_function.get_ref() );
		}
	}

	if( match_functions.empty() )
	{
		if( produce_errors )
		{
			REPORT_ERROR( CouldNotSelectOverloadedFunction, names_scope, file_pos )
		}
		return ust::optional</FunctionVariable/>();
	}

	var ust::vector</bool/> mut best_functions;
	best_functions.resize( match_functions.size(), true );

	// For each argument search functions, which is better, than another functions.
	// For NOT better (four current arg) functions set flags to false.

	var TemplateSignatureParam dummy_template_param= TemplateSignatureParam::TypeParam();

	for( auto mut arg_n= 0s; arg_n < actual_args.size(); ++arg_n )
	{
		foreach( &l_function : match_functions )
		{
			var FunctionType& l_type= l_function.t;

			auto mut l_arg_n= arg_n;
			if( first_actual_arg_is_this && !l_function.is_this_call )
			{
				if( l_arg_n == 0s )
				{
					continue;
				}
				--l_arg_n;
			}

			auto mut is_best_function_for_current_arg= true;
			foreach( &r_function : match_functions )
			{
				var FunctionType& r_type= r_function.t;

				auto mut r_arg_n= arg_n;
				if( first_actual_arg_is_this && !r_function.is_this_call )
				{
					if( r_arg_n == 0s )
					{
						continue;
					}
					--r_arg_n;
				}

				auto comp=
					CompareConversions(
						actual_args[arg_n],
						l_type.args[l_arg_n],
						r_type.args[r_arg_n],
						select( l_function.base_template.empty() ? dummy_template_param : l_function.base_template.get_ref().signature_params[l_arg_n] ),
						select( r_function.base_template.empty() ? dummy_template_param : r_function.base_template.get_ref().signature_params[r_arg_n] ) );

				if( comp == ConversionsCompareResult::Same || comp == ConversionsCompareResult::LeftIsBetter )
				{}
				else
				{
					is_best_function_for_current_arg= false;
					break;
				}
			} // for functions right

			// Set best functions bits.
			if( is_best_function_for_current_arg )
			{
				for( auto mut func_n= 0s; func_n < match_functions.size(); ++func_n )
				{
					var FunctionVariable& r_function= cast_imut(match_functions)[func_n];
					var FunctionType& r_type= r_function.t;

					auto mut r_arg_n= arg_n;
					if( first_actual_arg_is_this && !r_function.is_this_call )
					{
						if( r_arg_n == 0s )
						{
							continue;
						}
						--r_arg_n;
					}

					auto comp=
						CompareConversions(
							actual_args[arg_n],
							l_type.args[l_arg_n],
							r_type.args[r_arg_n],
							select( l_function.base_template.empty() ? dummy_template_param : l_function.base_template.get_ref().signature_params[l_arg_n] ),
							select( r_function.base_template.empty() ? dummy_template_param : r_function.base_template.get_ref().signature_params[r_arg_n] ) );
					if( comp != ConversionsCompareResult::Same )
					{
						best_functions[func_n]= false;
					}
				}
			}
		} // for functions left
	}

	var ust::optional</FunctionVariable/> mut selected_function;
	for( auto mut func_n= 0s; func_n < match_functions.size(); ++func_n )
	{
		if( best_functions[func_n] )
		{
			if( selected_function.empty() )
			{
				selected_function.reset( match_functions[func_n] );
			}
			else
			{
				selected_function.reset();
				break;
			}
		}
	}

	if( selected_function.empty() && produce_errors )
	{
		REPORT_ERROR( TooManySuitableOverloadedFunctions, names_scope, file_pos )
	}

	return selected_function;
}

fn CodeBuilder::GetOverloadedOperator(
	mut this,
	ust::array_view_imut</FunctionType::Arg/> args,
	OverloadedOperator operator,
	NamesScopePtr& names_scope, // Needed only for errors reporting
	FilePos& file_pos ) : ust::optional</FunctionVariable/>
{
	foreach( &arg : args )
	{
		if_var( &class_type : arg.arg_type.GetClassType() )
		{
			auto class_members= class_type.lock_imut().get_ref().members;
			auto members_lock= class_members.lock_imut();
			auto& members= members_lock.get_ref();
			if_var( &operators_value : members.GetThisScopeValue( OverloadedOperatorToString( operator ) ) )
			{
				auto functions_set_ptr= operators_value.Get</NamesScopeFunctionsSet/>();
				if( !functions_set_ptr.empty() )
				{
					BuildFunctionsSet( class_members, ust::to_non_nullable( functions_set_ptr ), false );

					auto functions_set_lock= functions_set_ptr.lock_imut();
					if_var( &function_selected : GetOverloadedFunction( functions_set_lock.get_ref().functions_set, args, false, names_scope, file_pos, false ) )
					{
						return function_selected;
					}
				}
			}
		}

		if( operator == OverloadedOperator::Indexing || operator == OverloadedOperator::Call )
		{
			break; // For indexing and call operators only check first argument.
		}
	}

	return ust::optional</FunctionVariable/>();
}

fn CodeBuilder::SelectMoreSpecializedTemplate(
	mut this,
	ust::array_view_imut</TypeTemplatePtr/> type_templates,
	size_type signature_args_to_check ) : size_type
{
	if( type_templates.empty() )
	{
		return ~0s;
	}

	if( type_templates.size() == 1s )
	{
		return 0s;
	}

	var ust::vector</bool/> mut best_templates;
	best_templates.resize( type_templates.size(), true );

	for( auto mut arg_n= 0s; arg_n < signature_args_to_check; ++arg_n )
	{
		foreach( &candidate_l : type_templates )
		{
			auto mut is_best_template_for_current_arg= true;
			foreach( &candidate_r : type_templates )
			{
				auto comp= CompareTemplateSpecialization(
					candidate_l.get_ref().signature_params[arg_n],
					candidate_r.get_ref().signature_params[arg_n] );
				if( comp == ConversionsCompareResult::Incomparable || comp == ConversionsCompareResult::RightIsBetter )
				{
					is_best_template_for_current_arg= false;
					break;
				}
			}

			if( !is_best_template_for_current_arg )
			{
				continue;
			}

			for( auto mut index_r= 0s; index_r < type_templates.size(); ++index_r )
			{
				auto& candidate_r= type_templates[index_r];
				auto comp= CompareTemplateSpecialization(
					candidate_l.get_ref().signature_params[arg_n],
					candidate_r.get_ref().signature_params[arg_n] );

				if( comp != ConversionsCompareResult::Same )
				{
					best_templates[index_r]= false;
				}
			}
		}
	}

	auto mut selected_template= ~0s;
	for( auto mut i= 0s; i < type_templates.size(); ++i )
	{
		if( best_templates[i] )
		{
			if( selected_template == ~0s )
			{
				selected_template= i;
			}
			else
			{
				return ~0s;
			}
		}
	}

	return selected_template;
}

} // namespace U
