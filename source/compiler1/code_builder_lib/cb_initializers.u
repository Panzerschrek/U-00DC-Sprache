import "../lex_synt_lib/keywords.uh"
import "error_reporting.uh"
import "code_builder.uh"

namespace U
{

fn CodeBuilder::BuildInitializer( this, NamesScopePtr& names_scope, FunctionContext &mut function_context, NamesScopeVariable& variable, Synt::Initializer& initializer ) : LLVMValueRef
{
	variant_visit( &i : initializer )
	{
		return BuildInitializer( names_scope, function_context, variable, i );
	}
	halt;
}

fn CodeBuilder::BuildInitializer( this, NamesScopePtr& names_scope, FunctionContext &mut function_context, NamesScopeVariable& variable, Synt::EmptyVariant& empty_initializer ) : LLVMValueRef
{
	halt;
}

fn CodeBuilder::BuildInitializer( this, NamesScopePtr& names_scope, FunctionContext &mut function_context, NamesScopeVariable& variable, Synt::Expression& expression_initializer ) : LLVMValueRef
{
	if( !variable.t.GetFundamentalType().empty() || !variable.t.GetEnumType().empty() )
	{
		auto expr_result= BuildExpressionCodeEnsureVariable( names_scope, function_context, expression_initializer );
		if( expr_result.t != variable.t )
		{
			REPORT_ERROR( TypesMismatch, names_scope, Synt::GetFilePos(expression_initializer), variable.t, expr_result.t )
			return LLVMValueRef::Null;
		}

		unsafe
		{
			auto value_in_register= CreateMoveToLLVMRegisterInstruction( expr_result, function_context );
			LLVMBuildStore( function_context.llvm_ir_builder, value_in_register, variable.llvm_value );
		}
		return expr_result.constexpr_value;
	}
	if( !variable.t.GetArrayType().empty() )
	{
		REPORT_ERROR( NotImplemented, names_scope, Synt::GetFilePos(expression_initializer), ust::string8("expression initialization for arrays") )
		return LLVMValueRef::Null;
	}
	if_var( &class_type : variable.t.GetClassType() )
	{
		auto expr_result= BuildExpressionCodeEnsureVariable( names_scope, function_context, expression_initializer );
		if( expr_result.t != variable.t )
		{
			REPORT_ERROR( TypesMismatch, names_scope, FilePos(), variable.t, expr_result.t )
			return LLVMValueRef::Null;
		}
		TryCallCopyConstructor( names_scope, function_context, variable.llvm_value, expr_result.llvm_value, class_type, Synt::GetFilePos(expression_initializer) );
		return LLVMValueRef::Null;
	}

	halt; // Unhandled type kind
}

fn CodeBuilder::BuildInitializer( this, NamesScopePtr& names_scope, FunctionContext &mut function_context, NamesScopeVariable& variable, Synt::SequenceInitializer& sequence_initializer ) : LLVMValueRef
{
	if_var( &array_type : variable.t.GetArrayType() )
	{
		if( sequence_initializer.elements_initializers.size() != size_type(array_type.element_count) )
		{
			REPORT_ERROR( ArrayInitializersCountMismatch,
				names_scope,
				sequence_initializer.file_pos,
				array_type.element_count,
				sequence_initializer.elements_initializers.size() )
			return LLVMValueRef::Null;
		}

		auto mut is_constant= variable.t.CanBeConstexpr();
		var ust::vector</ LLVMValueRef /> mut elements_constants;

		var NamesScopeVariable mut element_variable
		{
			.t= array_type.element_type.get_ref(),
		};

		auto mut i= 0s;
		foreach( &element_initializer : sequence_initializer.elements_initializers )
		{
			unsafe
			{
				var [ LLVMValueRef, 2 ] indexes
				[
					GetZeroGEPIndex(),
					LLVMConstInt( fundamental_llvm_types_.u32_, u64(i), LLVMBool::False )
				];

				element_variable.llvm_value= LLVMBuildGEP( function_context.llvm_ir_builder, variable.llvm_value, indexes[0], 2u, "[]\0"[0] );
			}

			auto element_constant= BuildInitializer( names_scope, function_context, element_variable, element_initializer.get_ref() );

			if( is_constant )
			{
				if( element_constant == LLVMValueRef::Null )
				{
					is_constant= false;
				}
				else
				{
					elements_constants.push_back( element_constant );
				}
			}

			++i;
		}

		if( is_constant )
		{
			halt if( elements_constants.size() != size_type(array_type.element_count) ); // TODO - replace with assert
			unsafe
			{
				if( elements_constants.empty() )
				{
					return LLVMConstArray( element_variable.t.GetLLVMType(), LLVMValueRef::Null, 0u );
				}
				else
				{
					return LLVMConstArray( element_variable.t.GetLLVMType(), cast_imut(elements_constants).front(), u32(elements_constants.size()) );
				}
			}
		}
		return LLVMValueRef::Null;
	}

	REPORT_ERROR( ArrayInitializerForNonArray, names_scope, sequence_initializer.file_pos )
	return LLVMValueRef::Null;
}

fn CodeBuilder::BuildInitializer( this, NamesScopePtr& names_scope, FunctionContext &mut function_context, NamesScopeVariable& variable, Synt::StructNamedInitializer& struct_named_initializer ) : LLVMValueRef
{
	auto class_type_ref= variable.t.GetClassType();
	if( class_type_ref.empty() )
	{
		REPORT_ERROR( StructInitializerForNonStruct, names_scope, struct_named_initializer.file_pos )
		return LLVMValueRef::Null;
	}
	var ClassTypePtr& class_type_ptr= class_type_ref.get_ref();
	auto class_type_lock= class_type_ptr.lock_imut();
	var ClassType& class_type= class_type_lock.get_ref();

	auto members_lock= class_type.members.lock_imut();
	var NamesScope& class_members= members_lock.get_ref();

	if( class_type.have_explicit_noncopy_constructors )
	{
		REPORT_ERROR( InitializerDisabledBecauseClassHaveExplicitNoncopyConstructors, names_scope, struct_named_initializer.file_pos )
	}

	foreach( &member_initializer : struct_named_initializer.members_initializers )
	{
		// TODO - use unordered_set
		foreach( &prev_initializer : struct_named_initializer.members_initializers )
		{
			if( ust::ref_to_int(prev_initializer) < ust::ref_to_int(member_initializer) &&
				member_initializer.name == prev_initializer.name )
			{
				REPORT_ERROR( DuplicatedStructMemberInitializer, names_scope, member_initializer.file_pos, member_initializer.name )
			}
		}

		auto member_ref= class_members.GetThisScopeValue( member_initializer.name );
		if( member_ref.empty() )
		{
			REPORT_ERROR( NameNotFound, names_scope, member_initializer.file_pos, member_initializer.name )
			return LLVMValueRef::Null;
		}
		auto field_ptr= member_ref.get_ref().Get</ClassField/>();
		if( field_ptr.empty() )
		{
			REPORT_ERROR( InitializerForNonfieldStructMember, names_scope, member_initializer.file_pos, member_initializer.name )
			return LLVMValueRef::Null;
		}
		auto field_lock= field_ptr.lock_imut();
		var ClassField& class_field= field_lock.get_ref();

		if( class_field.is_reference )
		{
			InitializeReferenceField( names_scope, function_context, variable, class_field, member_initializer.initializer.get_ref() );
		}
		else
		{
			var NamesScopeVariable mut member_variable
			{
				.t= class_field.t
			};

			auto op_name_nt= "." + member_initializer.name + "\0";
			unsafe
			{
				var [ LLVMValueRef, 2 ] indexes
				[
					GetZeroGEPIndex(),
					LLVMConstInt( fundamental_llvm_types_.u32_, u64(class_field.index), LLVMBool::False )
				];

				member_variable.llvm_value= LLVMBuildGEP( function_context.llvm_ir_builder, variable.llvm_value, indexes[0], 2u, op_name_nt.front() );
			}
			BuildInitializer( names_scope, function_context, member_variable, member_initializer.initializer.get_ref() );
		}
	}

	// Apply empty initializer for fileds, missing in struct named initializer.
	foreach( &class_member : class_members.GetInternalContainer() )
	{
		auto field_ptr= class_member.value().Get</ ClassField />();
		if( field_ptr.empty() )
		{
			continue;
		}
		auto mut found= false;
		foreach( &member_initializer : struct_named_initializer.members_initializers )
		{
			if( member_initializer.name == class_member.key() )
			{
				found= true;
				break;
			}
		}
		if( found )
		{
			continue;
		}

		auto field_lock= field_ptr.lock_imut();
		var ClassField & field= field_lock.get_ref();

		if( field.is_reference )
		{
			REPORT_ERROR( ExpectedInitializer, names_scope, struct_named_initializer.file_pos, class_member.key() )
			continue;
		}

		var NamesScopeVariable mut member_variable
		{
			.t= field.t
		};

		auto op_name_nt= "." + field.syntax_element.get_ref().name + "\0";
		unsafe
		{
			var [ LLVMValueRef, 2 ] indexes
			[
				GetZeroGEPIndex(),
				LLVMConstInt( fundamental_llvm_types_.u32_, u64(field.index), LLVMBool::False )
			];

			member_variable.llvm_value= LLVMBuildGEP( function_context.llvm_ir_builder, variable.llvm_value, indexes[0], 2u, op_name_nt.front() );
		}

		BuildEmptyInitializer( names_scope, function_context, member_variable, class_member.key(), struct_named_initializer.file_pos );
	}

	return LLVMValueRef::Null;
}

fn CodeBuilder::BuildInitializer( this, NamesScopePtr& names_scope, FunctionContext &mut function_context, NamesScopeVariable& variable, Synt::ConstructorInitializer& constructor_initializer ) : LLVMValueRef
{
	return BuildConstructorInitializer( names_scope, function_context, variable, constructor_initializer.args, constructor_initializer.file_pos );
}

fn CodeBuilder::BuildInitializer( this, NamesScopePtr& names_scope, FunctionContext &mut function_context, NamesScopeVariable& variable, Synt::ZeroInitializer& zero_initializer ) : LLVMValueRef
{
	if_var( &fundamental_type : variable.t.GetFundamentalType() )
	{
		var LLVMValueRef mut zero_value= zero_init;
		if( fundamental_type.fundamental_type == U_FundamentalType::bool_ ||
			IsInteger( fundamental_type.fundamental_type ) ||
			IsChar( fundamental_type.fundamental_type ) )
		{
			unsafe{  zero_value= LLVMConstInt( fundamental_type.llvm_type, 0u64, LLVMBool::False );  }
		}
		else
		{
			unsafe{  zero_value= LLVMConstReal( fundamental_type.llvm_type, 0.0 );  }
		}

		unsafe{  LLVMBuildStore( function_context.llvm_ir_builder, zero_value, variable.llvm_value );  }
		return zero_value;
	}
	if_var( &enum_type : variable.t.GetEnumType() )
	{
		// Currently, first element of enum have zero value.

		unsafe
		{
			auto zero_value= LLVMConstInt( variable.t.GetLLVMType(), 0u64, LLVMBool::False );
			LLVMBuildStore( function_context.llvm_ir_builder, zero_value, variable.llvm_value );
			return zero_value;
		}
	}
	if_var( &array_type : variable.t.GetArrayType() )
	{
		auto mut is_constant= variable.t.CanBeConstexpr();
		var ust::vector</ LLVMValueRef /> mut elements_constants;

		var NamesScopeVariable mut element_variable
		{
			.t= array_type.element_type.get_ref()
		};

		// TODO - generate REAL loop
		for( auto mut i= 0u64; i < array_type.element_count; ++i )
		{
			unsafe
			{
				var [ LLVMValueRef, 2 ] indexes
				[
					GetZeroGEPIndex(),
					LLVMConstInt( fundamental_llvm_types_.u32_, u64(i), LLVMBool::False )
				];

				element_variable.llvm_value= LLVMBuildGEP( function_context.llvm_ir_builder, variable.llvm_value, indexes[0], 2u, "[]\0"[0] );
			}

			auto element_constant= BuildInitializer( names_scope, function_context, element_variable, zero_initializer );

			if( is_constant )
			{
				if( element_constant == LLVMValueRef::Null )
				{
					is_constant= false;
				}
				else
				{
					elements_constants.push_back( element_constant );
				}
			}
		}

		if( is_constant )
		{
			halt if( elements_constants.size() != size_type(array_type.element_count) ); // TODO - replace with assert
			unsafe
			{
				if( elements_constants.empty() )
				{
					return LLVMConstArray( element_variable.t.GetLLVMType(), LLVMValueRef::Null, 0u );
				}
				else
				{
					return LLVMConstArray( element_variable.t.GetLLVMType(), cast_imut(elements_constants).front(), u32(elements_constants.size()) );
				}
			}
		}
		return LLVMValueRef::Null;
	}
	if_var( &class_type_ptr : variable.t.GetClassType() )
	{
		// TODO - disable zero_init for some classes.
		auto class_type_lock= class_type_ptr.lock_imut();
		var ClassType& class_type= class_type_lock.get_ref();
		auto members_lock= class_type.members.lock_imut();
		var NamesScope& class_members= members_lock.get_ref();

		foreach( &class_member : class_members.GetInternalContainer() )
		{
			auto field_ptr= class_member.value().Get</ ClassField />();
			if( field_ptr.empty() )
			{
				continue;
			}
			auto field_lock= field_ptr.lock_imut();
			var ClassField& field= field_lock.get_ref();

			if( field.is_reference )
			{
				REPORT_ERROR( UnsupportedInitializerForReference, names_scope, zero_initializer.file_pos )
				continue;
			}

			var NamesScopeVariable mut member_variable
			{
				.t= field.t
			};

			auto op_name_nt= "." + field.syntax_element.get_ref().name + "\0";
			unsafe
			{
				var [ LLVMValueRef, 2 ] indexes
				[
					GetZeroGEPIndex(),
					LLVMConstInt( fundamental_llvm_types_.u32_, u64(field.index), LLVMBool::False )
				];

				member_variable.llvm_value= LLVMBuildGEP( function_context.llvm_ir_builder, variable.llvm_value, indexes[0], 2u, op_name_nt.front() );
			}

			BuildInitializer( names_scope, function_context, member_variable, zero_initializer );
		}
		return LLVMValueRef::Null;
	}

	halt;
}

fn CodeBuilder::BuildInitializer( this, NamesScopePtr& names_scope, FunctionContext &mut function_context, NamesScopeVariable& variable, Synt::UninitializedInitializer& uninitialized_initializer ) : LLVMValueRef
{
	// TODO - emit error if not in unsafe block
	return LLVMValueRef::Null;
}

fn CodeBuilder::BuildEmptyInitializer( this, NamesScopePtr& names_scope, FunctionContext &mut function_context, NamesScopeVariable& variable, ust::string8& variable_name, FilePos& file_pos )
{
	if( !variable.t.IsDefaultConstructible() )
	{
		REPORT_ERROR( ExpectedInitializer, names_scope, file_pos, variable_name )
		return;
	}

	if_var( &array_type : variable.t.GetArrayType() )
	{
		var NamesScopeVariable mut element_variable
		{
			.t= array_type.element_type.get_ref()
		};

		// TODO - generate REAL loop
		for( auto mut i= 0u64; i < array_type.element_count; ++i )
		{
			unsafe
			{
				var [ LLVMValueRef, 2 ] indexes
				[
					GetZeroGEPIndex(),
					LLVMConstInt( fundamental_llvm_types_.u32_, u64(i), LLVMBool::False )
				];

				element_variable.llvm_value= LLVMBuildGEP( function_context.llvm_ir_builder, variable.llvm_value, indexes[0], 2u, "[]\0"[0] );
			}

			BuildEmptyInitializer( names_scope, function_context, element_variable, variable_name, file_pos );
		}
		return;
	}
	if_var( &class_type_ptr : variable.t.GetClassType() )
	{
		auto class_members= class_type_ptr.lock_imut().get_ref().members;
		auto members_lock= class_members.lock_imut();

		auto member= members_lock.get_ref().GetThisScopeValue( KeywordToString( Keyword::constructor_ ) );
		if( member.empty() )
		{
			// May be in case of error
			return;
		}

		auto constructors_ptr= member.get_ref().Get</NamesScopeFunctionsSet/>();
		// Should be functions set 100%
		// Should be complete here.

		var Variable this_
		{
			.t= variable.t,
			.value_type= ValueType::ReferenceMut,
			.llvm_value= variable.llvm_value,
		};

		var ust::vector</ ust::shared_ptr_final</Synt::Expression/> /> empty_args_list;

		CallFunctionsSet(
			names_scope,
			function_context,
			ust::optional_ref</ Variable, false />( this_ ),
			constructors_ptr.lock_imut().get_ref().functions_set,
			empty_args_list,
			file_pos );

		return;
	}
}

fn CodeBuilder::BuildConstructorInitializer( this, NamesScopePtr& names_scope, FunctionContext &mut function_context, NamesScopeVariable& variable, ust::vector</ ust::shared_ptr_final</Synt::Expression/> />& args, FilePos& file_pos )  : LLVMValueRef
{
	if_var( &dst_type : variable.t.GetFundamentalType() )
	{
		if( args.size() != 1s )
		{
			REPORT_ERROR( FundamentalTypesHaveConstructorsWithExactlyOneParameter, names_scope, file_pos )
			return LLVMValueRef::Null;
		}

		var Variable src_var= BuildExpressionCodeEnsureVariable( names_scope, function_context, args.front().get_ref() );

		var FundamentalType mut src_type;
		if_var( &fundamental_type : src_var.t.GetFundamentalType() )
		{
			src_type= fundamental_type;
		}
		if_var( &enum_type : src_var.t.GetEnumType() )
		{
			src_type= enum_type.lock_imut().get_ref().underlaying_type;
		}

		if( src_type.llvm_type == LLVMTypeRef::Null )
		{
			REPORT_ERROR( TypesMismatch, names_scope, file_pos, variable.t, src_var.t )
			return LLVMValueRef::Null;
		}

		auto mut llvm_value= CreateMoveToLLVMRegisterInstruction( src_var, function_context );
		unsafe
		{
			if( src_type.fundamental_type == dst_type.fundamental_type )
			{}
			else if( IsInteger( dst_type.fundamental_type ) && IsInteger( src_type.fundamental_type ) )
			{
				// int to int
				if( src_type.GetSize() < dst_type.GetSize() )
				{
					// We lost here some values in conversions, such i16 => u32, if src_type is signed.
					if( IsUnsignedInteger( dst_type.fundamental_type ) )
					{
						llvm_value= LLVMBuildZExt( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
					}
					else
					{
						llvm_value= LLVMBuildSExt( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
					}
				}
				else if( src_type.GetSize() > dst_type.GetSize() )
				{
					llvm_value= LLVMBuildTrunc( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
				}
				else{} // Same size integers - do nothing
			}
			else if( IsFloatingPoint( dst_type.fundamental_type ) && IsFloatingPoint( src_type.fundamental_type ) )
			{
				// float to float
				if( src_type.GetSize() < dst_type.GetSize() )
				{
					llvm_value= LLVMBuildFPExt( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
				}
				else if( src_type.GetSize() > dst_type.GetSize() )
				{
					llvm_value= LLVMBuildFPTrunc( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
				}
				else{ halt; } // WTF?
			}
			else if( IsFloatingPoint( dst_type.fundamental_type ) && IsInteger( src_type.fundamental_type ) )
			{
				// int to float
				if( IsSignedInteger( src_type.fundamental_type ) )
				{
					llvm_value= LLVMBuildSIToFP( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
				}
				else
				{
					llvm_value= LLVMBuildUIToFP( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
				}
			}
			else if( IsInteger( dst_type.fundamental_type ) && IsFloatingPoint( src_type.fundamental_type ) )
			{
				// float to int
				if( IsSignedInteger( dst_type.fundamental_type ) )
				{
					llvm_value= LLVMBuildFPToSI( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
				}
				else
				{
					llvm_value= LLVMBuildFPToUI( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
				}
			}
			else if( IsChar( dst_type.fundamental_type ) && ( IsInteger( src_type.fundamental_type ) || IsChar( src_type.fundamental_type ) ) )
			{
				// int to char or char to char
				if( src_type.GetSize() < dst_type.GetSize() )
				{
					llvm_value= LLVMBuildZExt( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
				}
				else if( src_type.GetSize() > dst_type.GetSize() )
				{
					llvm_value= LLVMBuildTrunc( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
				}
			}
			else if( IsInteger( dst_type.fundamental_type ) && IsChar( src_type.fundamental_type ) )
			{
				// char to int
				if( src_type.GetSize() < dst_type.GetSize() )
				{
					// We lost here some values in conversions, such i16 => u32, if src_type is signed.
					if( IsUnsignedInteger( dst_type.fundamental_type ) )
					{
						llvm_value= LLVMBuildZExt( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
					}
					else
					{
						llvm_value= LLVMBuildSExt( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
					}
				}
				else if( src_type.GetSize() > dst_type.GetSize() )
				{
					llvm_value= LLVMBuildTrunc( function_context.llvm_ir_builder, llvm_value, dst_type.llvm_type, "\0"[0] );
				}
				else{} // Same size - do nothing
			}
			else
			{
				if( dst_type.fundamental_type == U_FundamentalType::bool_ )
				{
					// TODO - error, bool have no constructors from other types
				}
				REPORT_ERROR( TypesMismatch, names_scope, file_pos, variable.t, src_var.t )
				return LLVMValueRef::Null;
			}

			LLVMBuildStore( function_context.llvm_ir_builder, llvm_value, variable.llvm_value );
		}

		unsafe{  if( LLVMIsConstant( llvm_value ) == LLVMBool::True ){ return llvm_value; }  }

		return LLVMValueRef::Null;
	}
	if_var( &enum_type : variable.t.GetEnumType() )
	{
		if( args.size() != 1s )
		{
			REPORT_ERROR( FundamentalTypesHaveConstructorsWithExactlyOneParameter, names_scope, file_pos )
			return LLVMValueRef::Null;
		}

		var Variable expr= BuildExpressionCodeEnsureVariable( names_scope, function_context, args.front().get_ref() );
		if( expr.t != variable.t )
		{
			REPORT_ERROR( TypesMismatch, names_scope, file_pos, variable.t, expr.t )
			return LLVMValueRef::Null;
		}

		unsafe
		{
			LLVMBuildStore( function_context.llvm_ir_builder, CreateMoveToLLVMRegisterInstruction( expr, function_context ), variable.llvm_value );
		}
		return expr.constexpr_value;
	}
	if_var( &class_type : variable.t.GetClassType() )
	{
		auto class_members= class_type.lock_imut().get_ref().members;
		auto members_lock= class_members.lock_imut();

		auto member= members_lock.get_ref().GetThisScopeValue( KeywordToString( Keyword::constructor_ ) );
		if( member.empty() )
		{
			REPORT_ERROR( ClassHaveNoConstructors, names_scope, file_pos )
			return LLVMValueRef::Null;
		}

		auto constructors_ptr= member.get_ref().Get</NamesScopeFunctionsSet/>();
		// Should be functions set 100%
		// Should be complete here.

		var Variable this_
		{
			.t= variable.t,
			.value_type= ValueType::ReferenceMut,
			.llvm_value= variable.llvm_value,
		};
		CallFunctionsSet(
			names_scope,
			function_context,
			ust::optional_ref</ Variable, false />( this_ ),
			constructors_ptr.lock_imut().get_ref().functions_set,
			args,
			file_pos );

		return LLVMValueRef::Null;
	}

	REPORT_ERROR( ConstructorInitializerForUnsupportedType, names_scope, file_pos )
	return LLVMValueRef::Null;
}

fn CodeBuilder::InitializeReferenceField( this, NamesScopePtr& names_scope, FunctionContext &mut function_context, NamesScopeVariable& variable, ClassField& field, Synt::Initializer& initializer )
{
	auto initializer_file_pos=  Synt::GetFilePos( initializer );

	var ust::optional_ref</Synt::Expression, false/> mut initializer_expression;
	if_var( &expression_initializer : initializer.get</Synt::Expression/>() )
	{
		initializer_expression.reset(expression_initializer);
	}
	if_var( &constructor_initializer : initializer.get</Synt::ConstructorInitializer/>() )
	{
		if( constructor_initializer.args.size() != 1s )
		{
			REPORT_ERROR( ReferencesHaveConstructorsWithExactlyOneParameter, names_scope, initializer_file_pos )
			return;
		}

		initializer_expression.reset( constructor_initializer.args.front().get_ref() );
	}

	if( initializer_expression.empty() )
	{
		REPORT_ERROR( UnsupportedInitializerForReference, names_scope, initializer_file_pos )
		return;
	}

	var Variable initializer_variable= BuildExpressionCodeEnsureVariable( names_scope, function_context, initializer_expression.get_ref() );
	auto initializer_expression_file_pos= Synt::GetFilePos( initializer_expression.get_ref() );
	if( initializer_variable.t != field.t )
	{
		REPORT_ERROR( TypesMismatch, names_scope, initializer_expression_file_pos, field.t, initializer_variable.t )
		return;
	}
	if( initializer_variable.value_type == ValueType::Value )
	{
		REPORT_ERROR( ExpectedReferenceValue, names_scope, initializer_expression_file_pos )
		return;
	}
	if( field.is_mutable && initializer_variable.value_type == ValueType::ReferenceImut )
	{
		REPORT_ERROR( BindingConstReferenceToNonconstReference, names_scope, initializer_expression_file_pos )
		return;
	}

	unsafe
	{
		var [ LLVMValueRef, 2 ] indexes
		[
			GetZeroGEPIndex(),
			LLVMConstInt( fundamental_llvm_types_.u32_, u64(field.index), LLVMBool::False )
		];
		auto address_of_reference= LLVMBuildGEP( function_context.llvm_ir_builder, variable.llvm_value, indexes[0], 2u, "\0"[0] );
		LLVMBuildStore( function_context.llvm_ir_builder, initializer_variable.llvm_value, address_of_reference );
	}
}

fn CodeBuilder::BuildConstructorInitialization(
	this,
	NamesScopePtr& names_scope,
	FunctionContext &mut function_context,
	Synt::StructNamedInitializer& constructor_initialization_list )
{
	// Function context should contain "this" with class type
	auto class_type= function_context.this_.get_ref().t.GetClassType().get_ref();
	auto class_members= class_type.lock_imut().get_ref().members;

	// Check for errors, build list of initialized fields.
	var ust::unordered_map</ust::string8, i32/> mut initialized_fields; // TODO - use unordered_set
	auto mut have_fields_errors= false;
	foreach( &member_initializer : constructor_initialization_list.members_initializers )
	{
		auto class_members_lock= class_members.lock_imut();
		auto member= class_members_lock.get_ref().GetThisScopeValue( member_initializer.name );
		if( member.empty() )
		{
			have_fields_errors= true;
			REPORT_ERROR( NameNotFound, names_scope, member_initializer.file_pos, member_initializer.name )
			continue;
		}
		auto class_field_ptr= member.get_ref().Get</ClassField/>();
		if( class_field_ptr.empty() )
		{
			have_fields_errors= true;
			REPORT_ERROR( InitializerForNonfieldStructMember, names_scope, member_initializer.file_pos, member_initializer.name )
			continue;
		}

		if( !initialized_fields.find( member_initializer.name ).empty() )
		{
			have_fields_errors= true;
			REPORT_ERROR( DuplicatedStructMemberInitializer, names_scope, member_initializer.file_pos, member_initializer.name )
			continue;
		}

		initialized_fields.insert( member_initializer.name, 0 );
		function_context.uninitialized_this_fields.insert( member_initializer.name, 0 );
	}

	var ust::vector</ust::string8/> mut uninitialized_fields;

	foreach( &class_member : class_members.lock_imut().get_ref().GetInternalContainer() )
	{
		auto field_ptr= class_member.value().Get</ ClassField />();
		if( !field_ptr.empty() )
		{
			if( initialized_fields.find( class_member.key() ).empty() )
			{
				uninitialized_fields.push_back( class_member.key() );
			}
		}
	}

	// Initialize fields, missing in initializer list.
	// TODO - use fiedls order.
	foreach( &field_name : uninitialized_fields )
	{
		auto field_ptr= class_members.lock_imut().get_ref().GetThisScopeValue( field_name ).get_ref().Get</ClassField/>();
		auto field_lock= field_ptr.lock_imut();
		var ClassField & class_field= field_lock.get_ref();

		if( class_field.is_reference )
		{
			REPORT_ERROR( ExpectedInitializer, names_scope, constructor_initialization_list.file_pos, field_name )
			continue;
		}

		var NamesScopeVariable mut field_variable
		{
			.t= class_field.t,
			.is_mutable= true,
		};

		auto op_name_nt= "." + field_name + "\0";
		unsafe
		{
			var [ LLVMValueRef, 2 ] indexes
			[
				GetZeroGEPIndex(),
				LLVMConstInt( fundamental_llvm_types_.u32_, u64(class_field.index), LLVMBool::False )
			];

			field_variable.llvm_value= LLVMBuildGEP( function_context.llvm_ir_builder, function_context.this_.get_ref().llvm_value, indexes[0], 2u, op_name_nt.front() );
		}

		BuildEmptyInitializer( names_scope, function_context, field_variable, field_name, constructor_initialization_list.file_pos );
	}

	if( have_fields_errors )
	{
		return;
	}

	// Apply explicit initializers.
	foreach( &member_initializer : constructor_initialization_list.members_initializers )
	{
		auto field_ptr= class_members.lock_imut().get_ref().GetThisScopeValue( member_initializer.name ).get_ref().Get</ClassField/>();
		auto field_lock= field_ptr.lock_imut();
		var ClassField & class_field= field_lock.get_ref();

		if( class_field.is_reference )
		{
			var NamesScopeVariable mut this_copy
			{
				.t= function_context.this_.get_ref().t,
			};
			this_copy.llvm_value= function_context.this_.get_ref().llvm_value;
			InitializeReferenceField( names_scope, function_context, this_copy, class_field, member_initializer.initializer.get_ref() );
		}
		else
		{
			var NamesScopeVariable mut field_variable
			{
				.t= class_field.t,
				.is_mutable= true,
			};

			auto op_name_nt= "." + member_initializer.name + "\0";
			unsafe
			{
				var [ LLVMValueRef, 2 ] indexes
				[
					GetZeroGEPIndex(),
					LLVMConstInt( fundamental_llvm_types_.u32_, u64(class_field.index), LLVMBool::False )
				];

				field_variable.llvm_value= LLVMBuildGEP( function_context.llvm_ir_builder, function_context.this_.get_ref().llvm_value, indexes[0], 2u, op_name_nt.front() );
			}

			BuildInitializer( names_scope, function_context, field_variable, member_initializer.initializer.get_ref() );
		}

		function_context.uninitialized_this_fields.erase( member_initializer.name );

	}
}

} // namespace U
