import "/string_conversions.u"
import "../lex_synt_lib/keywords.uh"
import "structs_combined.uh"

namespace U
{

namespace ToStringHelper
{

fn ToString( FundamentalType& fundamental_type ) : ust::string8
{
	return ust::string8( GetFundamentalTypeName( fundamental_type.fundamental_type ) );
}

fn ToString( ArrayType& array_type ) : ust::string8
{
	var ust::string8 mut result;
	result+= "[ ";
	result+= array_type.element_type.get_ref().ToString();
	result+= ", ";
	result+= ust::to_string8(array_type.element_count);
	result+= " ]";
	return move(result);
}

fn ToString( FunctionType& function_type ) : ust::string8
{
	var ust::string8 mut result;

	result+= KeywordToString( Keyword::fn_ );

	result+= "(";
	foreach( &arg : function_type.args )
	{
		result+= arg.arg_type.get_ref().ToString();
		result+= " arg";

		if( ust::ref_to_int(arg) != ust::ref_to_int(function_type.args.back()))
		{
			result+= ", ";
		}
	}
	result+= ") : ";

	result+= function_type.return_type.get_ref().ToString();

	return move(result);
}

fn ToString( ClassTypePtr& class_type ) : ust::string8
{
	return class_type.lock_imut().get_ref().members.lock_imut().get_ref().ToString();
}

} // namespace ToStringHelper

fn FundamentalType::constructor( U_FundamentalType in_fundamental_type, LLVMTypeRef in_llvm_type )
	( fundamental_type= in_fundamental_type, llvm_type= in_llvm_type )
{}

op FundamentalType::==( FundamentalType& l, FundamentalType& r ) : bool
{
	return l.fundamental_type == r.fundamental_type;
}

op FundamentalType::!=( FundamentalType& l, FundamentalType& r ) : bool
{
	return !(l == r);
}

fn ArrayType::constructor( mut this, ArrayType &imut other )
	( element_type(other.element_type.get_ref()), element_count(other.element_count), llvm_type(other.llvm_type) )
{}

op ArrayType::=( mut this, ArrayType &imut other )
{
	this.element_type.get_ref()= other.element_type.get_ref();
	this.element_count= other.element_count;
	this.llvm_type= other.llvm_type;
}

op ArrayType::==( ArrayType& l, ArrayType& r ) : bool
{
	return l.element_type.get_ref() == r.element_type.get_ref() && l.element_count == r.element_count;
}

op ArrayType::!=( ArrayType& l, ArrayType& r ) : bool
{
	return !(l == r);
}

op FunctionType::Arg::==(Arg& l, Arg& r) : bool
{
	return
		l.arg_type.get_ref() == r.arg_type.get_ref() &&
		l.is_reference == r.is_reference &&
		l.is_mutable == r.is_mutable;
}

op FunctionType::Arg::!=(Arg& l, Arg& r) : bool
{
	return !(l == r);
}

fn FunctionType::constructor( mut this, FunctionType &imut other )
	(
		return_type(other.return_type.get_ref()),
		llvm_type(other.llvm_type),
		return_value_is_reference(other.return_value_is_reference),
		return_value_is_mutable(other.return_value_is_mutable)
	)
{
	foreach( & in_arg : other.args )
	{
		var Arg mut arg
		{
			.arg_type( in_arg.arg_type.get_ref() ),
			.is_reference= in_arg.is_reference,
			.is_mutable= in_arg.is_mutable,
		};
		args.push_back( move(arg) );
	}
}

op FunctionType::=( mut this, FunctionType &imut other )
{
	return_type.get_ref()= other.return_type.get_ref();
	return_value_is_reference= other.return_value_is_reference;
	return_value_is_mutable= other.return_value_is_mutable;

	args.clear();
	foreach( & in_arg : other.args )
	{
		var Arg mut arg
		{
			.arg_type( in_arg.arg_type.get_ref() ),
			.is_reference= in_arg.is_reference,
			.is_mutable= in_arg.is_mutable,
		};
		args.push_back( move(arg) );
	}

	this.llvm_type= other.llvm_type;
}

op FunctionType::==( FunctionType& l, FunctionType& r ) : bool
{
	if( l.args.size() != r.args.size() )
	{
		return false;
	}

	auto mut i= 0s;
	while( i < l.args.size() )
	{
		if( l.args[i] != r.args[i] )
		{
			return false;
		}
		++i;
	}

	return
		l.return_type.get_ref() == r.return_type.get_ref() &&
		l.return_value_is_reference == r.return_value_is_reference &&
		l.return_value_is_mutable == r.return_value_is_mutable;
}

op FunctionType::!=( FunctionType& l, FunctionType& r ) : bool
{
	return !(l == r);
}

fn Type::conversion_constructor( FundamentalType mut fundamental_type )
	( something_( move(fundamental_type) ) )
{}

fn Type::conversion_constructor( ArrayType mut array_type )
	( something_( move(array_type) ) )
{}

fn Type::conversion_constructor( FunctionType mut function_type )
	( something_( move(function_type) ) )
{}

fn Type::conversion_constructor( ClassTypePtr mut class_type )
	( something_( move(class_type) ) )
{}

fn Type::GetFundamentalType(  mut this ) : ust::optional_ref</FundamentalType, true  />'this'
{
	return something_.get</FundamentalType/>();
}

fn Type::GetFundamentalType( imut this ) : ust::optional_ref</FundamentalType, false />'this'
{
	return something_.get</FundamentalType/>();
}

fn Type::GetArrayType(  mut this ) : ust::optional_ref</ArrayType, true  />'this'
{
	return something_.get</ArrayType/>();
}

fn Type::GetArrayType( imut this ) : ust::optional_ref</ArrayType, false />'this'
{
	return something_.get</ArrayType/>();
}

fn Type::GetFunctionType(  mut this ) : ust::optional_ref</FunctionType, true  />'this'
{
	return something_.get</FunctionType/>();
}

fn Type::GetFunctionType( imut this ) : ust::optional_ref</FunctionType, false />'this'
{
	return something_.get</FunctionType/>();
}

fn Type::GetClassType(  mut this ) : ust::optional_ref</ClassTypePtr, true  />'this'
{
	return something_.get</ClassTypePtr/>();
}

fn Type::GetClassType( imut this ) : ust::optional_ref</ClassTypePtr, false />'this'
{
	return something_.get</ClassTypePtr/>();
}

fn Type::GetLLVMType( imut this ) : LLVMTypeRef
{
	variant_visit( &t : something_ )
	{
		static_if( ust::is_same_type</ typeof(t), ClassTypePtr />() )
		{
			return t.lock_imut().get_ref().llvm_type;
		}
		else
		{
			return t.llvm_type;
		}
	}

	halt;
}

fn Type::ToString( this ) : ust::string8
{
	variant_visit( &t : something_ )
	{
		return ToStringHelper::ToString(t);
	}
	halt;
}

op Type::==( Type& l, Type& r ) : bool
{
	if( l.something_.index() != r.something_.index() )
	{
		return false;
	}

	variant_visit( &l_t : l.something_ )
	{
		static_if( ust::is_same_type</ typeof(l_t), ClassTypePtr />() )
		{
			// TODO - add == for shared_ptr
			return
				ust::ref_to_int( l_t.lock_imut().get_ref() ) ==
				ust::ref_to_int( r.something_.get</ typeof(l_t) />().get_ref().lock_imut().get_ref() );
		}
		else
		{
			return l_t == r.something_.get</ typeof(l_t) />().get_ref();
		}
	}

	halt;
}

op Type::!=( Type& l, Type& r ) : bool
{
	return !(l == r);
}

} // namespace U
