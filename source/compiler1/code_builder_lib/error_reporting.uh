import "/random_access_range.u"
import "/string_conversions.u"
import "/type_traits.u"
import "/vector.u"
import "/code_builder_errors.uh"
import "../lex_synt_lib/syntax_analyzer.uh"
import "structs_combined.uh"

?macro <? REPORT_ERROR:block ( ?error_code:ident, ?names_scope:expr, ?src_loc:expr ?args:rep<? , ?arg:expr ?> ) ?> ->
<?
	{
		var ust::vector</ust::string8/> mut ??format_args;
		?args<? ??format_args.push_back( PreprocessErrorArg( ?arg ) ); ?>

		auto ??container= ?names_scope.lock_imut().get_ref().GetErrors();
		auto mut ??lock= ??container.lock_mut();
		??lock.get_ref().push_back( ReportError( CodeBuilderErrorCode::?error_code, ?src_loc, ??format_args ) );
	}
?>

namespace U1
{

fn ReportError( CodeBuilderErrorCode code, SrcLoc& src_loc, ust::vector</ust::string8/>& format_args ) : CodeBuilderError;

fn ReportError( CodeBuilderErrorCode code, SrcLoc& src_loc ) : CodeBuilderError
{
	return ReportError( code, src_loc, ust::vector</ust::string8/>() );
}

fn ReportError( CodeBuilderErrorCode code, SrcLoc& src_loc, ust::string8 mut arg ) : CodeBuilderError
{
	var ust::vector</ust::string8/> mut args;
	args.push_back( move(arg) );
	return ReportError( code, src_loc, args );
}

template</type T/>
fn PreprocessErrorArg( T& x ) : ust::string8
{
	static_if( ust::is_same_type</T, ust::string8/>() )
	{
		return x;
	}
	else if( ust::is_same_type</T, Type/>() )
	{
		return x.ToString();
	}
	else if( ust::is_same_type</ T, ust::array_view_imut</char8/> />() )
	{
		return ust::string8(x);
	}
	else if( ust::is_same_type</ T, Synt::ComplexName />() )
	{
		return ComplexNameToString(x);
	}
	else
	{
		return ust::to_string8(x);
	}
}

fn ComplexNameToString( Synt::ComplexName& complex_name ) : ust::string8;

fn NormalizeErrors( ErrorsContainer &mut errors, Synt::MacroExpansionContexts& macro_expansion_contexts );

} // namespace U1
