import "error_reporting.uh"
import "code_builder.uh"

namespace U1
{

fn CodeBuilder::CheckCompleteFunctionReferenceNotation( FunctionType& function_type, NamesScopePtr& names_scope, SrcLoc& src_loc )
{
	CheckFunctionReferencesNotationInnerReferences( function_type, names_scope, src_loc );
	CheckFunctionReferencesNotationMutabilityCorrectness( function_type, names_scope, src_loc );
}

fn CodeBuilder::CheckFunctionReferencesNotationInnerReferences( FunctionType& function_type, NamesScopePtr& names_scope, SrcLoc& src_loc )
{
	foreach( &pollution : function_type.references_pollution )
	{
		CheckFunctionReferencesNotationParamReference( function_type, pollution.dst, names_scope, src_loc );
		CheckFunctionReferencesNotationParamReference( function_type, pollution.src, names_scope, src_loc );
	}

	foreach( &param_reference : function_type.return_references )
	{
		CheckFunctionReferencesNotationParamReference( function_type, param_reference, names_scope, src_loc );
	}
	foreach( &param_references : function_type.return_inner_references )
	{
		foreach( &param_reference : param_references )
		{
			CheckFunctionReferencesNotationParamReference( function_type, param_reference, names_scope, src_loc );
		}
	}

	if( !function_type.return_inner_references.empty() )
	{
		auto return_type_tag_count= function_type.return_type.ReferenceTagCount();
		if( function_type.return_inner_references.size() != size_type(return_type_tag_count) )
		{
			REPORT_ERROR( InnerReferenceTagCountMismatch, names_scope, src_loc, return_type_tag_count, function_type.return_inner_references.size() )
		}
	}
}

fn CodeBuilder::CheckFunctionReferencesNotationParamReference( FunctionType& function_type, FunctionType::ParamReference& param_reference, NamesScopePtr& names_scope, SrcLoc& src_loc )
{
	if( param_reference.reference_index != FunctionType::c_param_reference_number && size_type(param_reference.param_index) < function_type.params.size()  )
	{
		var Type& t= function_type.params[ size_type( param_reference.param_index) ].t;
		auto tag_count= t.ReferenceTagCount();
		if( u32(param_reference.reference_index) >= tag_count )
		{
			REPORT_ERROR( ReferenceTagOutOfRange, names_scope, src_loc, u32(param_reference.reference_index), t, tag_count )
		}
	}
}

fn CodeBuilder::CheckFunctionReferencesNotationMutabilityCorrectness( FunctionType& function_type, NamesScopePtr& names_scope, SrcLoc& src_loc )
{
	// Do not allow to specify reference notation where immutable references are linked with mutable references.

	if( function_type.return_value_type == ValueType::ReferenceMut )
	{
		CheckReferenceNotationMutabilityViolationForReturnReferences( function_type, function_type.return_references, names_scope, src_loc );
	}

	for(
		var size_type mut i= 0s, i_end= ust::min( size_type( function_type.return_type.ReferenceTagCount() ), function_type.return_inner_references.size() );
		i < i_end;
		++i )
	{
		if( function_type.return_type.GetInnerReferenceKind(i) == InnerReferenceKind::Mut )
		{
			CheckReferenceNotationMutabilityViolationForReturnReferences( function_type, function_type.return_inner_references[i], names_scope, src_loc );
		}
	}

	foreach( & pollution : function_type.references_pollution )
	{
		auto dst_param_index= size_type(pollution.dst.param_index);
		if( dst_param_index >= function_type.params.size() )
		{
			continue;
		}
		var FunctionType::Param& dst_param= function_type.params[ dst_param_index ];

		if( pollution.dst.reference_index != FunctionType::c_param_reference_number &&
			u32(pollution.dst.reference_index) < dst_param.t.ReferenceTagCount() &&
			dst_param.t.GetInnerReferenceKind( size_type( pollution.dst.reference_index ) ) == InnerReferenceKind::Mut )
		{
			CheckReferenceNotationMutabilityViolationForMutableReference( function_type, pollution.src, names_scope, src_loc );
		}
	}
}

fn CodeBuilder::CheckReferenceNotationMutabilityViolationForReturnReferences( FunctionType& function_type, FunctionType::ParamReferences& return_references, NamesScopePtr& names_scope, SrcLoc& src_loc )
{
	foreach( &return_reference : return_references )
	{
		CheckReferenceNotationMutabilityViolationForMutableReference( function_type, return_reference, names_scope, src_loc );
	}
}

fn CodeBuilder::CheckReferenceNotationMutabilityViolationForMutableReference( FunctionType& function_type, FunctionType::ParamReference& param_reference, NamesScopePtr& names_scope, SrcLoc& src_loc )
{
	auto param_index= size_type(param_reference.param_index);
	if( param_index >= function_type.params.size() )
	{
		return; // May be in case of error.
	}

	var FunctionType::Param& param= function_type.params[ param_index ];

	if( param_reference.reference_index == FunctionType::c_param_reference_number )
	{
		if( param.value_type == ValueType::ReferenceImut )
		{
			REPORT_ERROR( ReferenceNotationViolatesImmutability, names_scope, src_loc )
		}
	}
	else
	{
		if( u32(param_reference.reference_index) < param.t.ReferenceTagCount() &&
			param.t.GetInnerReferenceKind( size_type(param_reference.reference_index) ) != InnerReferenceKind::Mut )
		{
			REPORT_ERROR( ReferenceNotationViolatesImmutability, names_scope, src_loc )
		}
	}
}

fn CodeBuilder::CheckReturnedReferenceIsAllowed( this, NamesScopePtr& names, FunctionContext& function_context, VariablePtr& return_reference_node, SrcLoc& src_loc )
{
	foreach( &node : function_context.references_graph.GetAllAccessibleVariableNodes( return_reference_node ) )
	{
		if( !IsReferenceAllowedForReturn( function_context, node ) )
		{
			REPORT_ERROR( ReturningUnallowedReference, names, src_loc )
		}
	}
}

fn CodeBuilder::IsReferenceAllowedForReturn( this, FunctionContext& function_context, VariablePtr& variable_node ) : bool
{
	foreach( &return_reference : function_context.function_type.return_references )
	{
		auto i= size_type(return_reference.param_index);
		if( return_reference.reference_index == FunctionType::c_param_reference_number &&
			variable_node == function_context.args_nodes[i] )
		{
			return true;
		}
		if( size_type(return_reference.reference_index) < function_context.args_inner_variable_nodes[i].size() &&
			variable_node == function_context.args_inner_variable_nodes[i][size_type(return_reference.reference_index)] )
		{
			return true;
		}
	}

	return false;
}

fn CodeBuilder::CheckReturnedInnerReferenceIsAllowed( this, NamesScopePtr& names, FunctionContext& function_context, VariablePtr& return_reference_node, SrcLoc& src_loc )
{
	with( &inner_reference_nodes : return_reference_node.lock_imut().deref().inner_reference_nodes )
	{
		for( auto mut i= 0s; i < inner_reference_nodes.size(); ++i )
		{
			foreach( &node : function_context.references_graph.GetAllAccessibleVariableNodes( inner_reference_nodes[i] ) )
			{
				if( !IsReferenceAllowedForInnerReturn( function_context, node, i ) )
				{
					REPORT_ERROR( ReturningUnallowedReference, names, src_loc )
				}
			}
		}
	}
}

fn CodeBuilder::IsReferenceAllowedForInnerReturn( this, FunctionContext& function_context, VariablePtr& variable_node, size_type index ) : bool
{
	if( index >= function_context.function_type.return_inner_references.size() )
	{
		return false;
	}
	foreach( &return_reference : function_context.function_type.return_inner_references[index] )
	{
		auto param_n= size_type(return_reference.param_index);
		if( return_reference.reference_index == FunctionType::c_param_reference_number &&
			variable_node == function_context.args_nodes[param_n] )
		{
			return true;
		}
		if( size_type(return_reference.reference_index) < function_context.args_inner_variable_nodes[param_n].size() &&
			variable_node == function_context.args_inner_variable_nodes[param_n][ size_type(return_reference.reference_index) ] )
		{
			return true;
		}
	}
	return false;
}

fn CodeBuilder::CheckAsyncReturnReferenceIsAllowed(
	NamesScopePtr& names,
	FunctionContext& function_context,
	CoroutineTypeDescription& coroutine_type_description,
	VariablePtr& node,
	SrcLoc& src_loc )
{
	foreach( &var_node : function_context.references_graph.GetAllAccessibleVariableNodes( node ) )
	{
		auto coroutine_inner_reference= GetCoroutineInnerReferenceForParamNode( function_context, var_node );

		var bool mut found= false;
		foreach( &param_reference : coroutine_type_description.return_references )
		{
			if( coroutine_inner_reference == param_reference )
			{
				found= true;
				break;
			}
		}

		if( !found )
		{
			REPORT_ERROR( ReturningUnallowedReference, names, src_loc )
		}
	}
}

fn CodeBuilder::CheckAsyncReturnInnerReferencesAreAllowed(
	NamesScopePtr& names,
	FunctionContext& function_context,
	CoroutineTypeDescription& coroutine_type_description,
	VariablePtr& node,
	SrcLoc& src_loc )
{
	with( &inner_reference_nodes : node.lock_imut().deref().inner_reference_nodes )
	{
		for( var size_type mut i= 0s; i < inner_reference_nodes.size(); ++i )
		{
			foreach( &var_node : function_context.references_graph.GetAllAccessibleVariableNodes( inner_reference_nodes[i] ) )
			{
				auto coroutine_inner_reference= GetCoroutineInnerReferenceForParamNode( function_context, var_node );

				var bool mut found= false;
				if( i < coroutine_type_description.return_inner_references.size() )
				{
					foreach( &param_reference : coroutine_type_description.return_inner_references[i] )
					{
						if( coroutine_inner_reference == param_reference )
						{
							found= true;
							break;
						}
					}
				}

				if( !found )
				{
					REPORT_ERROR( ReturningUnallowedReference, names, src_loc )
				}
			}
		}
	}
}

fn CodeBuilder::GetCoroutineInnerReferenceForParamNode( FunctionContext& function_context, VariablePtr& node ) : ust::optional</FunctionType::ParamReference/>
{
	// Map coroutine function input references to returned coroutine inner references.
	// If this changed, "TransformCoroutineFunctionType" function must be changed too!

	auto mut coroutine_inner_reference_index= 0s;
	for( var size_type mut i= 0s; i < function_context.function_type.params.size(); ++i )
	{
		var FunctionType::Param& param= function_context.function_type.params[i];

		if( param.value_type == ValueType::Value )
		{
			auto& inner_nodes= function_context.args_inner_variable_nodes[i];
			for( var size_type mut j= 0s; j < inner_nodes.size(); ++j )
			{
				if( node == inner_nodes[j] )
				{
					var FunctionType::ParamReference mut param_reference{ .param_index= 0u8, .reference_index= u8(coroutine_inner_reference_index + j) };
					return move(param_reference);
				}
			}

			coroutine_inner_reference_index+= size_type( param.t.ReferenceTagCount() );
		}
		else
		{
			if( node == function_context.args_nodes[i] )
			{
				var FunctionType::ParamReference mut param_reference{ .param_index= 0u8, .reference_index= u8(coroutine_inner_reference_index) };
				return move(param_reference);
			}

			++coroutine_inner_reference_index;
		}
	}

	return ust::null_optional;
}

fn CodeBuilder::CheckReferencesPollutionBeforeReturn( this, NamesScopePtr& names_scope, FunctionContext& function_context, SrcLoc& src_loc )
{
	auto& function_type= function_context.function_type;
	auto& params= function_type.params;
	for( auto mut param_i= 0s; param_i < params.size(); ++param_i )
	{
		if( params[param_i].value_type == ValueType::Value )
		{
			continue;
		}
		with( &inner_reference_nodes : function_context.args_nodes[param_i].try_lock_imut().deref().inner_reference_nodes )
		{
			for( auto mut tag_i= 0s; tag_i < inner_reference_nodes.size(); ++tag_i )
			{
				var VariablePtr& inner_reference_node= inner_reference_nodes[tag_i];
				foreach( &accessible_variable_node : function_context.references_graph.GetAllAccessibleVariableNodes( inner_reference_node ) )
				{
					if( accessible_variable_node == function_context.args_inner_variable_nodes[param_i][tag_i] )
					{
						continue;
					}

					var ust::optional</FunctionType::ParamReference/> mut param_reference;
					for( auto mut j= 0s; j < params.size(); ++j )
					{
						if( accessible_variable_node == function_context.args_nodes[j] )
						{
							var FunctionType::ParamReference mut r{ .param_index= u8(j), .reference_index= FunctionType::c_param_reference_number };
							param_reference= move(r);
						}
						for( auto mut tag_j= 0s; tag_j < function_context.args_inner_variable_nodes[j].size(); ++tag_j )
						{
							if( accessible_variable_node == function_context.args_inner_variable_nodes[j][tag_j] )
							{
								var FunctionType::ParamReference mut r{ .param_index= u8(j), .reference_index=u8(tag_j) };
								param_reference= move(r);
							}
						}
					}

					if( param_reference.empty() )
					{
						continue;
					}

					var FunctionType::ReferencePollution actual_reference_pollution
					{
						.dst{ .param_index= u8(param_i), .reference_index= u8(tag_i) },
						.src= param_reference.try_deref(),
					};

					var bool mut found= false;
					foreach( &reference_pollution : function_type.references_pollution )
					{
						if( actual_reference_pollution == reference_pollution )
						{
							found= true;
							break;
						}
					}

					if( !found )
					{
						REPORT_ERROR( UnallowedReferencePollution, names_scope, src_loc )
					}
				}
			}
		}
	}
}

fn CodeBuilder::LambdaPreprocessingCollectReturnReferences( FunctionContext &mut function_context, VariablePtr& return_node )
{
	auto mut lock= function_context.lambda_preprocessing_context.try_lock_mut();
	var LambdaPreprocessingContext &mut lambda_preprocessing_context= lock.deref();

	foreach( &variable_node : function_context.references_graph.GetAllAccessibleVariableNodes( return_node ) )
	{
		for( auto mut arg_n= 0s; arg_n < function_context.args_nodes.size(); ++arg_n )
		{
			if( variable_node == function_context.args_nodes[arg_n] )
			{
				var FunctionType::ParamReference mut param_reference{ .param_index= u8(arg_n), .reference_index= FunctionType::c_param_reference_number };
				lambda_preprocessing_context.return_references.push_back( move(param_reference) );
			}
		}

		for( auto mut arg_n= 0s; arg_n < function_context.args_inner_variable_nodes.size(); ++arg_n )
		{
			for( auto mut tag_n= 0s; tag_n < function_context.args_inner_variable_nodes[arg_n].size(); ++tag_n )
			{
				if( variable_node == function_context.args_inner_variable_nodes[arg_n][tag_n] )
				{
					var FunctionType::ParamReference mut param_reference{ .param_index= u8(arg_n), .reference_index= u8(tag_n) };
					lambda_preprocessing_context.return_references.push_back( move(param_reference) );
				}
			}
		}

		foreach( &captured_variable_pair : lambda_preprocessing_context.captured_external_variables )
		{
			if( variable_node == captured_variable_pair.value().variable_node )
			{
				lambda_preprocessing_context.captured_variables_return_references.push_back( variable_node );
			}

			foreach( &accessible_variable : captured_variable_pair.value().accessible_variables )
			{
				if( variable_node == accessible_variable )
				{
					lambda_preprocessing_context.captured_variables_return_references.push_back( variable_node );
				}
			}
		}
	}
}

fn CodeBuilder::LambdaPreprocessingCollectReturnInnerReferences( FunctionContext &mut function_context, VariablePtr& return_node )
{
	auto mut lock= function_context.lambda_preprocessing_context.try_lock_mut();
	var LambdaPreprocessingContext &mut lambda_preprocessing_context= lock.deref();

	with( &inner_reference_nodes : return_node.lock_imut().deref().inner_reference_nodes )
	{
		if( lambda_preprocessing_context.return_inner_references.size() < inner_reference_nodes.size() )
		{
			lambda_preprocessing_context.return_inner_references.resize( inner_reference_nodes.size() );
		}

		if( lambda_preprocessing_context.captured_variables_return_inner_references.size() < inner_reference_nodes.size() )
		{
			lambda_preprocessing_context.captured_variables_return_inner_references.resize( inner_reference_nodes.size() );
		}

		for( auto mut i= 0s; i < inner_reference_nodes.size(); ++i )
		{
			foreach( &variable_node : function_context.references_graph.GetAllAccessibleVariableNodes( inner_reference_nodes[i] ) )
			{
				for( auto mut arg_n= 0s; arg_n < function_context.args_nodes.size(); ++arg_n )
				{
					if( variable_node == function_context.args_nodes[arg_n] )
					{
						var FunctionType::ParamReference mut param_reference{ .param_index= u8(arg_n), .reference_index= FunctionType::c_param_reference_number };
						lambda_preprocessing_context.return_inner_references[i].push_back( move(param_reference) );
					}
				}

				for( auto mut arg_n= 0s; arg_n < function_context.args_inner_variable_nodes.size(); ++arg_n )
				{
					for( auto mut tag_n= 0s; tag_n < function_context.args_inner_variable_nodes[arg_n].size(); ++tag_n )
					{
						if( variable_node == function_context.args_inner_variable_nodes[arg_n][tag_n] )
						{

							var FunctionType::ParamReference mut param_reference{ .param_index= u8(arg_n), .reference_index= u8(tag_n) };
							lambda_preprocessing_context.return_inner_references[i].push_back( move(param_reference) );
						}
					}
				}

				foreach( &captured_variable_pair : lambda_preprocessing_context.captured_external_variables )
				{
					if( variable_node == captured_variable_pair.value().variable_node )
					{
						lambda_preprocessing_context.captured_variables_return_inner_references[i].push_back( variable_node );
					}

					foreach( &accessible_variable : captured_variable_pair.value().accessible_variables )
					{
						if( variable_node == accessible_variable )
						{
							lambda_preprocessing_context.captured_variables_return_inner_references[i].push_back( variable_node );
						}
					}
				}
			}
		}
	}
}

fn CodeBuilder::LambdaPreprocessingCollectReferencePollution( FunctionContext& function_context )
{
	auto mut lock= function_context.lambda_preprocessing_context.try_lock_mut();
	var LambdaPreprocessingContext &mut lambda_preprocessing_context= lock.deref();

	auto& function_type= function_context.function_type;
	auto& params= function_type.params;
	for( auto mut param_i= 0s; param_i < params.size(); ++param_i )
	{
		if( params[param_i].value_type == ValueType::Value )
		{
			continue;
		}
		with( &inner_reference_nodes : function_context.args_nodes[param_i].try_lock_imut().deref().inner_reference_nodes )
		{
			for( auto mut tag_i= 0s; tag_i < inner_reference_nodes.size(); ++tag_i )
			{
				var VariablePtr& inner_reference_node= inner_reference_nodes[tag_i];
				foreach( &accessible_variable_node : function_context.references_graph.GetAllAccessibleVariableNodes( inner_reference_node ) )
				{
					if( accessible_variable_node == function_context.args_inner_variable_nodes[param_i][tag_i] )
					{
						continue;
					}

					var ust::optional</ LambdaPreprocessingContext::ReferenceLink /> mut src_param_reference;
					for( auto mut j= 0s; j < params.size(); ++j )
					{
						if( accessible_variable_node == function_context.args_nodes[j] )
						{
							var FunctionType::ParamReference mut r{ .param_index= u8(j), .reference_index= FunctionType::c_param_reference_number };
							src_param_reference= move(r);
						}
						for( auto mut tag_j= 0s; tag_j < function_context.args_inner_variable_nodes[j].size(); ++tag_j )
						{
							if( accessible_variable_node == function_context.args_inner_variable_nodes[j][tag_j] )
							{
								var FunctionType::ParamReference mut r{ .param_index= u8(j), .reference_index=u8(tag_j) };
								src_param_reference= move(r);
							}
						}
					}

					foreach( &captured_variable_pair : lambda_preprocessing_context.captured_external_variables )
					{
						if( accessible_variable_node == captured_variable_pair.value().variable_node )
						{
							src_param_reference= accessible_variable_node;
						}

						foreach( &captured_variable_accessible_variable : captured_variable_pair.value().accessible_variables )
						{
							if( accessible_variable_node == captured_variable_accessible_variable )
							{
								src_param_reference= accessible_variable_node;
							}
						}
					}

					if( src_param_reference.empty() )
					{
						continue;
					}

					var FunctionType::ParamReference mut dst_param_reference{ .param_index= u8(param_i), .reference_index= u8(tag_i) };

					var LambdaPreprocessingContext::ReferencePollution mut actual_reference_pollution
					{
						.dst= move(dst_param_reference),
						.src= src_param_reference.try_deref(),
					};
					lambda_preprocessing_context.references_pollution.push_back( move(actual_reference_pollution) );
				}
			}
		}
	}

	// TODO - collect also pollution for lambda captured variables as destination.
}

fn CodeBuilder::MergeReferencesGraphsAfterConditionalBranches( ust::array_view_imut</ReferencesGraph/> branches, NamesScopePtr names_scope, SrcLoc& src_loc ) : ReferencesGraph
{
	with( mut lock : names_scope.lock_imut().deref().GetErrors().try_lock_mut() )
	{
		return ReferencesGraph::MergeReferencesGraphsAfterConditionalBranches( branches, lock.deref(), src_loc );
	}
}

fn CodeBuilder::CheckReferencesGraphAfterLoop( ReferencesGraph& state_before, ReferencesGraph& state_after, NamesScopePtr names_scope, SrcLoc& src_loc )
{
	with( mut lock : names_scope.lock_imut().deref().GetErrors().try_lock_mut() )
	{
		ReferencesGraph::CheckReferencesGraphAfterLoop( state_before, state_after, lock.deref(), src_loc );
	}
}

fn CodeBuilder::SetupReferencesInCopyOrMove(
	NamesScopePtr& names_scope,
	FunctionContext &mut function_context,
	VariablePtr& dst_node,
	VariablePtr& src_node,
	SrcLoc& src_loc )
{
	auto reference_tag_count= dst_node.lock_imut().deref().t.ReferenceTagCount();
	if( reference_tag_count == 0u )
	{
		return;
	}

	auto src_lock= src_node.lock_imut();
	auto dst_lock= dst_node.lock_imut();
	for( auto mut i= 0s; i < size_type(reference_tag_count); ++i )
	{
		function_context.references_graph.TryAddLinkToAllAccessibleVariableNodesInnerReferences(
			src_lock.deref().inner_reference_nodes[i],
			dst_lock.deref().inner_reference_nodes[i],
			names_scope,
			src_loc );
	}
}

} // namespace U1
