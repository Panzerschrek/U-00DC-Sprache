import "/sort.u"
import "/keywords.uh"
import "error_reporting.uh"
import "code_builder.uh"

?macro <? ADD_BOOL_FIELD:block( ?name:expr, ?value:expr ) ?>
->
<?
	{
		var ClassField mut class_field
		{
			.t= bool_type_,
			.is_mutable= true,
			.is_reference= false,
			.index= u32(fields_llvm_types.size()),
			.syntax_element= dummy_syntax_element,
			.source_class= typeinfo_class,
		};

		fields_llvm_types.push_back( class_field.t.GetLLVMType() );
		fields_initializers.push_back( unsafe( LLVMConstInt( class_field.t.GetLLVMType(), select( ?value ? 1u64 : 0u64 ), LLVMBool::False ) ) );

		auto mut members_lock= class_members.lock_mut();
		members_lock.deref().AddName( ?name, move(class_field) );
	}
?>

?macro <? ADD_SIZE_FIELD:block( ?name:expr, ?value:expr ) ?>
->
<?
	{
		var ClassField mut class_field
		{
			.t= size_type_,
			.is_mutable= true,
			.is_reference= false,
			.index= u32(fields_llvm_types.size()),
			.syntax_element= dummy_syntax_element,
			.source_class= typeinfo_class,
		};

		fields_llvm_types.push_back( class_field.t.GetLLVMType() );
		fields_initializers.push_back( unsafe( LLVMConstInt( class_field.t.GetLLVMType(), u64(?value), LLVMBool::False ) ) );

		auto mut members_lock= class_members.lock_mut();
		members_lock.deref().AddName( ?name, move(class_field) );
	}
?>

?macro <? ADD_TYPEINFO_FIELD:block( ?name:expr, ?dependent_type:expr ) ?>
->
<?
	{
		auto dependent_type_typeinfo_ptr= GetTypeinfoPrototype( ?dependent_type, class_members.lock_imut().deref().GetParent().try_to_non_nullable() );
		auto lock= dependent_type_typeinfo_ptr.lock_imut();
		var Variable& dependent_type_typeinfo= lock.deref();
		var ClassField mut class_field
		{
			.t= dependent_type_typeinfo.t,
			.is_mutable= false,
			.is_reference= true,
			.index= u32(fields_llvm_types.size()),
			.syntax_element= dummy_syntax_element,
			.source_class= typeinfo_class,
		};

		fields_llvm_types.push_back( unsafe( LLVMPointerType( class_field.t.GetLLVMType(), 0u ) ) );

		fields_initializers.push_back( dependent_type_typeinfo.llvm_value );

		auto mut members_lock= class_members.lock_mut();
		members_lock.deref().AddName( ?name, move(class_field) );
	}
?>

?macro <? ADD_LIST_FIELD:block( ?name:expr, ?list:expr ) ?>
->
<?
	{
		auto list_variable= ?list;
		var ClassField mut class_field
		{
			.t= list_variable.t,
			.is_mutable= true,
			.is_reference= false,
			.index= u32(fields_llvm_types.size()),
			.syntax_element= dummy_syntax_element,
			.source_class= typeinfo_class,
		};

		fields_llvm_types.push_back( class_field.t.GetLLVMType() );
		fields_initializers.push_back( list_variable.constexpr_value );

		auto mut members_lock= class_members.lock_mut();
		members_lock.deref().AddName( ?name, move(class_field) );
	}
?>

?macro <? ADD_NAME_FIELD:block( ?name:expr ) ?>
->
<?
	{
		var ust::string8& element_name= ?name;

		var ArrayType mut array_type
		{
			.element_type= FundamentalType( U_FundamentalType::char8_, fundamental_llvm_types_.char8_ ),
			.element_count= u64(element_name.size()),
		};
		array_type.llvm_type= unsafe( LLVMArrayType( array_type.element_type.GetLLVMType(), u32(array_type.element_count) ) );

		var ClassField mut class_field
		{
			.t= move(array_type),
			.is_mutable= true,
			.is_reference= false,
			.index= u32(fields_llvm_types.size()),
			.syntax_element= dummy_syntax_element,
			.source_class= typeinfo_class,
		};

		fields_llvm_types.push_back( class_field.t.GetLLVMType() );
		fields_initializers.push_back( unsafe( LLVMConstStringInContext( llvm_context_, cast_mut(element_name).data(), u32(element_name.size()), LLVMBool::True ) ) );

		auto mut members_lock= class_members.lock_mut();
		members_lock.deref().AddName( "name", move(class_field) );
	}
?>

?macro <? ADD_CLASS_MEMBER_COMMON_FIELDS:block( ?name:expr ) ?>
->
<?
	{
		var ust::string8& name= ?name;
		ADD_NAME_FIELD( name )

		var Synt::ClassVisibility visibility= class_type.lock_imut().deref().GetMemberVisibility( name );
		ADD_BOOL_FIELD( "is_public"   , visibility == Synt::ClassVisibility::Public    )
		ADD_BOOL_FIELD( "is_protected", visibility == Synt::ClassVisibility::Protected )
		ADD_BOOL_FIELD( "is_private"  , visibility == Synt::ClassVisibility::Private   )
	}
?>

namespace U1
{

// Use reserved by language names, started with "_".
auto& g_typeinfo_root_class_name= "_TI";
auto& g_typeinfo_tuple_elements_list_node_class_name= "_TITL_";
auto& g_typeinfo_function_params_list_node_class_name= "_TIAL_";
auto& g_typeinfo_class_fields_list_node_class_name= "_TICFiL_";
auto& g_typeinfo_class_functions_list_node_class_name= "_TICFuL_";
auto& g_typeinfo_class_types_list_node_class_name= "_TICTL_";
auto& g_typeinfo_class_parents_list_node_class_name= "_TICPL_";
auto& g_typeinfo_enum_elements_list_node_class_name= "_TIEL_";

struct ClassFieldForTypeinfo
{
	ust::shared_ptr_imut</ClassField/> class_field;
	ust::string8 name;

	op<=>( ClassFieldForTypeinfo& l, ClassFieldForTypeinfo& r ) : i32
	{
		return l.name <=> r.name;
	}
}

struct ClassFunctionForTypeinfo
{
	FunctionVariable function;
	ust::string8 name;
	ust::string8 mangled_name;

	op<=>( ClassFunctionForTypeinfo& l, ClassFunctionForTypeinfo& r ) : i32
	{
		return l.mangled_name <=> r.mangled_name;
	}
}

struct ClassInnerTypeForTypeinfo
{
	Type t;
	ust::string8 name;

	op<=>( ClassInnerTypeForTypeinfo& l, ClassInnerTypeForTypeinfo& r ) : i32
	{
		return l.name <=> r.name;
	}
}

struct EnumElementForTypeinfo
{
	LLVMValueRef value;
	ust::string8 name;

	op<=>( EnumElementForTypeinfo& l, EnumElementForTypeinfo& r ) : i32
	{
		return l.name <=> r.name;
	}
}

fn FinishTypeinfoList( LLVMContextRef llvm_context, ust::vector</Type/> mut element_types, ust::vector</LLVMValueRef/>& initializers ) : Variable
{
	var TupleType mut tuple_type{ .element_types= move(element_types) };

	var ust::vector</LLVMTypeRef/> mut element_llvm_types;
	foreach( &element_type : tuple_type.element_types )
	{
		element_llvm_types.push_back( element_type.GetLLVMType() );
	}

	tuple_type.llvm_type= unsafe( LLVMStructTypeInContext( llvm_context, element_llvm_types.data(), u32(element_llvm_types.size()), LLVMBool::False ) );

	var Variable mut variable
	{
		.t= move(tuple_type),
		.value_type= ValueType::Value,
		.location= Variable::Location::LLVMRegister,
	};

	variable.constexpr_value= unsafe( LLVMConstStructInContext( llvm_context, cast_mut(initializers).data(), u32(initializers.size()), LLVMBool::False ) );
	variable.llvm_value= variable.constexpr_value;

	return move(variable);
}

fn CodeBuilder::BuildTypeinfo( mut this, Type& t, NamesScopePtr& root_namespace ) : VariablePtr
{
	GetTypeinfoPrototype( t, root_namespace );
	return BuildFullTypeinfo( t );
}

fn CodeBuilder::GetTypeinfoPrototype( mut this, Type& t, NamesScopePtr& root_namespace ) : VariablePtr
{
	// Check if already created.
	if_var( &v : typeinfo_cache_.find(t) )
	{
		return v;
	}

	var ClassTypePtr typeinfo_class= CreateTypeinfoClass( root_namespace, t, g_typeinfo_root_class_name );

	var Variable mut v
	{
		.t= typeinfo_class,
		.value_type= ValueType::ReferenceImut,
		.location= Variable::Location::Pointer,
		.name= Type(typeinfo_class).ToString()
	};

	var ust::string8 mut variable_name = "_val_of_";
	variable_name+= mangler_.deref().MangleType(typeinfo_class);
	unsafe
	{
		auto llvm_type= v.t.GetLLVMType();
		v.constexpr_value= LLVMGetUndef( llvm_type ); // llvm::Constant is immutable, so, we can create real constant only if we build comple typeinfo.
		v.llvm_value= AddGlobalConstantVariable( variable_name, llvm_type, v.constexpr_value );
	}

	auto mut v_ptr= VariablePtr( move(v) );
	typeinfo_cache_.insert( t, v_ptr );

	// Add type as typedef into class members namespace.
	// This allows us to get typename itself, using typeinfo variable and use such type as normal.
	{
		auto class_members= typeinfo_class.lock_imut().deref().members;
		auto mut members_lock= class_members.lock_mut();

		auto mut src_type= t;

		// Replace function type alias with function pointer type alias to forbid usage of function type in user code.
		if_var( &function_type : t.GetFunctionType() )
		{
			var FunctionPointerType mut function_pointer_type{ .function_type= function_type };
			function_pointer_type.llvm_type= unsafe( LLVMPointerType( function_pointer_type.function_type.llvm_type, 0u ) );
			src_type= move(function_pointer_type);
		}

		members_lock.deref().AddName( "src_type", src_type );
	}

	return move(v_ptr);
}

fn CodeBuilder::BuildFullTypeinfo( mut this, Type& t ) : VariablePtr
{
	var VariablePtr typeinfo_variable_ptr= typeinfo_cache_.find(t).try_deref();

	var ClassTypePtr typeinfo_class= typeinfo_variable_ptr.lock_imut().deref().t.GetClassType().try_deref();
	if( typeinfo_class.lock_imut().deref().is_complete )
	{
		return typeinfo_variable_ptr;
	}

	if( !( EnsureTypeComplete( t ) || !t.GetFunctionType().empty() ) )
	{
		// Ignore incomplete types here, generate error in "typeinfo" operator evaluation instead.
		// TODO - what if user try to access incomplete member of incomplete typeinfo class?
		return typeinfo_variable_ptr;
	}

	var NamesScopeMutPtr class_members= typeinfo_class.lock_imut().deref().members;
	var NamesScopePtr root_namespace= class_members.lock_imut().deref().GetParent().try_to_non_nullable();

	var ust::shared_ptr_final</ Synt::ClassField /> dummy_syntax_element( Synt::ClassField() );

	var ust::vector</LLVMTypeRef/> mut fields_llvm_types;
	var ust::vector</LLVMValueRef/> mut fields_initializers;

	if( t.GetFunctionType().empty() )
	{
		var LLVMTypeRef llvm_type= t.GetLLVMType();

		// see llvm/lib/IR/DataLayout.cpp:40
		ADD_SIZE_FIELD( "size_of", LLVMABISizeOfType( data_layout_, llvm_type ) )
		ADD_SIZE_FIELD( "align_of", LLVMABIAlignmentOfType( data_layout_, llvm_type ) )
	}

	ADD_SIZE_FIELD( "references_tags_count", select( t.GetInnerReferenceKind() == InnerReferenceKind::None ? 0s : 1s ) )
	ADD_BOOL_FIELD( "contains_mutable_references", t.GetInnerReferenceKind() == InnerReferenceKind::Mut )

	ADD_BOOL_FIELD( "is_fundamental"     , !t.GetFundamentalType    ().empty() )
	ADD_BOOL_FIELD( "is_array"           , !t.GetArrayType          ().empty() )
	ADD_BOOL_FIELD( "is_tuple"           , !t.GetTupleType          ().empty() )
	ADD_BOOL_FIELD( "is_raw_pointer"     , !t.GetRawPointerType     ().empty() )
	ADD_BOOL_FIELD( "is_function"        , !t.GetFunctionType       ().empty() )
	ADD_BOOL_FIELD( "is_function_pointer", !t.GetFunctionPointerType().empty() )
	ADD_BOOL_FIELD( "is_class"           , !t.GetClassType          ().empty() )
	ADD_BOOL_FIELD( "is_enum"            , !t.GetEnumType           ().empty() )

	ADD_BOOL_FIELD( "is_default_constructible", t.IsDefaultConstructible() )
	ADD_BOOL_FIELD( "is_copy_constructible"   , t.IsCopyConstructible()    )
	ADD_BOOL_FIELD( "is_copy_assignable"      , t.IsCopyAssignable()       )
	ADD_BOOL_FIELD( "is_equality_comparable"  , t.IsEqualityComparable()   )

	if_var( &fundamental_type : t.GetFundamentalType() )
	{
		ADD_BOOL_FIELD( "is_integer"         , IsInteger        ( fundamental_type.fundamental_type ) )
		ADD_BOOL_FIELD( "is_numeric"         , IsNumericType    ( fundamental_type.fundamental_type ) )
		ADD_BOOL_FIELD( "is_signed_integer"  , IsSignedInteger  ( fundamental_type.fundamental_type ) )
		ADD_BOOL_FIELD( "is_unsigned_integer", IsUnsignedInteger( fundamental_type.fundamental_type ) )
		ADD_BOOL_FIELD( "is_float"           , IsFloatingPoint  ( fundamental_type.fundamental_type ) )
		ADD_BOOL_FIELD( "is_char"            , IsChar           ( fundamental_type.fundamental_type ) )
		ADD_BOOL_FIELD( "is_byte"            , IsByte           ( fundamental_type.fundamental_type ) )
		ADD_BOOL_FIELD( "is_bool"            , fundamental_type.fundamental_type == U_FundamentalType::bool_ )
		ADD_BOOL_FIELD( "is_void"            , fundamental_type.fundamental_type == U_FundamentalType::void_ )
	}
	if_var( &array_type : t.GetArrayType() )
	{
		ADD_TYPEINFO_FIELD( "element_type", array_type.element_type )
		ADD_SIZE_FIELD( "element_count", array_type.element_count )
	}
	if_var( &tuple_type : t.GetTupleType() )
	{
		ADD_SIZE_FIELD( "element_count", tuple_type.element_types.size() )
		ADD_LIST_FIELD( "elements_list", BuildTypeinfoTupleElementsList( root_namespace, tuple_type ) )
	}
	if_var( &pointer_type : t.GetRawPointerType() )
	{
		ADD_TYPEINFO_FIELD( "element_type", pointer_type.element_type )
	}
	if_var( &function_type : t.GetFunctionType() )
	{
		ADD_TYPEINFO_FIELD( "return_type", function_type.return_type )
		ADD_LIST_FIELD( "arguments_list", BuildTypeinfoFunctionParamsList( root_namespace, function_type ) )
		ADD_BOOL_FIELD( "return_value_is_reference", function_type.return_value_type != ValueType::Value )
		ADD_BOOL_FIELD( "return_value_is_mutable"  , function_type.return_value_type == ValueType::ReferenceMut )
		ADD_BOOL_FIELD( "unsafe"                   , function_type.is_unsafe )
		// SPRACHE_TODO - add also reference pollution.
	}
	if_var( &function_pointer_type : t.GetFunctionPointerType() )
	{
		ADD_TYPEINFO_FIELD( "element_type", function_pointer_type.function_type )
	}
	if_var( &class_ptr : t.GetClassType() )
	{
		auto lock= class_ptr.lock_imut();
		var ClassType& class_= lock.deref();

		var bool is_polymorph=
			class_.kind == ClassType::Kind::Interface ||
			class_.kind == ClassType::Kind::Abstract ||
			class_.kind == ClassType::Kind::PolymorphNonFinal ||
			class_.kind == ClassType::Kind::PolymorphFinal;

		var size_type mut field_count= 0s;
		foreach( &member : class_.members.lock_imut().deref() )
		{
			field_count+= select( member.value().Get</ClassField/>().empty() ? 0s : 1s );
		}
		ADD_SIZE_FIELD( "field_count", field_count )

		ADD_SIZE_FIELD( "parent_count", class_.parents.size() )

		ADD_LIST_FIELD( "fields_list", BuildTypeinfoClassFieldsList( root_namespace, class_ptr ) )
		ADD_LIST_FIELD( "functions_list", BuildTypeinfoClassFunctionsList( root_namespace, class_ptr ) )
		ADD_LIST_FIELD( "types_list", BuildTypeinfoClassTypesList( root_namespace, class_ptr ) )
		ADD_LIST_FIELD( "parents_list", BuildTypeinfoClassParentsList( root_namespace, class_ptr ) )

		if( is_polymorph )
		{
			var ClassField mut class_field
			{
				.t= size_type_,
				.is_mutable= false,
				.is_reference= true,
				.index= u32(fields_llvm_types.size()),
				.syntax_element= dummy_syntax_element,
				.source_class= typeinfo_class,
			};

			unsafe
			{
				auto dummy_function_context= CreateGlobalFunctionContext();
				var [ LLVMValueRef, 3 ] mut indices[ GetZeroGEPIndex(), GetZeroGEPIndex(), GetZeroGEPIndex() ];
				var LLVMValueRef address=
					LLVMBuildGEP2(
						dummy_function_context.llvm_ir_builder,
						LLVMTypeOf( LLVMGetInitializer(class_.polymorph_type_id_table) ),
						class_.polymorph_type_id_table,
						$<(indices[0]), 3u, g_null_string );
				fields_initializers.push_back( address );
				fields_llvm_types.push_back( LLVMPointerType( class_field.t.GetLLVMType(), 0u ) );
			}

			auto mut members_lock= class_members.lock_mut();
			members_lock.deref().AddName( "type_id", move(class_field) );
		}

		ADD_BOOL_FIELD( "is_struct", class_.kind == ClassType::Kind::Struct )
		ADD_BOOL_FIELD( "is_polymorph", is_polymorph )
		ADD_BOOL_FIELD( "is_final",
			class_.kind == ClassType::Kind::Struct ||
			class_.kind == ClassType::Kind::NonPolymorph ||
			class_.kind == ClassType::Kind::PolymorphFinal )
		ADD_BOOL_FIELD( "is_abstract",
			class_.kind == ClassType::Kind::Abstract ||
			class_.kind == ClassType::Kind::Interface )

		ADD_BOOL_FIELD( "is_interface", class_.kind == ClassType::Kind::Interface )

		ADD_BOOL_FIELD( "is_typeinfo", !class_.typeinfo_src_type_.empty() )
	}
	if_var( &enum_ptr : t.GetEnumType() )
	{
		auto lock= enum_ptr.lock_imut();
		var Enum& enum_= lock.deref();

		ADD_SIZE_FIELD( "element_count", enum_.elements.size() )
		ADD_TYPEINFO_FIELD( "underlaying_type", enum_.underlaying_type )
		ADD_LIST_FIELD( "elements_list", BuildTypeinfoEnumElementsList( root_namespace, enum_ptr ) )
	}

	FinishTypeinfoClass( typeinfo_class, fields_llvm_types );

	auto mut v_lock= typeinfo_variable_ptr.lock_mut();
	var Variable &mut v= v_lock.deref();
	unsafe
	{
		auto initializer= LLVMConstNamedStruct( v.t.GetLLVMType(), fields_initializers.data(), u32(fields_initializers.size()) );
		LLVMSetInitializer( v.llvm_value, initializer );

		v.constexpr_value= initializer;
	}

	return typeinfo_variable_ptr;
}

fn CodeBuilder::CreateTypeinfoClass( mut this, NamesScopePtr& root_namespace, Type& src_type, ust::string8& name ) : ClassTypePtr
{
	var NamesScopeMutPtr class_members( NamesScope( name, root_namespace ) );
	var ClassType mut class_type
	{
		.members= class_members,
		.members_initial= class_members,
		.typeinfo_src_type_(src_type),
		.can_be_constexpr= true,
	};

	var ClassTypePtr mut class_type_ptr( move(class_type) );

	{
		auto mut lock= class_members.lock_mut();

		// Create functions set for constructors/destructors/assignment operators.
		var NamesScopeFunctionsSet functions_set{ .class_= class_type_ptr };
		lock.deref().AddName( KeywordToString( Keyword::constructor_ ), functions_set );
		lock.deref().AddName( KeywordToString( Keyword::destructor_  ), functions_set );
		lock.deref().AddName( OverloadedOperatorToString( OverloadedOperator::Assign ), functions_set );

		// Add special member to names scope to identify it as class names scope.
		lock.deref().SetClass( class_type_ptr );
	}

	auto name_mangled= mangler_.deref().MangleType( class_type_ptr );
	{
		auto mut lock= class_type_ptr.lock_mut();
		lock.deref().llvm_type= unsafe( LLVMStructCreateNamed( llvm_context_, name_mangled.front() ) );
	}

	typeinfo_class_table_.push_back( class_type_ptr );

	return move(class_type_ptr);
}

fn CodeBuilder::FinishTypeinfoClass( mut this, ClassTypePtr& typeinfo_class, ust::vector</LLVMTypeRef/>& fields_llvm_types )
{
	{
		auto mut class_lock= typeinfo_class.lock_mut();
		var ClassType &mut class_= class_lock.deref();

		unsafe( LLVMStructSetBody( class_.llvm_type, cast_mut(fields_llvm_types).data(), u32(fields_llvm_types.size()), LLVMBool::False ) );

		class_.is_complete= true;
	}

	// Each class should have destructor, so, generate it even for typeinfo class.
	TryGenerateDestructorPrototype( typeinfo_class );
	TryGenerateDestructor( typeinfo_class );
}

fn CodeBuilder::BuildTypeinfoTupleElementsList( mut this, NamesScopePtr& root_namespace, TupleType& tuple_type ) : Variable
{
	var ust::vector</Type/> mut list_types;
	var ust::vector</LLVMValueRef/> mut list_initializers;

	var ust::shared_ptr_final</ Synt::ClassField /> dummy_syntax_element( Synt::ClassField() );

	for( auto mut i= 0s; i < tuple_type.element_types.size(); ++i )
	{
		var ClassTypePtr typeinfo_class= CreateTypeinfoClass( root_namespace, tuple_type, g_typeinfo_tuple_elements_list_node_class_name + ust::to_string8(i) );
		var NamesScopeMutPtr class_members= typeinfo_class.lock_imut().deref().members;

		var ust::vector</LLVMTypeRef/> mut fields_llvm_types;
		var ust::vector</LLVMValueRef/> mut fields_initializers;

		ADD_TYPEINFO_FIELD( "type", tuple_type.element_types[i] )

		ADD_SIZE_FIELD( "index", i )
		ADD_SIZE_FIELD( "offset", unsafe(LLVMOffsetOfElement( data_layout_, tuple_type.llvm_type, u32(i) )) )

		FinishTypeinfoClass( typeinfo_class, fields_llvm_types );

		list_types.push_back( typeinfo_class );
		list_initializers.push_back( unsafe( LLVMConstNamedStruct( typeinfo_class.lock_imut().deref().llvm_type, fields_initializers.data(), u32(fields_initializers.size()) ) ) );
	}

	return FinishTypeinfoList( llvm_context_, move(list_types), list_initializers );
}

fn CodeBuilder::BuildTypeinfoFunctionParamsList( mut this, NamesScopePtr& root_namespace, FunctionType& function_type ) : Variable
{
	var ust::vector</Type/> mut list_types;
	var ust::vector</LLVMValueRef/> mut list_initializers;

	var ust::shared_ptr_final</ Synt::ClassField /> dummy_syntax_element( Synt::ClassField() );

	for( auto mut i= 0s; i < function_type.params.size(); ++i )
	{
		var ClassTypePtr typeinfo_class= CreateTypeinfoClass( root_namespace, function_type, g_typeinfo_function_params_list_node_class_name + ust::to_string8(i) );
		var NamesScopeMutPtr class_members= typeinfo_class.lock_imut().deref().members;

		var ust::vector</LLVMTypeRef/> mut fields_llvm_types;
		var ust::vector</LLVMValueRef/> mut fields_initializers;

		auto& param= function_type.params[i];
		ADD_TYPEINFO_FIELD( "type", param.t )
		ADD_BOOL_FIELD( "is_reference", param.value_type != ValueType::Value )
		ADD_BOOL_FIELD( "is_mutable"  , param.value_type == ValueType::ReferenceMut )

		// SPRACHE_TODO - add reference pollution

		FinishTypeinfoClass( typeinfo_class, fields_llvm_types );

		list_types.push_back( typeinfo_class );
		list_initializers.push_back( unsafe( LLVMConstNamedStruct( typeinfo_class.lock_imut().deref().llvm_type, fields_initializers.data(), u32(fields_initializers.size()) ) ) );
	}

	return FinishTypeinfoList( llvm_context_, move(list_types), list_initializers );
}

fn CodeBuilder::BuildTypeinfoClassFieldsList( mut this, NamesScopePtr& root_namespace, ClassTypePtr& class_type ) : Variable
{
	var ust::vector</ ClassFieldForTypeinfo /> mut class_fields;
	{
		auto class_members_ptr= class_type.lock_imut().deref().members;
		auto class_members_lock= class_members_ptr.lock_imut();
		var NamesScope& class_members= class_members_lock.deref();

		foreach( &member : class_members )
		{
			auto field_ptr= member.value().Get</ClassField/>();
			if( !field_ptr.empty() )
			{
				var ClassFieldForTypeinfo mut f{ .class_field= field_ptr.try_to_non_nullable(), .name= member.key() };
				class_fields.push_back( move(f) );
			}
		}
	}

	// Sort class fields in typeinfo by name, because we need strong order.
	ust::sort( class_fields.range() );

	var ust::vector</Type/> mut list_types;
	var ust::vector</LLVMValueRef/> mut list_initializers;

	var ust::shared_ptr_final</ Synt::ClassField /> dummy_syntax_element( Synt::ClassField() );

	foreach( &class_field_for_typeinfo : class_fields )
	{
		auto field_lock= class_field_for_typeinfo.class_field.lock_imut();
		var ClassField& field= field_lock.deref();

		var ClassTypePtr typeinfo_class= CreateTypeinfoClass( root_namespace, class_type, g_typeinfo_class_fields_list_node_class_name + ust::to_string8(list_types.size()) );
		var NamesScopeMutPtr class_members= typeinfo_class.lock_imut().deref().members;

		var ust::vector</LLVMTypeRef/> mut fields_llvm_types;
		var ust::vector</LLVMValueRef/> mut fields_initializers;

		ADD_TYPEINFO_FIELD( "type", field.t )
		ADD_TYPEINFO_FIELD( "class_type", field.source_class )

		// Base class always have zero offset, so, we can just use offset of class field in its own class.
		auto field_source_class_llvm_type= field.source_class.lock_imut().deref().llvm_type;
		ADD_SIZE_FIELD( "offset", unsafe( LLVMOffsetOfElement( data_layout_, field_source_class_llvm_type, u32(field.index) ) ) )

		ADD_BOOL_FIELD( "is_mutable", field.is_mutable )
		ADD_BOOL_FIELD( "is_reference", field.is_reference )

		ADD_CLASS_MEMBER_COMMON_FIELDS( class_field_for_typeinfo.name )

		FinishTypeinfoClass( typeinfo_class, fields_llvm_types );

		list_types.push_back( typeinfo_class );
		list_initializers.push_back( unsafe( LLVMConstNamedStruct( typeinfo_class.lock_imut().deref().llvm_type, fields_initializers.data(), u32(fields_initializers.size()) ) ) );
	}

	return FinishTypeinfoList( llvm_context_, move(list_types), list_initializers );
}

fn CodeBuilder::BuildTypeinfoClassFunctionsList( mut this, NamesScopePtr& root_namespace, ClassTypePtr& class_type ) : Variable
{
	var ust::vector</ClassFunctionForTypeinfo/> mut functions_list;

	with( class_members_ptr : class_type.lock_imut().deref().members )
	{
		foreach( &class_member : class_members_ptr.lock_imut().deref() )
		{
			auto functions_set_ptr= class_member.value().Get</NamesScopeFunctionsSet/>();
			if( functions_set_ptr.empty() )
			{
				continue;
			}

			foreach( &function : functions_set_ptr.try_lock_imut().deref().functions_set.functions )
			{
				var ClassFunctionForTypeinfo mut f{ .function= function, .name= class_member.key() };

				unsafe
				{
					var size_type mut name_length= 0s;
					var $(char8) name_start= LLVMGetValueName2( function.llvm_function, name_length );

					if( name_length > 0s )
					{
						f.mangled_name.resize( name_length, "\0"c8 );
						ust::memory_copy( cast_ref_unsafe</byte8/>( f.mangled_name.front() ), cast_ref_unsafe</byte8/>( $>(name_start) ), name_length );
					}
				}

				functions_list.push_back( move(f) );
			}
		}
	}

	// Sort class functions in typeinfo by mangled name, because we need strong order.
	ust::sort( functions_list.range() );

	var ust::vector</Type/> mut list_types;
	var ust::vector</LLVMValueRef/> mut list_initializers;

	var ust::shared_ptr_final</ Synt::ClassField /> dummy_syntax_element( Synt::ClassField() );

	foreach( &class_function : functions_list)
	{
		var FunctionVariable& function= class_function.function;

		var ClassTypePtr typeinfo_class= CreateTypeinfoClass( root_namespace, class_type, g_typeinfo_class_functions_list_node_class_name + ust::to_string8(list_types.size()) );
		var NamesScopeMutPtr class_members= typeinfo_class.lock_imut().deref().members;

		var ust::vector</LLVMTypeRef/> mut fields_llvm_types;
		var ust::vector</LLVMValueRef/> mut fields_initializers;

		ADD_TYPEINFO_FIELD( "type", function.t )

		ADD_BOOL_FIELD( "is_this_call" , function.is_this_call )
		ADD_BOOL_FIELD( "is_generated" , function.is_generated )
		ADD_BOOL_FIELD( "is_deleted"   , function.is_deleted   )
		ADD_BOOL_FIELD( "is_virtual"   , function.virtual_table_index != ~0u )

		ADD_CLASS_MEMBER_COMMON_FIELDS( class_function.name )

		FinishTypeinfoClass( typeinfo_class, fields_llvm_types );

		list_types.push_back( typeinfo_class );
		list_initializers.push_back( unsafe( LLVMConstNamedStruct( typeinfo_class.lock_imut().deref().llvm_type, fields_initializers.data(), u32(fields_initializers.size()) ) ) );
	}

	return FinishTypeinfoList( llvm_context_, move(list_types), list_initializers );
}

fn CodeBuilder::BuildTypeinfoClassTypesList( mut this, NamesScopePtr& root_namespace, ClassTypePtr& class_type ) : Variable
{
	var ust::vector</ClassInnerTypeForTypeinfo/> mut class_inner_types;
	{
		auto class_members_ptr= class_type.lock_imut().deref().members;
		auto class_members_lock= class_members_ptr.lock_imut();
		var NamesScope& class_members= class_members_lock.deref();

		foreach( &class_member : class_members)
		{
			var Type mut t;

			auto type_ptr= class_member.value().Get</Type/>();
			auto type_alias_ptr= class_member.value().Get</TypeAlias/>();
			auto class_ptr= class_member.value().Get</ClassType/>();
			auto enum_ptr= class_member.value().Get</Enum/>();
			if( !type_ptr.empty() )
			{
				t= type_ptr.try_lock_imut().deref();
			}
			else if( !type_alias_ptr.empty() )
			{
				var ust::optional</ Type /> inner_type= type_alias_ptr.try_lock_imut().deref().t;
				if( inner_type.empty() ) { continue; }// WTF?
				t= inner_type.try_deref();
			}
			else if( !class_ptr.empty() )
			{
				t= class_ptr.try_to_non_nullable();
			}
			else if( !enum_ptr.empty() )
			{
				t= enum_ptr.try_to_non_nullable();
			}
			else
			{
				continue;
			}

			var ClassInnerTypeForTypeinfo mut inner_type{ .t= move(t), .name= class_member.key() };
			class_inner_types.push_back( move(inner_type) );
		}
	}

	// Sort class types in typeinfo by name, because we need strong order.
	ust::sort( class_inner_types.range() );

	var ust::vector</Type/> mut list_types;
	var ust::vector</LLVMValueRef/> mut list_initializers;

	var ust::shared_ptr_final</ Synt::ClassField /> dummy_syntax_element( Synt::ClassField() );

	foreach( &inner_type : class_inner_types )
	{
		var ClassTypePtr typeinfo_class= CreateTypeinfoClass( root_namespace, class_type, g_typeinfo_class_types_list_node_class_name + ust::to_string8(list_types.size()) );
		var NamesScopeMutPtr class_members= typeinfo_class.lock_imut().deref().members;

		var ust::vector</LLVMTypeRef/> mut fields_llvm_types;
		var ust::vector</LLVMValueRef/> mut fields_initializers;

		ADD_TYPEINFO_FIELD( "type", inner_type.t )

		ADD_CLASS_MEMBER_COMMON_FIELDS( inner_type.name )

		FinishTypeinfoClass( typeinfo_class, fields_llvm_types );

		list_types.push_back( typeinfo_class );
		list_initializers.push_back( unsafe( LLVMConstNamedStruct( typeinfo_class.lock_imut().deref().llvm_type, fields_initializers.data(), u32(fields_initializers.size()) ) ) );
	}

	return FinishTypeinfoList( llvm_context_, move(list_types), list_initializers );
}

fn CodeBuilder::BuildTypeinfoClassParentsList( mut this, NamesScopePtr& root_namespace, ClassTypePtr& class_type ) : Variable
{
	var ust::vector</Type/> mut list_types;
	var ust::vector</LLVMValueRef/> mut list_initializers;

	var ust::shared_ptr_final</ Synt::ClassField /> dummy_syntax_element( Synt::ClassField() );

	auto class_lock= class_type.lock_imut();
	var ClassType& class_= class_lock.deref();
	foreach( &parent : class_.parents )
	{
		var ClassTypePtr typeinfo_class= CreateTypeinfoClass( root_namespace, class_type, g_typeinfo_class_parents_list_node_class_name + ust::to_string8(list_types.size()) );
		var NamesScopeMutPtr class_members= typeinfo_class.lock_imut().deref().members;

		var ust::vector</LLVMTypeRef/> mut fields_llvm_types;
		var ust::vector</LLVMValueRef/> mut fields_initializers;

		ADD_TYPEINFO_FIELD( "type", parent.class_ )

		ADD_SIZE_FIELD( "offset", unsafe( LLVMOffsetOfElement( data_layout_, class_.llvm_type, parent.field_number ) ) )

		FinishTypeinfoClass( typeinfo_class, fields_llvm_types );

		list_types.push_back( typeinfo_class );
		list_initializers.push_back( unsafe( LLVMConstNamedStruct( typeinfo_class.lock_imut().deref().llvm_type, fields_initializers.data(), u32(fields_initializers.size()) ) ) );
	}

	return FinishTypeinfoList( llvm_context_, move(list_types), list_initializers );
}

fn CodeBuilder::BuildTypeinfoEnumElementsList( mut this, NamesScopePtr& root_namespace, EnumTypePtr& enum_type ) : Variable
{
	auto enum_lock= enum_type.lock_imut();
	var Enum& enum_= enum_lock.deref();

	var ust::vector</EnumElementForTypeinfo/> mut enum_elemens_list;

	foreach( &enum_element : enum_.elements )
	{
		var EnumElementForTypeinfo mut el
		{
			.value= enum_element.value().constexpr_value,
			.name= enum_element.key()
		};

		enum_elemens_list.push_back( move(el) );
	}

	// Sort enum elements in typeinfo by name, because we need strong order.
	ust::sort( enum_elemens_list.range() );

	var ust::vector</Type/> mut list_types;
	var ust::vector</LLVMValueRef/> mut list_initializers;

	var ust::shared_ptr_final</ Synt::ClassField /> dummy_syntax_element( Synt::ClassField() );

	foreach( &enum_element : enum_elemens_list )
	{
		var ClassTypePtr typeinfo_class= CreateTypeinfoClass( root_namespace, enum_type, g_typeinfo_enum_elements_list_node_class_name + ust::to_string8(list_types.size()) );
		var NamesScopeMutPtr class_members= typeinfo_class.lock_imut().deref().members;

		var ust::vector</LLVMTypeRef/> mut fields_llvm_types;
		var ust::vector</LLVMValueRef/> mut fields_initializers;

		ADD_NAME_FIELD( enum_element.name )

		{
			var ClassField mut class_field
			{
				.t= enum_.underlaying_type,
				.is_mutable= true,
				.is_reference= false,
				.index= u32(fields_llvm_types.size()),
				.syntax_element= dummy_syntax_element,
				.source_class= typeinfo_class,
			};

			fields_llvm_types.push_back( class_field.t.GetLLVMType() );
			fields_initializers.push_back( enum_element.value );

			auto mut members_lock= class_members.lock_mut();
			members_lock.deref().AddName( "value", move(class_field) );
		}

		FinishTypeinfoClass( typeinfo_class, fields_llvm_types );

		list_types.push_back( typeinfo_class );
		list_initializers.push_back( unsafe( LLVMConstNamedStruct( typeinfo_class.lock_imut().deref().llvm_type, fields_initializers.data(), u32(fields_initializers.size()) ) ) );
	}

	return FinishTypeinfoList( llvm_context_, move(list_types), list_initializers );
}

} // namespace U1
