import "/atomic.u"
import "error_reporting.uh"
import "references_graph.uh"

namespace U1
{

// Use global unique identifiers for bugs prevention.
var u32 mut g_references_graph_next_node_id = 1u;

fn GetNextNodeId() : u32
{
	unsafe{  return ust::atomic_inc(g_references_graph_next_node_id);  }
}

struct NodeReplacement
{
	ReferencesGraph::NodeId first;
	ReferencesGraph::NodeId second;
}

op ReferencesGraph::Link::==(Link& l, Link& r ) : bool
{
	return l.src == r.src && l.dst == r.dst;
}

fn ReferencesGraph::AddNode( mut this, NodeKind kind, ust::string8 mut name ) : NodeId
{
	var NodeId node_id= GetNextNodeId();

	var Node mut node{ .kind= kind, .name= move(name), .inner_reference_node= c_empty_node_id, .moved= false };
	nodes_.insert( node_id, move(node) );

	return node_id;
}

fn ReferencesGraph::RemoveNode( mut this, NodeId node_id )
{
	if( nodes_.find( node_id ).empty() )
	{
		return;
	}

	var NodeId inner_reference_node_id= nodes_[node_id].inner_reference_node;
	RemoveNode( inner_reference_node_id );

	RemoveNodeLinks( node_id );

	// Erase node.
	nodes_.erase( node_id );
}

fn ReferencesGraph::AddLink( mut this, NodeId from, NodeId to )
{
	if( from == to )
	{
		return;
	}

	var Link link{ .src= from, .dst= to };

	foreach( &prev_link : links_ )
	{
		if( link == prev_link )
		{
			return;
		}
	}
	links_.push_back( link );
}

fn ReferencesGraph::TryAddLink( mut this, NodeId from, NodeId to ) : bool
{
	if( ( GetNodeKind( to ) == NodeKind::ReferenceMut && HaveOutgoingLinks( from ) ) ||
		HaveOutgoingMutableNodes( from ) )
	{
		return false;
	}

	AddLink( from, to );
	return true;
}

fn ReferencesGraph::CreateNodeInnerReference( mut this, NodeId node_id, NodeKind kind ) : NodeId
{
	halt if( kind == NodeKind::Variable );

	var ust::string8 mut node_name= nodes_[node_id].name + " inner reference";
	var NodeId inner_node_id= AddNode( kind, move(node_name) );

	var Node &mut dst_node= nodes_[node_id];
	halt if( dst_node.inner_reference_node != c_empty_node_id );
	dst_node.inner_reference_node= inner_node_id;

	return inner_node_id;
}

fn ReferencesGraph::MoveNode( mut this, NodeId node_id )
{
	var NodeId mut inner_reference_node_id= c_empty_node_id;
	{
		var Node &mut node= nodes_[node_id];
		halt if(node.moved);
		node.moved= true;

		inner_reference_node_id= node.inner_reference_node;
		node.inner_reference_node= c_empty_node_id;
	}

	RemoveNode( inner_reference_node_id );
	RemoveNodeLinks( node_id );
}

fn ReferencesGraph::NodeIsMoved( this, NodeId node_id ) : bool
{
	return nodes_[node_id].moved;
}

fn ReferencesGraph::HaveOutgoingLinks( this, NodeId from ) : bool
{
	foreach( &link : links_ )
	{
		if( link.src == from )
		{
			return true;
		}
	}
	return false;
}

fn ReferencesGraph::HaveOutgoingMutableNodes( this, NodeId from ) : bool
{
	foreach( &link : links_ )
	{
		if( link.src == from )
		{
			if_var( &node : nodes_.find( link.dst ) )
			{
				if( node.kind == NodeKind::ReferenceMut )
				{
					return true;
				}
			}
		}
	}
	return false;
}

fn ReferencesGraph::GetNodeKind( this, NodeId node_id ) : NodeKind
{
	return nodes_[node_id].kind;
}

fn ReferencesGraph::GetNodeName( this, NodeId node_id ) : ust::string8&
{
	return nodes_[node_id].name;
}

fn ReferencesGraph::GetNodeInnerReference( this, NodeId node_id ) : NodeId
{
	return nodes_[node_id].inner_reference_node;
}

fn ReferencesGraph::GetAllAccessibleVariableNodes( this, NodeId node_id ) : NodesSet
{
	var NodesSet mut nodes_set;
	GetAllAccessibleVariableNodes_r( node_id, nodes_set );
	return move(nodes_set);
}

fn ReferencesGraph::GetAccessibleVariableNodesInnerReferences( this, NodeId node_id ) : NodesSet
{
	var NodesSet mut nodes_set;
	GetAccessibleVariableNodesInnerReferences_r( node_id, nodes_set );
	return move(nodes_set);
}

fn ReferencesGraph::MergeReferencesGraphsAfterConditionalBranches( ust::array_view_imut</ReferencesGraph/> branches,  ErrorsContainer &mut errors, SrcLoc& src_loc ) : ReferencesGraph
{
	var ReferencesGraph mut result;

	var ust::vector</NodeReplacement/> mut replaced_nodes;
	foreach( &branch_state : branches )
	{
		foreach( &node_pair : branch_state.nodes_ )
		{
			var NodeId node_id= node_pair.key();
			var Node& src_node= node_pair.value();

			if( result.nodes_.find( node_id ).empty() )
			{
				result.nodes_.insert( node_id, src_node );
			}

			var Node &mut dst_node= result.nodes_[ node_id ];
			if( src_node.moved != dst_node.moved )
			{
				errors.push_back( ReportError( CodeBuilderErrorCode::ConditionalMove, src_loc, src_node.name ) );
			}

			if( src_node.inner_reference_node == dst_node.inner_reference_node )
			{} // ok - same nodes.
			else if( dst_node.inner_reference_node == c_empty_node_id )
			{  // ok - dst have no node, take node from src.
				dst_node.inner_reference_node= src_node.inner_reference_node;
			}
			else if( src_node.inner_reference_node == c_empty_node_id )
			{} // ok - src have no node.
			else
			{
				// Node created in both branches.
				// Take mutable node and replace it with immutable node.
				if( branch_state.nodes_[ src_node.inner_reference_node ].kind == NodeKind::ReferenceMut )
				{
					var NodeReplacement mut replacement{ .first= dst_node.inner_reference_node, .second= src_node.inner_reference_node };
					dst_node.inner_reference_node= src_node.inner_reference_node;
					replaced_nodes.push_back( move(replacement) );
				}
				else
				{
					var NodeReplacement mut replacement{ .first= src_node.inner_reference_node, .second= dst_node.inner_reference_node };
					replaced_nodes.push_back( move(replacement) );
				}
			}
		}

		foreach( &src_link : branch_state.links_ )
		{
			var bool mut found= false;
			foreach( &dst_link : result.links_ )
			{
				if( src_link == dst_link )
				{
					found= true;
					break;
				}
			}

			if( !found )
			{
				result.links_.push_back( src_link );
			}
		}
	}

	if( !replaced_nodes.empty() )
	{
		var ust::vector</Link/> mut links_corrected;
		foreach( mut link : result.links_ )
		{
			foreach( &replacement : replaced_nodes )
			{
				if( link.src == replacement.first )
				{
					link.src = replacement.second;
				}
				if( link.dst == replacement.first )
				{
					link.dst= replacement.second;
				}
			}

			var bool mut found= false;
			foreach( &prev_link : links_corrected )
			{
				if( link == prev_link )
				{
					found= true;
					break;
				}
			}

			if( !found )
			{
				links_corrected.push_back( link );
			}
		}

		result.links_= move(links_corrected);
	}

	foreach( &replacement : replaced_nodes )
	{
		if( !result.nodes_.find( replacement.first ).empty() )
		{
			result.nodes_.erase( replacement.first );
		}
	}

	// Check mutable reference count correctness.
	foreach( &node_pair : result.nodes_ )
	{
		var size_type mut mutable_links_count= 0s, mut immutable_links_count= 0s;
		foreach( &link : result.links_ )
		{
			if( link.src == node_pair.key() )
			{
				++select(
					cast_imut(result).nodes_[link.dst].kind == NodeKind::ReferenceMut
						? mutable_links_count
						: immutable_links_count );
			}
		}
		if( mutable_links_count > 1s || ( immutable_links_count > 0s && mutable_links_count > 0s ) )
		{
			errors.push_back( ReportError( CodeBuilderErrorCode::ReferenceProtectionError, src_loc, node_pair.value().name ) );
		}
	}

	return move(result);
}

fn ReferencesGraph::CheckReferencesGraphAfterLoop( ReferencesGraph& state_before, ReferencesGraph& state_after, ErrorsContainer &mut errors, SrcLoc& src_loc )
{
	foreach( &node_before_pair : state_before.nodes_ )
	{
		var Node& node_before= node_before_pair.value();
		var Node& node_after= state_after.nodes_[node_before_pair.key()];

		if( !node_before.moved && node_after.moved )
		{
			errors.push_back( ReportError( CodeBuilderErrorCode::OuterVariableMoveInsideLoop, src_loc, node_before.name ) );
		}

		if( node_before.inner_reference_node == c_empty_node_id && node_after.inner_reference_node  == c_empty_node_id )
		{
			continue;
		}

		// TODO - what if inner variable node changed? What if links removed?
		var NodesSet mut new_accessible_variables= state_after.GetAllAccessibleVariableNodes( node_after.inner_reference_node );
		if( node_before.inner_reference_node != c_empty_node_id )
		{
			foreach( variable_node_id : state_before.GetAllAccessibleVariableNodes( node_before.inner_reference_node ) )
			{
				if( new_accessible_variables.exists(variable_node_id) )
				{
					new_accessible_variables.drop( variable_node_id );
				}
			}
		}

		foreach( added_node_id : new_accessible_variables )
		{
			errors.push_back(
				ReportError(
					CodeBuilderErrorCode::ReferencePollutionOfOuterLoopVariable,
					src_loc,
					node_before.name,
					state_before.nodes_[added_node_id].name ) );
		}
	}
}

fn ReferencesGraph::GetAllAccessibleVariableNodes_r( this, NodeId node_id, NodesSet &mut out_nodes )
{
	if( nodes_[node_id].kind == NodeKind::Variable )
	{
		out_nodes.insert( node_id );
	}

	foreach( &link : links_ )
	{
		if( link.dst == node_id )
		{
			GetAllAccessibleVariableNodes_r( link.src, out_nodes );
		}
	}
}

fn ReferencesGraph::GetAccessibleVariableNodesInnerReferences_r( this, NodeId node_id, NodesSet &mut out_nodes )
{
	auto inner_node= nodes_[node_id].inner_reference_node;
	if( inner_node != c_empty_node_id )
	{
		out_nodes.insert( inner_node );
		return;
	}

	foreach( &link : links_ )
	{
		if( link.dst == node_id )
		{
			GetAccessibleVariableNodesInnerReferences_r( link.src, out_nodes );
		}
	}
}

fn ReferencesGraph::RemoveNodeLinks( mut this, NodeId node_id )
{
	// Collect in/out nodes.
	var ust::vector</NodeId/> mut in_nodes, mut out_nodes;
	foreach( &link : links_ )
	{
		if( link.src == link.dst ){ continue; } // Self-loop link.

		if( link.src == node_id ) { out_nodes.push_back( link.dst ); }
		if( link.dst == node_id ) {  in_nodes.push_back( link.src ); }
	}

	// Remove links.
	for( auto mut i= 0s; i < links_.size(); )
	{
		if( links_[i].src == node_id || links_[i].dst == node_id )
		{
			auto back= links_.size() - 1s;
			links_.swap( i, back );
			links_.pop_back();
		}
		else
		{
			++i;
		}
	}

	// Create new links.
	foreach( from : in_nodes )
	{
		foreach( to : out_nodes )
		{
			AddLink( from, to );
		}
	}
}

} // namespace U1
