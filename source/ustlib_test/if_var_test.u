//##success_test
import "../ustlib/box_nullable.u"
import "../ustlib/optional.u"

type IntBox= ust::box_nullable</i32/>;
type IntOpt= ust::optional</i32/>;

fn nomangle main() : i32
{
	{
		var IntBox box(66);
		var bool mut has_value= false;
		if_var( &value : box )
		{
			has_value= true;
			halt if( value != 66 );
		}
		else { halt; }
		halt if( !has_value );
	}
	{
		var IntBox mut box(5556);
		var bool mut has_value= false;
		if_var( mut value : box )
		{
			has_value= true;
			halt if( value != 5556 );
			value= 0; // Must modify copy of value
		}
		halt if( !has_value );
		halt if( box.try_deref() != 5556 );
	}
	{
		var IntBox mut box(-14);
		var bool mut has_value= false;
		if_var( &mut value : box )
		{
			has_value= true;
			halt if( value != -14 );
			value= 741; // Must modify content of box
		}
		halt if( !has_value );
		halt if( box.try_deref() != 741 );
	}
	{
		var IntBox box;
		if_var( value : box )
		{
			ust::ignore_unused(value);
			halt; // Must not be here, if box is empty.
		}
	}
	{
		var IntBox mut box( 66 );
		box.reset();
		if_var( value : box )
		{
			ust::ignore_unused(value);
			halt; // Must not be here, if box is empty.
		}
	}

	{
		var IntOpt opt(66);
		var bool mut has_value= false;
		if_var( &value : opt )
		{
			has_value= true;
			halt if( value != 66 );
		}
		halt if( !has_value );
	}
	{
		var IntOpt mut opt(5556);
		var bool mut has_value= false;
		if_var( mut value : opt )
		{
			has_value= true;
			halt if( value != 5556 );
			value= 0; // Must modify copy of value
		}
		halt if( !has_value );
		halt if( opt.try_deref() != 5556 );
	}
	{
		var IntOpt mut opt(-14);
		var bool mut has_value= false;
		if_var( &mut value : opt )
		{
			has_value= true;
			halt if( value != -14 );
			value= 741; // Must modify content of box
		}
		else { halt; }
		halt if( !has_value );
		halt if( opt.try_deref() != 741 );
	}
	{
		var IntOpt opt;
		if_var( value : opt )
		{
			ust::ignore_unused(value);
			halt; // Must not be here, if opt is empty.
		}
	}
	{
		var IntOpt mut opt( 66 );
		opt.reset();
		if_var( value : opt )
		{
			ust::ignore_unused(value);
			halt; // Must not be here, if opt is empty.
		}
	}

	return 0;
}
