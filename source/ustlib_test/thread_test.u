//##success_test
import "../ustlib/thread.u"

fn Bar(){}

struct CallableObject
{
	i32 &mut x;
	op()( mut this )
	{
		++x;
	}
}

fn Get42() : i32
{
	return 42;
}

fn U_Main() : i32
{
	{
		// Can create thread for function.
		auto thread= ust::thread_create( (fn())(Bar) );
	}
	{
		// Can create thread for callable object.
		var i32 mut x= 0, mut y= 0;
		var CallableObject mut obj{ .x= x };

		{
			auto thread= ust::thread_create( move(obj) );
			// While thread is alive, we can do something.
			++y;
		}
		halt if( x != 1 );
		halt if( y != 1 );
	}
	{
		// Can het result of thread.
		var ust::thread_ret</ fn() : i32, i32 /> mut thread( (fn() : i32 )( Get42 ) );
		var i32 res= thread.join( move(thread) );
		halt if( res != 42 );
	}

	return 0;
}
