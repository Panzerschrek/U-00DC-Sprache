//##success_test
import "../ustlib/thread.u"
import "../ustlib/shared_mt/shared_ptr_mt_mut.u"
import "../ustlib/vector.u"

fn Bar(){}

type IntShared= ust::shared_ptr_mt_mut</i32/>;

struct CallableObject
{
	IntShared x;
	op()( mut this )
	{
		auto mut l= x.lock_mut();
		++l.get_ref();
	}
}

fn Get42() : i32
{
	return 42;
}

struct Incrementer
{
	IntShared ptr;
	op()( mut this )
	{
		auto mut lock= ptr.lock_mut();
		++lock.get_ref();
	}
}

fn nomangle main() : i32
{
	{
		// Can create thread for function.
		auto thread= ust::thread_create( (fn())(Bar) );
	}
	{
		// Can create thread for callable object.
		var IntShared x(0);
		var i32 mut y= 0;
		var CallableObject mut obj{ .x= x };

		{
			auto thread= ust::thread_create( move(obj) );
			// While thread is alive, we can do something.
			++y;
		}
		halt if( x.lock_imut().get_ref() != 1 );
		halt if( y != 1 );
	}
	{
		// Can get result of thread.
		var ust::thread_ret</ fn() : i32, i32 /> mut thread( (fn() : i32 )( Get42 ) );
		var i32 res= thread.join( move(thread) );
		halt if( res != 42 );
	}
	{
		var IntShared ptr(0);
		var size_type mut i(0);
		var ust::vector</ ust::thread</Incrementer/> /> mut threads;
		while( i < size_type(64) )
		{
			var Incrementer incrementer{ .ptr= ptr };
			threads.push_back( ust::thread_create(incrementer) );
			++i;
		}

		while( ptr.lock_imut().get_ref() < 4 ) {} // Wait a bit.

		threads.clear(); // Make join here.
		halt if( ptr.lock_imut().get_ref() != 64 );
	}

	return 0;
}
