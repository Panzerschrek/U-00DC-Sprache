import "/string.u"
import "/variant.u"
import "/vector.u"

namespace BK
{

enum BuildTargetType
{
	// Normal executable, which can be launched.
	Executable,
	// Ü library, which can be used by other targets.
	Library,
	// Native shared library, ".so" or ".dll".
	SharedLibrary,
	// Object file, which may be used by a native linker to link result binary file.
	ObjectFile,
}

struct BuildTarget
{
	// Type of the build target. This should be specified explicitly.
	BuildTargetType target_type;

	// Name of a build target.
	// Empty names, names with some special characters and path separators aren't allowed.
	ust::string8 name;

	// List of source files of this build target. Order is irrelevant.
	// Containts paths relative to the package source directory (where the build script is located).
	// Paths with "." and ".." aren't allowed.
	// Duplicates aren't allowed.
	ust::vector</ust::string8/> source_files;

	// List of generated source files of this build target. Order is irrelevant.
	// Contains paths relative to the generated sources directory of this build target.
	// Paths with "." and ".." aren't allowed.
	// Duplicates aren't allowed.
	// For each generated source file there should be a custom build step which generates it.
	ust::vector</ust::string8/> generated_source_files;

	// List of generated private header files of this build target. Order is irrelevant.
	// Contains paths relative to the generated sources directory of this build target.
	// Paths with "." and ".." aren't allowed.
	// Duplicates aren't allowed.
	// For each generated private header file there should be a custom build step which generates it.
	// Specifying generated headers is necessary
	// in order to perform their generation before compiling source files which use them.
	ust::vector</ust::string8/> generated_private_header_files;

	// Publuc include directories of this build target.
	// They are used in this target and in all build targets dependent on it.
	// Imports like
	//    import "some_target/some_file.uh"
	// will lead to these directories - some_file.uh will be searched within them.
	// Paths with "." and ".." aren't allowed.
	// Duplicates aren't allowed.
	ust::vector</ust::string8/> public_include_directories;

	// List of dependencies, which should be also dependencies of other build targets dependent on this build target.
	ust::vector</ DependencyName /> public_dependencies;

	// List of dependencies of this build target, which are used internally.
	ust::vector</ DependencyName /> private_dependencies;

	// Contains names or paths of external libraries to link, like system libraries or libraries built outside Ü built system.
	ust::vector</ust::string8/> external_libraries;
}

struct DependencyName
{
	// A package where dependency is located.
	PackageName package_name;
	// Name of dependency. Should be a name of a build target within package specified.
	ust::string8 name;
}

// Package name - used for package dependencies and target names.
struct PackageName
{
	// Marker of case where this is name relative to current package.
	struct CurrentPackage{}

	// Base of package name (where to search it).
	ust::variant</ tup[ CurrentPackage, GlobalVersionedPackageName ] /> name_base;
	// Path starting from base.
	ust::string8 path;
}

// A package from global packages directory/centralized repository, with version specified.
struct GlobalVersionedPackageName
{
	ust::string8 name;
	SemanticVersion version;

	op<=>( GlobalVersionedPackageName& l, GlobalVersionedPackageName& r ) : i32
	{
		if( l.name != r.name ) { return l.name <=> r.name; }
		return l.version <=> r.version;
	}
}

struct SemanticVersion
{
	u32 major;
	u32 minor;
	u32 patch;
	u32 tweak;

	// Construct zero version.
	fn constructor()
		( major= 0u, minor= 0u, patch= 0u, tweak= 0u )
	{}

	// Major and minor numbers must be specified. Other are zero by default.

	fn constructor( u32 in_major, u32 in_minor )
		( major= in_major, minor= in_minor, patch= 0u, tweak= 0u )
	{}

	fn constructor( u32 in_major, u32 in_minor, u32 in_patch )
		( major= in_major, minor= in_minor, patch= in_patch, tweak= 0u )
	{}

	fn constructor( u32 in_major, u32 in_minor, u32 in_patch, u32 in_tweak )
		( major= in_major, minor= in_minor, patch= in_patch, tweak= in_tweak )
	{}

	// Order compate for semantic version.
	op<=>( SemanticVersion& l, SemanticVersion& r ) : i32
	{
		if( l.major != r.major ) { return l.major <=> r.major; }
		if( l.minor != r.minor ) { return l.minor <=> r.minor; }
		if( l.tweak != r.tweak ) { return l.tweak <=> r.tweak; }
		return l.patch <=> r.patch;
	}
}

// Dependency description for a package.
struct PackageDependency
{
	enum RequiredSystem
	{
		Target,
		Host,
		Both,
	}

	// Which package to depend on.
	PackageName name;
	// How to depend on this package.
	RequiredSystem required_system;

	// Construct from package name - with required system equal to target.
	fn conversion_constructor( PackageName mut in_name )
		( name= move(in_name), required_system= RequiredSystem::Target )
	{}

	// Construct with both params.
	fn constructor( PackageName mut in_name, RequiredSystem in_required_system )
		( name= move(in_name), required_system= in_required_system )
	{}
}

// Custom build step - for producing a file (or several files), possibly based on another files.
struct CustomBuildStep
{
	// An executable to run.
	struct ExeCommand
	{
		// Path to the executable.
		ust::string8 exe_path;
		// Command line arguments.
		ust::vector</ust::string8/> command_line;
	}

	// Copy file from one place to another.
	struct CopyCommand
	{
		ust::string8 destination_path;
		ust::string8 source_path;
	}

	// Generate file with contents specified.
	// This command is designed to generate relative small files,
	// if result file contents is too big, prefer using external command instead.
	struct GenerateFileCommand
	{
		ust::string8 destination_path;
		// TODO - support here binary contents.
		ust::string8 contents;
	}

	// List of full paths to input files.
	ust::vector</ust::string8/> input_files;
	// List of full paths to output files.
	ust::vector</ust::string8/> output_files;

	// What to execute.
	ust::variant</ tup[ ExeCommand, CopyCommand, GenerateFileCommand ] /> command;

	// What to display during the build.
	ust::string8 comment;

	// TODO - support dep-files.
}

struct PackageInfo
{
	// Build targets of a package. Order is irrelevant.
	ust::vector</BuildTarget/> build_targets;

	// Custom build steps.
	ust::vector</CustomBuildStep/> custom_build_steps;

	// Dependencies of this package.
	ust::vector</PackageDependency/> package_dependencies;
}

// Signature of a function that should return package info.
// Returns empty optional in case of error.
type GetPackageInfoFunctionType = fn( BuildSystemInterface &mut build_system_interface ) : ust::optional</PackageInfo/>;

// A class which provides necessary information for "GetPackageInfo" function and also contains some helper functions.
// Use inheritance to avoid exposing build system internals to the build script.
//
// Also it helps to import necessary build functions into the build script shared library.
// Otherwise it would be necessary to mark each exported from the build system executable function as "dllexport" or "visibility(default)".
// Making all functions virtual and thus forcing call via the virtual table solves this problem.
class BuildSystemInterface interface
{
	fn virtual pure GetBuildConfiguration(this) : BuildConfiguration;
	fn virtual pure GetTargetTriple(this) : TargetTriple;

	fn virtual pure LogVerbose( mut this, ust::string_view8 message );
	fn virtual pure LogInfo( mut this, ust::string_view8 message );
	fn virtual pure LogError( mut this, ust::string_view8 message );

	// Get value for given configuration option.
	// Options are specified via a configuration file provided as build system command line option.
	// Returns empty ust::optional if option with given name wasn't specified.
	// Both option names and values are case-sensetive.
	// Non-string options are not supported.
	fn virtual pure GetConfigurationOption( this, ust::string_view8 name ) : ust::optional</ust::string8/>;

	// Get absolute path to the root source directory of current package.
	fn virtual pure GetCurrentPackageSourceDirectory( this ) : ust::string8;

	// Get absolute path to the build directory of current package for current target triple and build configuration.
	fn virtual pure GetCurrentPackageBuildDirectory( this ) : ust::string8;

	// Get absolute path to the generated sources directory for a build target of current package.
	fn virtual pure GetCurrentPackageBuildTargetGeneratedSorucesDirectory( this, ust::string_view8 build_target_name ) : ust::string8;
}

enum BuildConfiguration
{
	Debug,
	Release,
	// TODO - add other configurations
}

struct TargetTriple
{
	// Components are all lowercase strings.
	ust::string8 architecture;
	ust::string8 vendor;
	ust::string8 operating_system;
	ust::string8 environment;
}

} // namespace BK
