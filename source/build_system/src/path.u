import "path.uh"

namespace BK
{

fn SplitPathIntoComponents( ust::filesystem_path_view path )
	: ust::vector</ust::filesystem_path_view/> @( ust::reference_notation::return_inner_references::param0_inner_reference0 )
{
	var ust::vector</ust::filesystem_path_view/> mut res;

	// Hack to prevent "ReferencePollutionOfOuterLoopVariable" error.
	if(false)
	{
		res.push_back(path);
	}

	auto mut prev_component_start = 0s;
	for( var size_type mut i= 0s; i < path.size(); ++i )
	{
		var char8 c= path[i];
		if( c == "/"c8 || c == "\\"c8 )
		{
			auto component = path.subrange( prev_component_start, i );
			if( !component.empty() )
			{
				res.push_back( component );
			}
			prev_component_start= i + 1s;
		}
	}

	auto last_component = path.subrange( prev_component_start, path.size() );
	if( !last_component.empty() )
	{
		res.push_back( last_component );
	}

	return res;
}

fn NormalizePath( ust::filesystem_path_view path ) : ust::filesystem_path
{
	auto components = SplitPathIntoComponents( path );

	var ust::vector</ust::filesystem_path_view/> mut components_normalized;

	// Hack to prevent "ReferencePollutionOfOuterLoopVariable" error.
	if(false)
	{
		components_normalized.push_back( components.front() );
	}

	foreach( &component : components )
	{
		if( component == "." )
		{} // Ignore it.
		else if( component == ".." )
		{
			if( !components_normalized.empty() )
			{
				// Remove last component.
				components_normalized.drop_back();
			}
			else
			{
				// Wrong path with too much "..".
				// We have nothing to do here - just preserve "..".
				components_normalized.push_back( component );
			}
		}
		else
		{
			components_normalized.push_back( component );
		}
	}

	var ust::filesystem_path mut res;

	if( !path.empty() && path.front() == "/"c8 )
	{
		// Input path is absolute Unix path. Output should be absolute too.
		res+= "/";
	}

	foreach( &component : components_normalized )
	{
		if( !res.empty() && res.back() != "/"c8 )
		{
			res+= "/";
		}
		res+= component;
	}

	return res;
}

fn GetLastPathComponent( ust::filesystem_path_view mut path )
	: ust::filesystem_path_view @( ust::reference_notation::return_inner_references::param0_inner_reference0 )
{
	if( path.empty() )
	{
		return path;
	}

	// Handle trailing slash.
	if( path.back() == "/"c8 || path.back()  == "\\"c8 )
	{
		path.drop_back();
		if( path.empty() )
		{
			return path;
		}
	}

	var size_type mut split_pos= path.size() - 1s;

	loop
	{
		var char8 c= path[split_pos];
		if( c == "/"c8 || c == "\\"c8 )
		{
			// Found it.
			return path.subrange( split_pos + 1s, path.size() );
		}
		if( split_pos == 0s )
		{
			return path;
		}
		--split_pos;
	}
}

} // namespace BK
