import "/integer_parsing.u"
import "/stdout.u"
import "options.uh"

namespace BK
{

fn GetHelpMessage() : ust::string_view8
{
	return g_help_message;
}

//
// Keep this up-to date with the options parsing code!
//

auto constexpr g_help_message=
	"Bürokratie - the Ü build system\n" +
	"\n" +
	"USAGE: BuildSystem [command] [options]\n" +
	"\n" +
	"COMMANDS:\n" +
	"  build                              - perform the build\n" +
	"  build_single <file>                - build a simple single-file program with no dependencies\n" +
	"  init                               - initialize a stub project\n" +
	"  help                               - show this message\n" +
	"\n" +
	"OPTIONS:\n" +
	"\n" +
	"  -q                                     - be quiet\n" +
	"  -v                                     - be verbose\n" +
	"  -h, --help                             - show this message\n" +
	"  -j <number>                            - max number of parallel build jobs. Use 0 for using all CPU cores.\n" +
	"  --build-configuration                  - select build configuration. Available configuarations are \"debug\", \"release\". Default configuration is \"release\".\n" +
	"  --configuration-options                - provide path to configuration options JSON file\n" +
	"  --target-triple                        - select target triple in form of architecture-vendor-operating_system or architecture-vendor-operating_system-environment.\n" +
	"  --project-directory <path>             - path to the root package directory. Default is current directory.\n" +
	"  --build-directory <path>               - custom path to the build directory. Default value is \"build\" subdirectory of the root package.\n" +
	"  --compiler-executable <path>           - custom path to the compiler executable\n" +
	"  --ustlib-path <path>                   - custom path to the ustlib directory\n" +
	"  --build-system-imports-path <path>     - custom path to the build system imports directory\n" +
	"  --packages-repository-directory <path> - provide path to packages repository directory\n" +
	"  --sysroot <path>                       - provide sysroot directory for the compiler (in order to find system libraries for linking)\n" +
	"  --host-sysroot <path>                  - provide sysroot directory for the compiler while compiling for host target triple (in order to find system libraries for linking)\n"
	;

fn ParseOptions( ust::array_view_imut</ust::string_view8/> args ) : ust::optional</Options/>
{
	//
	// Keep this up-to date with the help message!
	//

	if( args.empty() )
	{
		ust::stderr_print( "Error, expected command as first command-line parameter.\n" );
		return ust::null_optional;
	}

	var Options mut res;

	auto mut i= 0s;

	// Command should be always at first place.
	var ust::string_view8 command= args.front();
	if( command == "help" || command == "--help" )
	{
		res.command= Options::Command::Help;
		++i;
	}
	else if( command == "build" )
	{
		res.command= Options::Command::Build;
		++i;
	}
	else if( command == "build_single" )
	{
		res.command= Options::Command::BuildSingle;
		++i;
		if( i >= args.size() )
		{
			ust::stderr_print( "Error, missing an argument after \"build_single\".\n" );
			return ust::null_optional;
		}
		res.single_file_to_build= args[i];
		++i;
	}
	else if( command == "init" )
	{
		res.command= Options::Command::Init;
		++i;
	}
	else
	{
		ust::stderr_print( ust::concat( "Error, unknown command \"", command, "\".\n" ) );
		return ust::null_optional;
	}

	// Parse options after command.
	while( i < args.size() )
	{
		if( args[i] == "-q" )
		{
			res.quiet= true;
			++i;
		}
		else if( args[i] == "-v" )
		{
			res.verbose= true;
			++i;
		}
		else if( args[i] == "-h" || args[i] == "--help" )
		{
			res.help= true;
			++i;
		}
		else if( args[i] == "-j" )
		{
			if( i + 1s >= args.size() )
			{
				ust::stderr_print( "Error, missing an argument after \"-j\".\n" );
				return ust::null_optional;
			}
			if_var( num : ust::parse_integer_exact</u32/>( args[i + 1s] ) )
			{
				res.number_of_jobs= num;
			}
			else
			{
				ust::stderr_print( "Error, failed to parse number after \"-j\".\n" );
				return ust::null_optional;
			}
			i+= 2s;
		}
		else if( args[i] == "--build-configuration" )
		{
			if( i + 1s >= args.size() )
			{
				ust::stderr_print( "Error, missing an argument after \"--build-configuration\".\n" );
				return ust::null_optional;
			}
			auto conf = StringToBuildConfiguration( args[i + 1s] );
			if( conf.empty() )
			{
				ust::stderr_print( ust::concat( "Error, unknown build configuration \"", args[i + 1s], "\".\n" ) );
				return ust::null_optional;
			}
			res.build_configuration= conf.try_deref();
			i+= 2s;
		}
		else if( args[i] == "--configuration-options" )
		{
			if( i + 1s >= args.size() )
			{
				ust::stderr_print( "Error, missing an argument after \"--configuration-options\".\n" );
				return ust::null_optional;
			}
			res.configuration_options_file= args[i + 1s];
			i+= 2s;
		}
		else if( args[i] == "--target-triple" )
		{
			if( i + 1s >= args.size() )
			{
				ust::stderr_print( "Error, missing an argument after \"--target-triple\".\n" );
				return ust::null_optional;
			}
			res.target_triple= args[i + 1s];
			i+= 2s;
		}
		else if( args[i] == "--compiler-executable" )
		{
			if( i + 1s >= args.size() )
			{
				ust::stderr_print( "Error, missing an argument after \"--compiler-executable\".\n" );
				return ust::null_optional;
			}
			res.compiler_executable_path= args[i + 1s];
			i+= 2s;
		}
		else if( args[i] == "--build-directory" )
		{
			if( i + 1s >= args.size() )
			{
				ust::stderr_print( "Error, missing an argument after \"--build-directory\".\n" );
				return ust::null_optional;
			}
			res.build_directory= args[i + 1s];
			i+= 2s;
		}
		else if( args[i] == "--project-directory" )
		{
			if( i + 1s >= args.size() )
			{
				ust::stderr_print( "Error, missing an argument after \"--project-directory\".\n" );
				return ust::null_optional;
			}
			res.root_package_directory= args[i + 1s];
			i+= 2s;
		}
		else if( args[i] == "--ustlib-path" )
		{
			if( i + 1s >= args.size() )
			{
				ust::stderr_print( "Error, missing an argument after \"--ustlib-path\".\n" );
				return ust::null_optional;
			}
			res.ustlib_path= args[i + 1s];
			i+= 2s;
		}
		else if( args[i] == "--build-system-imports-path" )
		{
			if( i + 1s >= args.size() )
			{
				ust::stderr_print( "Error, missing an argument after \"--build-system-imports-path\".\n" );
				return ust::null_optional;
			}
			res.build_system_imports_path= args[i + 1s];
			i+= 2s;
		}
		else if( args[i] == "--packages-repository-directory" )
		{
			if( i + 1s >= args.size() )
			{
				ust::stderr_print( "Error, missing an argument after \"--packages-repository-directory\".\n" );
				return ust::null_optional;
			}
			res.packages_repository_directory= args[i + 1s];
			i+= 2s;
		}
		else if( args[i] == "--sysroot" )
		{
			if( i + 1s >= args.size() )
			{
				ust::stderr_print( "Error, missing an argument after \"--sysroot\".\n" );
				return ust::null_optional;
			}
			res.sysroot= args[i + 1s];
			i+= 2s;
		}
		else if( args[i] == "--host-sysroot" )
		{
			if( i + 1s >= args.size() )
			{
				ust::stderr_print( "Error, missing an argument after \"--host-sysroot\".\n" );
				return ust::null_optional;
			}
			res.host_sysroot= args[i + 1s];
			i+= 2s;
		}
		else
		{
			ust::stderr_print( ust::concat( "Error, unrecognized option \"", args[i], "\".\n" ) );
			return ust::null_optional;
		}
	}

	return res;
}

fn StringToBuildConfiguration( ust::string_view8 s ) : ust::optional</BuildConfiguration/>
{
	// Use manual conversion because we use different case for string representation.
	if( s == "debug" )
	{
		return BuildConfiguration::Debug;
	}
	if( s == "release" )
	{
		return BuildConfiguration::Release;
	}
	return ust::null_optional;
}

fn BuildConfigurationToString( BuildConfiguration c ) : ust::string_view8
{
	// Use manual conversion because we use different case for string representation.
	switch(c)
	{
		BuildConfiguration::Debug -> { return "debug"; },
		BuildConfiguration::Release -> { return "release"; },
	}
}

} // namespace BK
