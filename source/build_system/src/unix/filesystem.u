import "/file.u"
import "/file_helpers.u"
import "/string_conversions.u"
import "../filesystem.uh"
import "../path.uh"
import "errno.uh"
import "unix.uh"


namespace BK
{

fn MakePathAbsolute( ust::string_view8 path ) : ust::string8
{
	if( !path.empty() && path.front() == "/"c8 )
	{
		return path;
	}

	return ust::concat( ust::get_current_directory(), "/", path );
}

fn EnsureDirectoryExists( Logger &mut logger, ust::string_view8 path ) : bool
{
	var ust::string8 path_absolute= MakePathAbsolute( path );

	// Optimistically assume that we need to create only one directory.
	if( EnsureSingleDirectoryExists( path_absolute ) )
	{
		return true;
	}

	// Failed to create it. Try to create all directories in given path.

	var ust::string8 mut path_iterative;
	foreach( &component : SplitPathIntoComponents( path_absolute ) )
	{
		path_iterative+= "/";
		path_iterative+= component;
		if( !EnsureSingleDirectoryExists( path_iterative ) )
		{
			logger.LogInfo( ust::concat( "Failed to create directory \"", path_iterative, "\": " ) );
			return false;
		}
	}

	return true;
}

fn EnsureSingleDirectoryExists( ust::string_view8 path_absolute ) : bool
{
	return ust::create_directory( path_absolute ).is_ok();
}

fn WriteFile( Logger &mut logger, ust::string_view8 path, ust::string_view8 contents ) : bool
{
	result_match( ust::write_string_view_to_file( path, contents ) )
	{
		Ok(v) -> { ust::ignore_unused(v); return true; },
		Err(e) ->
		{
			logger.LogError( ust::concat( "Failed to write file \"", path, "\" error: ", ust::to_string8( u32(e) ) ) );
			return false;
		},
	}
}

fn CopyFile( Logger &mut logger, ust::string_view8 destination_path, ust::string_view8 source_path ) : bool
{
	var ust::string8 mut source_path_null_terminated = source_path;
	source_path_null_terminated+= "\0";

	var i32 fd_source = unsafe( ::open( source_path_null_terminated.data(), O_RDONLY, 0u ) );
	if( fd_source == -1 )
	{
		logger.LogError( ust::concat( "Failed to open file \"", source_path, "\" for reading, error: ", ust::to_string8( GetErrno() ) ) );
		return false;
	}

	auto source_file_size_opt= unsafe( GetFileSize( fd_source ) );
	if( source_file_size_opt.empty() )
	{
		logger.LogError( ust::concat( "Failed to get file size for \"", source_path, "\"." ) );
		unsafe( ::close(fd_source) );
		return false;
	}
	var u64 source_file_size= source_file_size_opt.try_deref();

	var ust::string8 mut destination_path_null_terminated = destination_path;
	destination_path_null_terminated+= "\0";

	var i32 fd_destination = unsafe( ::open( destination_path_null_terminated.data(), O_WRONLY | O_CREAT | O_TRUNC, 0o755u ) );
	if( fd_destination == -1 )
	{
		logger.LogError( ust::concat( "Failed to open file \"", destination_path, "\" for writing, error: ", ust::to_string8( GetErrno() ) ) );
		unsafe( ::close( fd_source ) );
		return false;
	}

	for( var off_t mut offset(0); offset < off_t(source_file_size); )
	{
		var ssize_type bytes_sent= unsafe( ::sendfile( fd_destination, fd_source, $<(offset), size_t( off_t(source_file_size) - offset ) ) );
		if( bytes_sent <= ssize_type(0) )
		{
			logger.LogError( ust::concat( "sendfile failed for destination \"", destination_path, "\" and source \"", source_path, "\", error: ", ust::to_string8( GetErrno() ) ) );
			unsafe( ::close( fd_source ) );
			unsafe( ::close( fd_destination ) );
			return false;
		}
	}

	unsafe( ::close( fd_source ) );
	unsafe( ::close( fd_destination ) );
	return true;
}

fn ReadFile( ust::string_view8 path ) : ust::optional</ust::string8/>
{
	result_match( ust::read_file_to_string( path ) )
	{
		Ok(s) -> { return s; },
		Err(e) -> { ust::ignore_unused(e); return ust::null_optional; },
	}
}

fn GetFileModificationTime( ust::string_view8 path ) : ust::optional</FileModificationTime/>
{
	result_match( ust::get_metadata_for_path( path ) )
	{
		Ok(v) -> { return v.modification_time; },
		Err(e) -> { ust::ignore_unused(e); return ust::null_optional; },
	}
}

fn GetCurrentExecutablePath() : ust::string8
{
	auto mut link_path_nt= "/proc/self/exe\0";

	var ust::string8 mut res;
	res.resize( 256s, "\0"c8 );
	for( auto mut i= 0s; i < 8s; ++i )
	{
		auto len = unsafe( ::readlink( $<(link_path_nt[0]), res.data(), res.size() ) );
		if( len < ssize_type(0) )
		{
			return "";
		}
		if( size_type(len) >= res.size() )
		{
			// Possible truncation.
			auto new_size= res.size() * 2s;
			res.resize( new_size, "\0"c8 );
			continue;
		}

		res.resize( size_type(len), " "c8 );
		return res;
	}

	return "";
}

fn GetFileSize( i32 fd ) unsafe : ust::optional</u64/>
{
	var stat_ mut s= zero_init;
	auto res= unsafe( ::fstat( fd, $<(s) ) );
	if( res != 0 )
	{
		return ust::null_optional;
	}

	return u64(s.st_size);
}

} // namespace BK
