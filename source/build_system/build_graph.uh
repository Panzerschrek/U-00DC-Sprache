import "/string.u"
import "/vector.u"
import "logger.uh"

namespace BK
{

// Directed (normally) acyclic graph.
// Nodes in build graph are commands, edges are files.
// Caution! File paths should be normalized!
struct BuildGraph
{
	struct Node
	{
		// Program to run.
		// Caution! If this program is generated during the build or may be changed in some another way, it should be listed in input files!
		ust::string8 program;
		// Its arguments.
		ust::vector</ust::string8/> command_line;

		// What is shown to the user during rebuild.
		// Contains something like command output file name.
		ust::string8 comment;

		// If non-empty - represents a dependency file.
		// It's produced during the command execution and may contain implicit input files ( like imports (headers) ).
		//
		// Caution! Dependency files are designed to track dependencies on static files (like imported sources).
		// If a file is generated by another command, it should be listed in "input_files", otherwise build order may be incorrect!
		ust::string8 dep_file;

		// Files which this command requires (including command executable file, if it may be changed).
		ust::vector</ust::string8/> input_files;
		// Files which this command produces.
		ust::vector</ust::string8/> output_files;
	}

	// Nodes of the graph in some unspecified order.
	// Use vector to make index-based access possible.
	ust::vector</Node/> nodes;

	// Edges aren't stored explicitly - input/output edges (files) are stored in nodes.
	// Edges without start nodes are possible - like for source files.
	// Edges witohut end nodes are possible too - like for result files (executables, shared libraries, etc.).
}

fn PerformGraphBuild( Logger &mut logger, BuildGraph& build_graph ) : bool;

} // namespace BK
