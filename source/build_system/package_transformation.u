import "/sort.u"
import "package_transformation.uh"
import "path.uh"

namespace BK
{

fn TransformAndValidatePackageInfo(
	Logger &mut logger,
	PackageInfo& package_info,
	WorkspaceInfo::PackageAbsoluteName& package_absolute_name,
	WorkspaceInfo &mut workspace ) : bool
{
	var bool mut ok= true;

	var ust::vector</ ust::vector</ust::string8 /> /> mut prev_targets_directories;

	foreach( &build_target : package_info.build_targets )
	{
		var ust::string8& target_name= build_target.name;

		if( !TargetNameIsValid( target_name ) )
		{
			logger.LogError( "Error, invalid build target name \"" + target_name + "\"!" );
			ok= false;
		}

		var WorkspaceInfo::BuildTargetAbsoluteName target_absolute_name
		{
			.package_name= package_absolute_name,
			.name= target_name,
		};

		if( workspace.build_targets.exists( target_absolute_name ) )
		{
			logger.LogError( "Error, duplicated build target \"" + target_name + "\"!" );
			ok= false;
		}

		var WorkspaceInfo::BuildTarget mut out_build_target{ .target_type= build_target.target_type };

		// Process sources.

		if( !build_target.source_files.empty() )
		{
			// Check each source - if it's correct.
			foreach( &source_name : build_target.source_files )
			{
				if( !TargetSourceNameIsValid( source_name ) )
				{
					logger.LogError( "Invalid, source name \"" + source_name + "\" of build target \"" + target_name + "\"!" );
					ok= false;
					continue;
				}

				out_build_target.source_files.push_back( NormalizePath( source_name ) );
			}

			// Normalize list of sources and detect duplicates.
			ust::sort( out_build_target.source_files );
			for( auto mut i = 1s; i < out_build_target.source_files.size(); ++i )
			{
				if( cast_imut(out_build_target).source_files[ i - 1s ] == cast_imut(out_build_target).source_files[i] )
				{
					logger.LogError( "Error, duplicated source file \"" + out_build_target.source_files[i] + "\" of the build target \"" + target_name + "\"!" );
					ok= false;
				}
			}

			var ust::vector</ust::string8/> mut sources_directory= CalculateBuildTargetSourceFilesCommonDirectory( out_build_target.source_files  );

			foreach( &component : sources_directory )
			{
				out_build_target.sources_directory+= component;
				out_build_target.sources_directory+= "/";
			}

			logger.LogVerbose( "All sources of the build target \"" + target_name + "\" are located within directory \"" + out_build_target.sources_directory + "\"." );

			// Check for source directory conflict.
			foreach( &prev_source_directory : prev_targets_directories )
			{
				if( prev_source_directory == sources_directory )
				{
					logger.LogError( "Source directory \"" + out_build_target.sources_directory + "\" of the build target \"" + target_name + "\" is already in use." );
					ok= false;
				}
				else if( sources_directory.size() < prev_source_directory.size() &&
					sources_directory == prev_source_directory.range().subrange_end( sources_directory.size() ) )
				{
					logger.LogError( "Source directory \"" + out_build_target.sources_directory + "\" of the build target \"" + target_name + "\" is a prefix of another used directory." );
					ok= false;
				}
				else if( sources_directory.size() > prev_source_directory.size() &&
					prev_source_directory == cast_imut(sources_directory).range().subrange_end( prev_source_directory.size() ) )
				{
					logger.LogError( "Source directory \"" + out_build_target.sources_directory + "\" of the build target \"" + target_name + "\" is located within another used directory." );
					ok= false;
				}
			}

			prev_targets_directories.push_back( move(sources_directory) );
		}

		// Process public include directories.

		if( build_target.target_type == BuildTargetType::Executable && !build_target.public_include_directories.empty() )
		{
			// For now forbid specifying public include directories for executables.
			// It's useless, since exporting symbols from executables isn't supported.
			ok= false;
			logger.LogError( "Non-empty public include directories list for an executable build target \"" + build_target.name + "\"." );
		}

		foreach( &public_include_directory : build_target.public_include_directories )
		{
			if( !TargetSourceNameIsValid( public_include_directory ) )
			{
				logger.LogError( "Invalid path of a public source directory \"" + public_include_directory + "\" of the build target \"" + target_name + "\"." );
				ok= false;
				continue;
			}
			auto mut public_include_directory_component_splitted=
				ust::vector</ust::string8/>::from_iterator( SplitPathIntoComponents( public_include_directory ).iter() );

			// Check for directory conflict.
			foreach( &prev_source_directory : prev_targets_directories )
			{
				if( prev_source_directory == public_include_directory_component_splitted )
				{
					logger.LogError( "Public include directory \"" + public_include_directory + "\" of the build target \"" + target_name + "\" is already in use." );
					ok= false;
				}
				else if( public_include_directory_component_splitted.size() < prev_source_directory.size() &&
					public_include_directory_component_splitted == prev_source_directory.range().subrange_end( public_include_directory_component_splitted.size() ) )
				{
					logger.LogError( "Public include directory \"" + public_include_directory + "\" of the build target \"" + target_name + "\" is a prefix of another used directory." );
					ok= false;
				}
				else if( public_include_directory_component_splitted.size() > prev_source_directory.size() &&
					prev_source_directory == cast_imut(public_include_directory_component_splitted).range().subrange_end( prev_source_directory.size() ) )
				{
					logger.LogError( "Public include directory \"" + public_include_directory + "\" of the build target \"" + target_name + "\" is located within another used directory." );
					ok= false;
				}
			}

			var ust::string8 mut directory_normalized;
			foreach( &component : public_include_directory_component_splitted )
			{
				directory_normalized+= component;
				directory_normalized+= "/";
			}
			out_build_target.public_include_directories.push_back( move(directory_normalized) );

			prev_targets_directories.push_back( move(public_include_directory_component_splitted) );
		}

		NormalizeList( out_build_target.public_include_directories );

		// Process public dependencies.

		if( build_target.target_type == BuildTargetType::Executable && !build_target.public_dependencies.empty() )
		{
			// For now forbid specifying public dependencies for executables.
			// It's useless, since exporting symbols from executables isn't supported.
			ok= false;
			logger.LogError( "Non-empty public dependencies list for an executable build target \"" + build_target.name + "\"." );
		}


		// Process public dependencies.
		foreach( &public_dependency : build_target.public_dependencies )
		{
			out_build_target.public_dependencies.push_back(
				TransformDependencyName( package_absolute_name, public_dependency ) );
		}
		NormalizeList(out_build_target.public_dependencies);

		// Process private dependencies.
		foreach( &private_dependency : build_target.private_dependencies )
		{
			out_build_target.private_dependencies.push_back(
				TransformDependencyName( package_absolute_name, private_dependency ) );
		}
		NormalizeList(out_build_target.private_dependencies);

		// Process external libraries.
		out_build_target.external_libraries= build_target.external_libraries;
		NormalizeList( out_build_target.external_libraries );

		workspace.build_targets.insert( target_absolute_name, move( out_build_target ) );
	}

	return ok;
}

fn ProcessWorkspaceDependenciesGraph( Logger &mut logger, WorkspaceInfo &mut workspace ) : bool
{
	var bool mut ok= true;

	// Check for dependency graph validity.
	foreach( &pair : cast_imut(workspace).build_targets )
	{
		ok&= DependencyGraphIsValid( logger, workspace, pair.key() );
	}

	if( !ok )
	{
		// Stop here, if we have errors.
		// Avoid performing dependency graph analysis steps if dependency graph isn't valid.
		return false;
	}

	// Collect build target names in order to have possibility to iterate and change build targets map elements.
	var ust::vector</WorkspaceInfo::BuildTargetAbsoluteName/> mut build_target_names;
	foreach( &pair : workspace.build_targets )
	{
		build_target_names.push_back( pair.key() );
	}

	// Perform public dependencies retrieval.
	foreach( &build_target_name : build_target_names )
	{
		auto mut dependencies = GetAllTargetPublicDependencies( workspace, build_target_name );
		workspace.build_targets[build_target_name].public_dependencies_including_transitive= move(dependencies);
	}

	// Perform private dependencies retrieval.
	foreach( &build_target_name : build_target_names )
	{
		auto dependencies = GetAllTargetPrivateAndTheirTransitivePublicDependencies( workspace, build_target_name );

		auto &mut out_build_target= workspace.build_targets[ build_target_name ];

		// Remove private dependencies which are also public dependencies.

		auto& public_dependencies= out_build_target.public_dependencies_including_transitive;
		auto is_not_in_public_dependencies=
			lambda[&]( WorkspaceInfo::BuildTargetAbsoluteName& target_name ) : bool
			{
				foreach( &dependency : public_dependencies )
				{
					if( target_name == dependency )
					{
						return false;
					}
				}
				return true;
			};

		out_build_target.private_dependencies_including_transitive_public_dependencies.append(
			dependencies.iter().filter( is_not_in_public_dependencies ) );
	}

	// Perform shared libraries to link retrieval.
	foreach( &build_target_name : build_target_names )
	{
		auto mut dependencies= GetAllTargetSharedLibraryDependenciesToLink( workspace, build_target_name );
		workspace.build_targets[ build_target_name ].shared_library_dependencies_to_link= move(dependencies);
	}

	// Perform external libraries to link retrieval.
	foreach( &build_target_name : build_target_names )
	{
		auto mut libraries= GetAllTargetExternalLibrariesToLink( workspace, build_target_name );
		workspace.build_targets[ build_target_name ].all_external_libraries_to_link= move(libraries);
	}

	return ok;
}

fn TargetNameIsValid( ust::string_view8 name ) : bool
{
	if( name.empty() )
	{
		return false;
	}

	if( name.back() == " "c8 )
	{
		return false; // Prevent names ending with space (Windows limitation).
	}
	if( name.front() == " "c8 )
	{
		return false; // Just to be sure prevent leading spaces.
	}

	foreach( c : name )
	{
		// Prevent symbols which may have special meaning in paths.
		switch(c)
		{
			// Dots are allowed in paths, but in target names they may be confusing.
			// Using dots is legal for file extension, but we don't need they in target names.
			"."c8,
			"/"c8,
			"\\"c8,
			":"c8,
			"\""c8,
			"'"c8,
			"<"c8,
			">"c8,
			"|"c8,
			"?"c8,
			"*"c8,
			char8(0) ... char8(31) // Forbid binary null and all control characters.
			-> { return false; },
			default -> {}
		}
	}

	return true;
}

fn TargetSourceNameIsValid( ust::string_view8 name ) : bool
{
	if( name.empty() )
	{
		return false;
	}

	if( name.front() == "/"c8 || name.front() == "\\"c8 )
	{
		// Do not allow absolute paths.
		return false;
	}

	if( name.size() >= 2s && name[0s] >= "A"c8 && name[0s] <= "Z"c8 && name[1s] == ":"c8 )
	{
		// Do not allow absolute Windows paths.
		return false;
	}

	// Disable "." and ".." in source file names.
	foreach( &component : SplitPathIntoComponents( name ) )
	{
		if( component == "." || component == ".." )
		{
			return false;
		}
	}

	// Further checks aren't really necessary.
	// If for example file name is invalid, it will not be found and build will fail.

	return true;
}

fn CalculateBuildTargetSourceFilesCommonDirectory( ust::array_view_imut</ust::string8/> sources ) : ust::vector</ust::string8/>
{
	if( sources.empty() )
	{
		return ust::vector</ust::string8/>();
	}

	var ust::vector</ust::vector</ust::string8/>/> mut sources_splitted_per_component;
	foreach( &source : sources )
	{
		sources_splitted_per_component.push_back( SplitPathIntoComponents(source).iter().collect</ ust::vector</ ust::string8 /> />() );
	}

	if( sources_splitted_per_component.size() == 1s )
	{
		// Special case with only one file - remove last component (presumably file name) and return it.
		var ust::vector</ust::string8/> mut result= sources_splitted_per_component.pop_back();
		if( !result.empty() )
		{
			result.drop_back();
		}
		return result;
	}

	var ust::vector</ust::string8/> mut result;

	// Calculate common prefix for all files.

	for( auto mut i= 0s; ; ++i ) label component_loop
	{
		var ust::vector</ust::string8/>& first_source= sources_splitted_per_component.front();
		if( i >= first_source.size() )
		{
			break label component_loop;
		}
		var ust::string8& first_source_component= first_source[i];

		foreach( & source_splitted : cast_imut(sources_splitted_per_component) )
		{
			if( i >= source_splitted.size() || source_splitted[i] != first_source_component )
			{
				break label component_loop;
			}
		}

		result.push_back( first_source_component );
	}

	return result;
}

fn TransformDependencyName(
	WorkspaceInfo::PackageAbsoluteName& package_absolute_name,
	DependencyName& dependency_name ) : WorkspaceInfo::BuildTargetAbsoluteName
{
	if_var( &relative_path : dependency_name.package_name.get</PackageRelativePath/>() )
	{
		if( relative_path.path.empty() )
		{
			return WorkspaceInfo::BuildTargetAbsoluteName{ .package_name= package_absolute_name, .name= dependency_name.name };
		}

		var ust::string8 mut path;
		if( !package_absolute_name.path.empty() )
		{
			path+= package_absolute_name.path;
			path+= "/";
		}
		path+= relative_path.path;

		return WorkspaceInfo::BuildTargetAbsoluteName
		{
			// TODO - process "..".
			.package_name{ .path= NormalizePath( path ) },
			.name= dependency_name.name
		};
	}
	else
	{
		// TODO - support global versioned dependencies.
		halt;
	}
}

// Returns true if dependency graph is valid.
fn DependencyGraphIsValid(
	Logger &mut logger,
	WorkspaceInfo& workspace,
	WorkspaceInfo::BuildTargetAbsoluteName& build_target_name ) : bool
{
	var ust::vector</WorkspaceInfo::BuildTargetAbsoluteName/> mut call_stack;
	return DependencyGraphIsValid_r( logger, workspace, build_target_name, call_stack );
}

fn DependencyGraphIsValid_r(
	Logger &mut logger,
	WorkspaceInfo& workspace,
	WorkspaceInfo::BuildTargetAbsoluteName& build_target_name,
	ust::vector</WorkspaceInfo::BuildTargetAbsoluteName/> &mut call_stack ) : bool
{
	for( auto mut i= 0s; i < call_stack.size(); ++i )
	{
		if( build_target_name == call_stack[i] )
		{
			var ust::string8 mut message= "Dependency loop detected: ";
			for( auto mut j= i; j < call_stack.size(); ++j )
			{
				message += "\"";
				message += call_stack[j].name;
				message += "\"";
				message += " -> ";
			}
			message += "\"";
			message += build_target_name.name;
			message += "\"";

			logger.LogError( message );

			return false;
		}
	}

	call_stack.push_back( build_target_name );

	var bool mut ok= true;

	if_var( &build_target : workspace.build_targets.find( build_target_name ) )
	{
		foreach( &dependency : build_target.public_dependencies.iter().chain( build_target.private_dependencies.iter() ) )
		{
			if( dependency == build_target_name )
			{
				logger.LogError( "Build target \"" + build_target_name.name + "\" depends on itself." );
				ok= false;
				continue;
			}
			if_var( &dependency_build_target : workspace.build_targets.find( dependency ) )
			{
				if( !(
					dependency_build_target.target_type == BuildTargetType::Library ||
					dependency_build_target.target_type == BuildTargetType::SharedLibrary
					// For now do not allow depend on object file build target.
					// Technically it can be possible, but has a lot of caveats, so it's better not to support it.
					// Regular library targets should be used, if some common code needed to be used in some another build target(s).
					) )
				{
					logger.LogError( "Build target \"" + build_target_name.name + "\" depends on non-library build target \"" + dependency.name + "\"." );
					ok= false;
				}
			}

			ok&= DependencyGraphIsValid_r( logger, workspace, dependency, call_stack );
		}
	}
	else
	{
		ok= false;
		logger.LogError( "Dependency \"" + build_target_name.name + "\" not found." );
	}

	call_stack.pop_back();
	return ok;
}

fn GetAllTargetPublicDependencies(
	WorkspaceInfo& workspace,
	WorkspaceInfo::BuildTargetAbsoluteName& build_target_name )
	: ust::vector</WorkspaceInfo::BuildTargetAbsoluteName/>
{
	// Build targets existence should be checked before.

	var ust::vector</WorkspaceInfo::BuildTargetAbsoluteName/> mut result;
	GetAllTargetPublicDependencies_r( workspace, build_target_name, result );

	NormalizeList(result);

	return result;
}

fn GetAllTargetPrivateAndTheirTransitivePublicDependencies(
	WorkspaceInfo& workspace,
	WorkspaceInfo::BuildTargetAbsoluteName& build_target_name )
	: ust::vector</WorkspaceInfo::BuildTargetAbsoluteName/>
{
	// Build targets existence should be checked before.

	var ust::vector</WorkspaceInfo::BuildTargetAbsoluteName/> mut result;

	foreach( &private_dependency : workspace.build_targets[ build_target_name ].private_dependencies )
	{
		GetAllTargetPublicDependencies_r( workspace, private_dependency, result );
		result.push_back( private_dependency );
	}

	NormalizeList(result);

	return result;
}

fn GetAllTargetPublicDependencies_r(
	WorkspaceInfo& workspace,
	WorkspaceInfo::BuildTargetAbsoluteName& build_target_name,
	ust::vector</WorkspaceInfo::BuildTargetAbsoluteName/> &mut result )
{
	// Possible dependency loops and target names validity should be checked before.

	foreach( &public_dependency : workspace.build_targets[ build_target_name ].public_dependencies )
	{
		GetAllTargetPublicDependencies_r( workspace, public_dependency, result );
		result.push_back(public_dependency);
	}
}

fn GetAllTargetSharedLibraryDependenciesToLink(
	WorkspaceInfo& workspace,
	WorkspaceInfo::BuildTargetAbsoluteName& build_target_name ) : ust::vector</WorkspaceInfo::BuildTargetAbsoluteName/>
{
	// Possible dependency loops and target names validity should be checked before.

	var WorkspaceInfo::BuildTarget& build_target= workspace.build_targets[ build_target_name ];
	var ust::vector</WorkspaceInfo::BuildTargetAbsoluteName/> mut result;

	foreach( &dependency : build_target.public_dependencies.iter().chain( build_target.private_dependencies.iter() ) )
	{
		GetAllTargetSharedLibraryDependenciesToLink_r( workspace, dependency, result );
	}

	NormalizeList(result);

	return result;
}

fn GetAllTargetSharedLibraryDependenciesToLink_r(
	WorkspaceInfo& workspace,
	WorkspaceInfo::BuildTargetAbsoluteName& build_target_name,
	ust::vector</WorkspaceInfo::BuildTargetAbsoluteName/> &mut result )
{
	// Possible dependency loops and target names validity should be checked before.

	var WorkspaceInfo::BuildTarget& build_target= workspace.build_targets[ build_target_name ];

	switch( build_target.target_type )
	{
		BuildTargetType::Executable -> {}, // This should be checked before.
		BuildTargetType::Library ->
		{
			// Extract shared library dependencies of public and private library dependencies.
			foreach( &dependency : build_target.public_dependencies.iter().chain( build_target.private_dependencies.iter() ) )
			{
				GetAllTargetSharedLibraryDependenciesToLink_r( workspace, dependency, result );
			}
		},
		BuildTargetType::SharedLibrary ->
		{
			// Use this shared librarty.
			result.push_back( build_target_name );

			// Extract also shared library dependencies of public dependencies of this shared library.
			// But DO NOT extract shared library dependencies of private dependencies.
			foreach( &public_dependency : build_target.public_dependencies )
			{
				GetAllTargetSharedLibraryDependenciesToLink_r( workspace, public_dependency, result );
			}
		},
		BuildTargetType::ObjectFile -> {}, // This should be checked before.
	}
}

fn GetAllTargetExternalLibrariesToLink(
	WorkspaceInfo& workspace,
	WorkspaceInfo::BuildTargetAbsoluteName& build_target_name ) : ust::vector</ust::string8/>
{
	// Possible dependency loops and target names validity should be checked before.

	var ust::vector</ust::string8/> mut result;
	GetAllTargetExternalLibrariesToLink_r( workspace, build_target_name, result );
	NormalizeList(result);

	return result;
}

fn GetAllTargetExternalLibrariesToLink_r(
	WorkspaceInfo& workspace,
	WorkspaceInfo::BuildTargetAbsoluteName& build_target_name,
	ust::vector</ust::string8/> &mut result )
{
	// Possible dependency loops and target names validity should be checked before.

	var WorkspaceInfo::BuildTarget& build_target= workspace.build_targets[ build_target_name ];

	foreach( &external_library : build_target.external_libraries )
	{
		result.push_back( external_library );
	}

	// Extract external libraries of public and private library dependencies.
	foreach( &dependency : build_target.public_dependencies.iter().chain( build_target.private_dependencies.iter() ) )
	{
		if( workspace.build_targets[ dependency ].target_type == BuildTargetType::SharedLibrary )
		{
			// Do not extract external libraries of dependencies of shared library.
			// Assume that external libraries are used inside a shared library only privately.
			continue;
		}
		GetAllTargetExternalLibrariesToLink_r( workspace, dependency, result );
	}

}

template</type T/>
fn NormalizeList( ust::vector</T/> &mut list )
{
	ust::sort( list );
	list.remove_adjacent_duplicates();
}

} // namespace BK
