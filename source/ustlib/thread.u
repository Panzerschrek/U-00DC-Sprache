import "alloc.u"
import "container_utils.u"
import "optional.u"
import "pthread.u"

namespace ust
{

// Implementation of thread class, using "pthreads" library.

template</ type Func, type ResultType />
class thread
{
	static_assert( !non_sync</Func/>, "passing non_sync functor into another thread" );

public:
	fn constructor( mut this, Func mut func ) @(c_param1_pollution)
	( impl_= zero_init )
	{
		unsafe
		{
			impl_= $<( cast_ref_unsafe</Impl/>( memory_allocate( typeinfo</Impl/>.size_of ) ) );
			var Impl &mut impl= $>(impl_);

			move_into_uninitialized( impl, Impl( move(func) ) );

			var pthread_attr_t mut attr= zero_init;
			var pthread_t mut thread_id(0);

			halt if( pthread_attr_init( attr ) != 0 );
			halt if( pthread_create( thread_id, attr, thread_function, $<( cast_ref_unsafe</byte8/>(impl) ) ) != 0 );
			halt if( pthread_attr_destroy( attr ) != 0 );

			impl.thread_id= thread_id;
		}
	}

	fn constructor( mut this, this_type &imut other )= delete;
	op=( mut this, this_type &imut other )= delete;

	// Implement "join" as byval this-call method to enforce user to move thread to join it and avoid calling "join" again.
	fn join( byval mut this ) : ResultType
	{
		halt if( unsafe( pthread_join( $>(impl_).thread_id, nullptr</$(byte8)/>() ) ) != 0 );

		auto res= unsafe( $>(impl_) ).result.try_take();

		unsafe( memory_free( cast_ref_unsafe</byte8/>( $>(impl_) ) ) );
		impl_= nullptr</Impl/>();

		return res;
	}

	fn destructor()
	{
		if( impl_ != nullptr</Impl/>() )
		{
			halt if( unsafe( pthread_join( $>(impl_).thread_id, nullptr</$(byte8)/>() ) ) != 0 );
			unsafe( memory_free( cast_ref_unsafe</byte8/>( $>(impl_) ) ) );
		}
	}

private:
	type this_type= thread</ Func, ResultType />;
	struct Impl
	{
		Func func;
		optional</ResultType/> result;
		pthread_t thread_id(0);

		fn constructor( Func mut in_func ) @(c_param1_pollution)
			( func(move(in_func)) )
		{}
	}

	auto constexpr c_reference_tag_count= typeinfo</Func/>.reference_tag_count;
	static_assert( typeinfo</Func/>.reference_tag_count == typeinfo</Impl/>.reference_tag_count );

	auto c_param1_pollution= reference_notation::pollution::param0_param1_all_inner_references</ c_reference_tag_count />();

private:
	fn thread_function( $(byte8) impl_ptr ) : $(byte8)
	{
		unsafe
		{
			auto &mut impl= cast_ref_unsafe</Impl/>( $>(impl_ptr) );

			auto mut res= impl.func();
			impl.result.reset( move(res) );

			return nullptr</byte8/>();
		}
	}

private:
	ContainerTag</Impl/> container_tag_;

	// Store content indirectly, because we send raw pointer to it to thread function.
	$(Impl) impl_;
}

template</ type Func />
type thread_void= thread</ Func, void />;

template</ type Func />
fn make_thread_void( Func mut func ) : auto
{
	return thread_void</Func/>( move(func) );
}

template</ type ReturnType, type Func />
fn make_thread( Func mut func ) : auto
{
	return thread</ Func, ReturnType />( move(func) );
}

} // namespace ust
