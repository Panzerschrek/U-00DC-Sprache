import "random_access_range.u"

namespace ust
{

template</type T/>
fn sort( ust::random_access_range</T, true/> range )
{
	sort_impl::do_sort( range );
}

namespace sort_impl
{

template</type T/>
fn do_sort( ust::random_access_range</T, true/> range )
{
	if( range.size() <= 1s )
	{
		return;
	}
	if( range.size() == 2s )
	{
		var bool mut less= false;
		{
			var ust::random_access_range</T, false/> range_copy= range;
			less= range_copy.front() < range_copy.back();
		}
		if( !less )
		{
			range.swap( 0s, 1s );
		}
		return;
	}

	// TODO - find approximate middle element and place it into last position.

	var size_type s= range.size();
	var size_type last= s - 1s;
	var size_type mut lo= 0s, mut hi= last;

	while( lo < hi )
	{
		var bool mut less= false;
		{
			var ust::random_access_range</T, false/> range_copy= range;
			less= range_copy[lo] < range_copy[last];
		}

		if( less )
		{
			++lo;
		}
		else
		{
			--hi;
			range.swap( lo, hi );
		}
	}

	if( hi < last )
	{
		range.swap( hi, last );
	}

	do_sort( range.subrange( 0s, hi ) );
	do_sort( range.subrange( hi + 1s, s ) );
}

} // namespace sort_impl

} // namespace ust
