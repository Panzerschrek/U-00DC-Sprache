import "box.u"
import "container_utils.u"
import "hash.u"
import "pointer.u"
import "polymorph.u"
import "type_traits.u"

namespace ust
{

template</ type T />
class box_nullable
{
public:
	type hasher= box_nullable_hasher;

public:
	// Default initializer = set to zero.
	fn constructor(){}

	// Construct with value.
	fn constructor( mut this'a...', T mut val'b...' ) ' a <- b '
	{
		unsafe
		{
			ptr_= raw_ptr_mut</ T />( cast_ref_unsafe</T/>( memory_allocate( typeinfo</T/>.size_of ) ) );
			move_unsafe</T/>( ptr_.get_ref(), move(val) );
		}
	}

	// Construct with value, taken from box.
	fn conversion_constructor( mut this'a...', box</T/> mut val'b...' ) ' a <- b '
	{
		unsafe
		{
			static_assert( typeinfo</ box</T/> />.size_of == typeinfo</ raw_ptr_mut</T/> />.size_of );
			memory_copy( ptr_, val, typeinfo</ box</T/> />.size_of );
			move_into_nowhere( move(val) );
		}
	}
	
	// Construct with value, taken from nullable box of compatible type.
	template</ type U />
	fn enable_if( !is_same_type</T, U/>() && is_ancestor_for</T, U/>() )
	conversion_constructor( mut this'a...', box_nullable</U/> mut val'b...' ) ' a <- b '
	{
		unsafe
		{
			if( !val.empty() )
			{
				// TODO - do not call "get_ref", because it checks value.
				ptr_= raw_ptr_mut</ T />( val.get_ref() ); // Convert reference here.
			}
			move_into_nowhere( move(val) );
		}
	}
	
	// Construct with value, taken from box of compatible type.
	template</ type U />
	fn enable_if( !is_same_type</T, U/>() && is_ancestor_for</T, U/>() )
	conversion_constructor( mut this'a...', box</U/> mut val'b...' ) ' a <- b '
	{
		unsafe
		{
			ptr_= raw_ptr_mut</ T />( val.get_ref() ); // Convert reference here.
			move_into_nowhere( move(val) );
		}
	}

	// box is NOT copyable, because copying required memory allocation. Copying must be done manually.

	fn destructor()
	{
		if( !empty() )
		{
			unsafe
			{
				call_destructor( ptr_.get_ref() );
				memory_free( polymorph_restore_original_ref( ptr_.get_ref() ) );
			}
		}
	}

	fn get_ref( mut this ) : T &mut
	{
		halt if( empty() );
		unsafe{  return get_ref_unsafe();  }
	}

	fn get_ref( imut this ) : T &imut
	{
		halt if( empty() );
		unsafe{  return get_ref_unsafe();  }
	}

	fn get_ref_unsafe( mut this ) unsafe : T &mut
	{
		unsafe{  return ptr_.get_ref();  }
	}

	fn get_ref_unsafe( imut this ) unsafe : T &imut
	{
		unsafe{  return ptr_.get_ref();  }
	}

	fn empty( this )  : bool
	{
		return ptr_.is_null();
	}

	fn reset( mut this )
	{
		if( !empty() )
		{
			unsafe
			{
				call_destructor( ptr_.get_ref() );
				memory_free( polymorph_restore_original_ref( ptr_.get_ref() ) );
				ptr_= raw_ptr_mut</T/>();
			}
		}
	}

	fn reset( mut this'a...', T mut val'b...' ) ' a <- b '
	{
		unsafe
		{
			if( empty() )
			{
				ptr_= raw_ptr_mut</ T />( cast_ref_unsafe</T/>( memory_allocate( typeinfo</T/>.size_of ) ) );
				move_unsafe</T/>( ptr_.get_ref(), move(val) );
			}
			else
			{
				ptr_.get_ref()= move(val);
			}
		}
	}

private:
	ContainerTag</ T /> container_tag_;
	raw_ptr_mut</ T /> ptr_;
}

struct box_nullable_hasher
{
	template</ type T />
	fn hash( box_nullable</T/>& b ) : size_type
	{
		if_var( &value : b )
		{
			return default_hasher::hash(value);
		}
		return size_type(0);
	}
}

template</ type T />
fn to_non_nullable( box_nullable</T/> mut val ) : box</T/>
{
	halt if( val.empty() );
	unsafe
	{
		static_assert( typeinfo</ box</T/> />.size_of == typeinfo</ box_nullable</T/> />.size_of );
		var box</T/> mut result= uninitialized;
		memory_copy( result, val, typeinfo</ box</T/> />.size_of );
		move_into_nowhere( move(val) );
		return move(result);
	}
}

} // namespace ust
