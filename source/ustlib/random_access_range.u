import "container_utils.u"
import "memory.u"
import "pointer.u"

namespace ust
{

template</ type T, bool is_mutable />
class random_access_range
{
public:
	fn constructor( mut this )= delete;
	fn constructor( mut this, this_type &imut other )= default;

	op=( mut this, this_type &imut other )= default;

	template</ size_type size />
	fn enable_if( is_mutable )
	conversion_constructor( mut this'a', [ T, size ] &'b  mut arr ) ' a <-  b '
	( begin_(arr), end_(begin_ + size) )
	{}

	template</ size_type size />
	fn enable_if( !is_mutable )
	conversion_constructor( mut this'a', [ T, size ] &'b imut arr ) ' a <- b '
	{
		unsafe
		{
			begin_= ptr_type( cast_mut(arr) );
			end_= begin_ + size;
		}
	}

	fn enable_if( !is_mutable )
	conversion_constructor( mut this'a', random_access_range</ T, true />& src_range'b' ) ' a <- b '
	{
		unsafe
		{	// Hack! we can not access begin_/end_ members of different range type, so, just copy memory.
			static_assert( typeinfo</typeof(this)/>.size_of == typeinfo</typeof(src_range)/>.size_of );
			memory_copy( this, src_range, typeinfo</typeof(this)/>.size_of );
		}
	}

	// Precondition - begin <= end, all memory in range [ begin, end ) is valid.
	fn constructor( raw_ptr_mut</ T /> begin, raw_ptr_mut</ T /> end ) unsafe
		( begin_(begin), end_(end) )
	{}

	op enable_if( is_mutable )
	[]( this'x', size_type index ) : T &'x  mut
	{
		halt if( empty() );
		unsafe
		{
			return cast_mut( cast_ref_unsafe</T/>( int_to_ref( ref_to_int( begin_[index] ) ) ) );
		}
	}

	op
	enable_if( !is_mutable )
	[]( this'x', size_type index ) : T &'x imut
	{
		halt if( empty() );
		unsafe
		{
			return cast_ref_unsafe</T/>( int_to_ref( ref_to_int( begin_[index] ) ) );
		}
	}

	fn enable_if(  is_mutable ) front( this'x' ) : T &'x  mut
	{
		halt if(empty());
		unsafe{  return front_unsafe();  }
	}

	fn enable_if( !is_mutable ) front( this'x' ) : T &'x imut
	{
		halt if(empty());
		unsafe{  return front_unsafe();  }
	}

	fn enable_if(  is_mutable ) back ( this'x' ) : T &'x  mut
	{
		halt if(empty());
		unsafe{  return back_unsafe ();  }
	}

	fn enable_if( !is_mutable ) back ( this'x' ) : T &'x imut
	{
		halt if(empty());
		unsafe{  return back_unsafe ();  }
	}

	fn enable_if(  is_mutable ) front_unsafe( this'x' ) unsafe : T &'x  mut
	{
		unsafe{  return cast_mut( cast_ref_unsafe</T/>( int_to_ref( ref_to_int( begin_.get_ref() ) ) ) );  }
	}

	fn enable_if( !is_mutable ) front_unsafe( this'x' ) unsafe : T &'x imut
	{
		unsafe{  return cast_mut( cast_ref_unsafe</T/>( int_to_ref( ref_to_int( begin_.get_ref() ) ) ) );  }
	}

	fn enable_if(  is_mutable ) back_unsafe ( this'x' ) unsafe : T &'x  mut
	{
		unsafe{  return cast_mut( cast_ref_unsafe</T/>( int_to_ref( ref_to_int( ( end_ - size_type(1) ).get_ref() ) ) ) );  }
	}

	fn enable_if( !is_mutable ) back_unsafe ( this'x' ) unsafe : T &'x imut
	{
		unsafe{  return cast_mut( cast_ref_unsafe</T/>( int_to_ref( ref_to_int( ( end_ - size_type(1) ).get_ref() ) ) ) );  }
	}

	fn subrange( this'x', size_type start_index, size_type end_index ) : this_type'x'
	{
		halt if( start_index > end_index );
		halt if( end_index > size() );
		unsafe{  return this_type( begin_ + start_index, begin_ + end_index );  }
	}

	fn empty( this ) : bool
	{
		return begin_ == end_;
	}

	fn size( this ) : size_type
	{
		return size_type( end_ - begin_ );
	}

	fn drop_front( mut this )
	{
		halt if( empty() );
		++begin_;
	}

	fn drop_back( mut this )
	{
		halt if( empty() );
		--end_;
	}

	// Precondition - range is nonempty.
	fn drop_front_unsafe( mut this ) unsafe
	{
		++begin_;
	}

	// Precondition - range is nonempty.
	fn drop_back_unsafe( mut this ) unsafe
	{
		--end_;
	}

	fn range( this'x' ) : this_type'x'
	{
		return this;
	}

	// Access raw data.
	fn data( this ) unsafe : ptr_type
	{
		return begin_;
	}

private:
	type this_type= random_access_range</ T, is_mutable />;
	type ptr_type= raw_ptr_mut</ T />;

private:
	ReferenceContainerTag</ T, is_mutable /> reference_tag_;

private:
	ptr_type begin_;
	ptr_type end_;
}

template</ type T />
type random_access_range_mut = random_access_range</ T, true  />;
template</ type T />
type array_view_mut = random_access_range</ T, true  />;

template</ type T />
type random_access_range_imut= random_access_range</ T, false />;
template</ type T />
type array_view_imut= random_access_range</ T, false />;

} // namespace ust
