import "aligned_storage.u"
import "container_utils.u"
import "optional_ref.u"

// Use this macro to call some function (or generally, evaluate any expression), returning "result".
// If result is successfull - it is unwrapped into specified variable.
// Else error is extracted and returned.
// Such macro is designated for simplifying passing errors to caller code.
// Use word "return" in macro name in order to make visible, that this macro contains possible return point.
?macro <? result_unwrap_or_return:block ( ?m:opt<? mut ?> ?var_name:ident : ?e:expr ); ?>
->
<?
	auto mut ??res = ?e;
	if( ??res.is_error() )
	{
		// This works for functions returning result::ErrorT, functionis returning "result" with ErrorT and any other OkT, optionals and generally any types, constructible from result::ErrorT.
		return move( ??res ).try_take_error();
	}
	auto ?m<? mut ?> ?var_name = move( ??res ).try_take();
?>

// Use this macro to call some function (or generally, evaluate any expression), returning "result".
// In case of error this error is unwrapped and returned.
// Success result is ignored.
// Use word "return" in macro name in order to make visible, that this macro contains possible return point.
?macro <? result_check_or_return:block ( ?e:expr ); ?>
->
<?
	with( mut ??res : ?e )
	{
		if( ??res.is_error() )
		{
			// This works for functions returning result::ErrorT, functionis returning "result" with ErrorT and any other OkT, optionals and generally any types, constructible from result::ErrorT.
			return move( ??res ).try_take_error();
		}
	}
?>

// Use this macro to process result or error.
// TODO - maybe simplify it somehow?
?macro <?
	result_match:block ( ?e:expr )
	{
		Ok ( ?ok_mut :opt<? mut ?> ?ok_variable :ident ) -> ?b_ok:block ,
		Err( ?err_mut:opt<? mut ?> ?err_variable:ident ) -> ?b_err:block ?o:opt<? , ?>
	}
?>
->
<?
	with( mut ??res : ?e )
	{
		if( ??res.is_ok() )
		{
			auto ?ok_mut<? mut ?> ?ok_variable = move( ??res ).try_take();
			?b_ok
		}
		else
		{
			auto ?err_mut<? mut ?> ?err_variable = move( ??res ).try_take_error();
			?b_err
		}
	}
?>

namespace ust
{

template</type OkT, type ErrorT/>
class result nodiscard
{
public:
	static_assert( !same_type</OkT, ErrorT/>, "Result and error types must be distinct!" );

	static_assert( typeinfo</OkT/>.reference_tag_count == 0s, "TODO - support references inside result." );
	static_assert( typeinfo</ErrorT/>.reference_tag_count == 0s, "TODO - support references inside result." );

public:
	// Construt from ok value.
	fn conversion_constructor( mut this, OkT mut res )
		( is_ok_= true, storage_= unsafe(get_uninitialized_value</storage_type/>()) )
	{
		unsafe( move_into_uninitialized( deref_unchecked(), move(res) ) );
	}

	// Construct from error value.
	fn conversion_constructor( mut this, ErrorT mut err )
		( is_ok_= false, storage_= unsafe(get_uninitialized_value</storage_type/>()) )
	{
		unsafe( move_into_uninitialized( deref_error_unchecked(), move(err) ) );
	}

	// Disable copying.
	// Since "result" container is designated for usage as return value, there is no reason to copy it somehow.
	// Normal usage is to call function, returning "result", check it and extract result or error.
	// Disabling copying prevens some common mistakes/ineffectiveness in usage of "result".
	fn constructor( mut this, this_type& other )= delete;
	op=( mut this, this_type& other )= delete;

	// Destroy either result or error.
	fn destructor()
	{
		if( is_ok_ )
		{
			unsafe( call_destructor( cast_ref_unsafe</OkT/>( storage_ ) ) );
		}
		else
		{
			unsafe( call_destructor( cast_ref_unsafe</ErrorT/>( storage_ ) ) );
		}
	}

	// Check contents kind.

	fn is_ok( this ) : bool
	{
		return is_ok_;
	}

	fn is_error( this ) : bool
	{
		return !is_ok();
	}

	// Access ok value.

	fn try_deref(  mut this ) : OkT & mut @( reference_notation::return_references::param0 )
	{
		halt if(!is_ok());
		return unsafe( deref_unchecked() );
	}

	fn try_deref( imut this ) : OkT &imut @( reference_notation::return_references::param0 )
	{
		halt if(!is_ok());
		return unsafe( deref_unchecked() );
	}

	fn deref_unchecked(  mut this ) unsafe : OkT & mut @( reference_notation::return_references::param0 )
	{
		return unsafe( cast_ref_unsafe</OkT/>( storage_ ) );
	}

	fn deref_unchecked( imut this ) unsafe : OkT &imut @( reference_notation::return_references::param0 )
	{
		return unsafe( cast_ref_unsafe</OkT/>( storage_ ) );
	}

	// Access error value.

	fn try_deref_error(  mut this ) : ErrorT & mut @( reference_notation::return_references::param0 )
	{
		halt if(!is_error());
		return unsafe( deref_error_unchecked() );
	}

	fn try_deref_error( imut this ) : ErrorT &imut @( reference_notation::return_references::param0 )
	{
		halt if(!is_error());
		return unsafe( deref_error_unchecked() );
	}

	fn deref_error_unchecked(  mut this ) unsafe : ErrorT & mut @( reference_notation::return_references::param0 )
	{
		return unsafe( cast_ref_unsafe</ErrorT/>( storage_ ) );
	}

	fn deref_error_unchecked( imut this ) unsafe : ErrorT &imut @( reference_notation::return_references::param0 )
	{
		return unsafe( cast_ref_unsafe</ErrorT/>( storage_ ) );
	}

	// Get optional reference.

	fn
	enable_if( typeinfo</OkT/>.reference_tag_count == 0s )
	as_ref(  mut this ) : optional_ref_mut </OkT/> @( reference_notation::return_inner_references::param0 )
	{
		if( is_ok_ )
		{
			return unsafe( deref_unchecked() );
		}
		else
		{
			return null_optional_ref;
		}
	}

	fn
	enable_if( typeinfo</OkT/>.reference_tag_count == 0s )
	as_ref( imut this ) : optional_ref_imut</OkT/> @( reference_notation::return_inner_references::param0 )
	{
		if( is_ok_ )
		{
			return unsafe( deref_unchecked() );
		}
		else
		{
			return null_optional_ref;
		}
	}

	fn
	enable_if( typeinfo</ErrorT/>.reference_tag_count == 0s )
	as_error_ref(  mut this ) : optional_ref_mut </ErrorT/> @( reference_notation::return_inner_references::param0 )
	{
		if( !is_ok_ )
		{
			return unsafe( deref_error_unchecked() );
		}
		else
		{
			return null_optional_ref;
		}
	}

	fn
	enable_if( typeinfo</ErrorT/>.reference_tag_count == 0s )
	as_error_ref( imut this ) : optional_ref_imut</ErrorT/> @( reference_notation::return_inner_references::param0 )
	{
		if( !is_ok_ )
		{
			return unsafe( deref_error_unchecked() );
		}
		else
		{
			return null_optional_ref;
		}
	}

	// Extract and return ok value.
	fn nodiscard try_take( byval mut this ) : OkT
	{
		unsafe
		{
			var OkT mut t= uninitialized;
			{
				auto &mut ref= try_deref(); // try_deref will halt if result contains error.
				memory_copy_aligned( typeinfo</OkT/>.align_of, ptr_cast_to_byte8($<(t)), ptr_cast_to_byte8( $<( ref ) ), typeinfo</OkT/>.size_of ); // Copy contents into the result.
			}
			move_into_nowhere( move(this) ); // Forget about the result container.
			return t;
		}
	}

	// Extract and return error value.
	fn nodiscard try_take_error( byval mut this ) : ErrorT
	{
		unsafe
		{
			var ErrorT mut t= uninitialized;
			{
				auto &mut ref= try_deref_error(); // try_deref will halt if result contains no error.
				memory_copy_aligned( typeinfo</ErrorT/>.align_of, ptr_cast_to_byte8($<(t)), ptr_cast_to_byte8( $<( ref ) ), typeinfo</ErrorT/>.size_of ); // Copy contents into the result.
			}
			move_into_nowhere( move(this) ); // Forget about the result container.
			return t;
		}
	}

	// ==

	op enable_if( c_is_equality_comparable )
	==( this_type& l, this_type& r ) : bool
	{
		if( l.is_ok_ && r.is_ok_ )
		{
			// Both oks - compare them.
			return unsafe( l.deref_unchecked() ) == unsafe( r.deref_unchecked() );
		}
		if( !l.is_ok_ && !r.is_ok_ )
		{
			// Both errors - compare them.
			return unsafe( l.deref_error_unchecked() ) == unsafe( r.deref_error_unchecked() );
		}

		// Different kinds - not equals.
		return false;
	}

	op enable_if( c_is_equality_comparable )
	==( this_type& l, OkT& r ) : bool
	{
		if( l.is_ok_ )
		{
			// Both oks - compare them.
			return unsafe( l.deref_unchecked() ) == r;
		}

		// Different kinds - not equals.
		return false;
	}

	op enable_if( c_is_equality_comparable )
	==( OkT& l, this_type& r ) : bool
	{
		if( r.is_ok_ )
		{
			// Both oks - compare them.
			return l == unsafe( r.deref_unchecked() );
		}

		// Different kinds - not equals.
		return false;
	}

	op enable_if( c_is_equality_comparable )
	==( this_type& l, ErrorT& r ) : bool
	{
		if( !l.is_ok_ )
		{
			// Both errors - compare them.
			return unsafe( l.deref_error_unchecked() ) == r;
		}

		// Different kinds - not equals.
		return false;
	}

	op enable_if( c_is_equality_comparable )
	==( ErrorT& l, this_type& r ) : bool
	{
		if( !r.is_ok_ )
		{
			// Both errors - compare them.
			return l == unsafe( r.deref_error_unchecked() );
		}

		// Different kinds - not equals.
		return false;
	}

private:
	type this_type= result</OkT, ErrorT/>;
	type storage_type= aligned_storage</ c_storage_size, c_storage_align />;

	var size_type constexpr c_storage_size = ( typeinfo</OkT/>.size_of  > typeinfo</ErrorT/>.size_of  ? typeinfo</OkT/>.size_of  : typeinfo</ErrorT/>.size_of  );
	var size_type constexpr c_storage_align= ( typeinfo</OkT/>.align_of > typeinfo</ErrorT/>.align_of ? typeinfo</OkT/>.align_of : typeinfo</ErrorT/>.align_of );

	var bool constexpr c_is_equality_comparable= typeinfo</OkT/>.is_equality_comparable && typeinfo</ErrorT/>.is_equality_comparable;

private:
	ContainerTag</ tup[ OkT, ErrorT ] /> container_tag_;
	storage_type storage_;
	bool is_ok_;
}

} // namespace ust
