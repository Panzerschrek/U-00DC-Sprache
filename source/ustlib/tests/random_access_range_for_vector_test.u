//##success_test
import "../imports/vector.u"

type VecType= ust::vector</i32/>;


fn nomangle main() : i32
{
	{
		var VecType mut vec;
		vec.push_back( 32 );
		vec.push_back( 37 );
		vec.push_back( 99 );

		auto mut sum= 0;
		foreach( x : vec ) { sum+= x; }
		halt if( sum != 32 + 37 + 99 );
	}
	{
		var VecType mut vec;
		var size_type mut iterations(0);
		foreach( x : vec ){ ust::ignore_unused(x); ++iterations; }
		halt if( iterations > 0s );
	}
	{
		var VecType mut vec;
		vec.push_back( 66 );
		foreach( &mut x : vec ){ x*= 2; }
		halt if( vec[0s] != 132 );
	}
	{
		var VecType mut vec;
		vec.push_back( 17 );
		vec.push_back( 21 );
		vec.push_back( 1 );
		vec.push_back( 55 );
		var i32 mut sum= 0;
		foreach( &x : cast_imut(vec) )
		{
			sum+= x + cast_imut(vec).front();
		}
		halt if( sum != 17 * 4 + 17 + 21 + 1 + 55 );
	}
	{
		var VecType mut vec;
		vec.push_back( 55 );
		vec.push_back( 66 );
		vec.push_back( 77 );
		halt if( vec.range().size() != 3s );
		halt if( vec.range().empty() );
	}
	{
		var VecType vec;
		halt if( vec.range().size() != 0s );
		halt if( !vec.range().empty() );
	}
	{
		var VecType mut vec;
		vec.push_back( 14 );
		vec.push_back( 15 );
		vec.push_back( 16 );
		auto mut range= cast_imut(vec).range();
		halt if( range.size() != 3s );
		unsafe
		{
			range.drop_front();
			halt if( range.size() != 2s );
			halt if( vec.size() != 3s );
			range.drop_back();
			halt if( range.size() != 1s );
			halt if( vec.size() != 3s );
		}
	}
	{
		var VecType mut vec;
		vec.push_back( 5 );
		vec.push_back( 3 );
		vec.push_back( 7 );
		auto range= vec.range();
		auto mut res= 5 * 3 * 7 * 65;
		foreach( x : range ) { res/= x; }
		halt if( res != 65 );
	}
	{
		var VecType mut vec;
		vec.push_back( 22 );
		vec.push_back( 55 );
		vec.push_back( 11 );
		vec.push_back( 33 );
		auto range= vec.range();
		auto mut res= 1;
		foreach( x : range.subrange( 1s, 3s ) ) { res*= x; }
		halt if( res != 55 * 11 );
	}
	{
		var VecType mut vec;
		vec.push_back( 31 );
		vec.push_back( -7 );
		vec.push_back( -8 );
		vec.push_back( 22 );
		vec.push_back(  7 );
		vec.push_back(  6 );
		vec.push_back( -4 );
		var i32 mut res= 0, mut iterations= 0;
		foreach( x : vec )
		{
			if( x < 0 ) { continue; }
			res+= x;
			++iterations;
		}
		halt if( res != 31 + 22 + 7 + 6 );
		halt if( iterations != 4 );
	}
	{ // Subrange start.
		var VecType vec= ust::make_array( 6, 8, 10, 12, 14 );

		halt if( !vec.range().subrange_start(5s).empty() );
		halt if( !vec.range().subrange_start(100s).empty() );
		halt if( vec.range() != vec.range().subrange_start(0s) );

		auto subrange_3= vec.range().subrange_start(3s);
		halt if(subrange_3.size() != 2s );
		halt if( subrange_3[0s] != 12 );
		halt if( subrange_3[1s] != 14 );
	}
	{ // Subrange end.
		var VecType vec= ust::make_array( 5, 4, 3, 2, 1 );

		halt if( !vec.range().subrange_end(0s).empty() );
		halt if( vec.range().subrange_end(5s) != vec.range() );
		halt if( vec.range().subrange_end(100s) != vec.range() );

		auto subrange_3= vec.range().subrange_end(3s);
		halt if(subrange_3.size() != 3s );
		halt if( subrange_3[0s] != 5 );
		halt if( subrange_3[1s] != 4 );
		halt if( subrange_3[2s] != 3 );
	}
	{ // Can construct immutable range from a vector.
		var VecType vec= ust::make_array( 7, 66, 555, 4444 );
		var ust::array_view_imut</i32/> r= vec;
		halt if( r.size() != 4s );
		halt if( r[0s] != 7 );
		halt if( r[1s] != 66 );
		halt if( r[2s] != 555 );
		halt if( r[3s] != 4444 );
	}
	{ // Can construct a mutable range from a vector.
		var VecType mut vec= ust::make_array( 11, 222, 3333 );
		var ust::array_view_mut</i32/> mut r= vec;
		halt if( r.size() != 3s );
		foreach( &mut x : r )
		{
			x*= -2;
		}
		halt if( r[0s] != -22 );
		halt if( r[1s] != -444 );
		halt if( r[2s] != -6666 );
	}
	{ // "foreach" macro for immutable "array_view_mut".
		var VecType mut vec= ust::make_array( 6, -3, 17 );
		var ust::array_view_mut</i32/> r= vec;
		foreach( &mut x : r )
		{
			x*= x;
		}
		halt if( r[0s] != 36 );
		halt if( r[1s] != 9 );
		halt if( r[2s] != 289 );
	}

	return 0;
}
