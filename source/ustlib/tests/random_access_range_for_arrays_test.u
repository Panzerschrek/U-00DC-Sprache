//##success_test
import "../imports/random_access_range.u"
import "../imports/string.u"

type IntRangeMut = ust::random_access_range_mut </i32/>;
type IntRangeImut= ust::random_access_range_imut</i32/>;

fn GetIntRangeSize( IntRangeImut range ) : size_type
{
	return range.size();
}

fn GetCharRangeSize( ust::random_access_range_imut</char8/> char_range ) : size_type
{
	return char_range.size();
}

enum SomeEnum{ EZero, EOne, ETwo, EThree }

struct StructWithTrivialLayout ordered
{
	u32 x;
	f32 y;
	[ char8, 4 ] z;
}

struct StructWithRef
{
	i32& r;
}

fn nomangle main() call_conv( "C" ) : i32
{
	{ // Random access range is empty by-default.
		var IntRangeImut range;
		halt if( !range.empty() );
	}
	{ // Can construct imut range from imut array.
		var [ i32, 5 ] arr= zero_init;
		var IntRangeImut range(arr);
		halt if( range.size() != 5s );
	}
	{ // Can construct imut range from mut array.
		var [ i32, 5 ] mut arr= zero_init;
		var IntRangeImut range(arr);
		halt if( range.size() != 5s );
	}
	{ // Can implicitly convert array to range.
		var [ i32, 16 ] arr= zero_init;
		auto size= GetIntRangeSize( arr );
		halt if( size != 16s );
	}
	{ // Can implicitly convert mut array to mut range.
		var [ i32, 3 ] mut arr[1, 22, 333];
		{
			var IntRangeMut mut r= arr;
			halt if( r.size() != 3s );
			foreach( &mut i : r )
			{
				i= -i;
			}
		}
		halt if(arr[0] != -1);
		halt if(arr[1] != -22);
		halt if(arr[2] != -333);
	}
	{ // Can convert mut range to imut range.
		var [ i32, 16 ] mut arr= zero_init;
		var IntRangeMut  range_mut (arr);
		var IntRangeImut range_imut(range_mut);
	}
	{ // Can implicitly convert mut range to imut range.
		var [ i32, 8 ] mut arr= zero_init;
		var IntRangeMut  range_mut (arr);
		auto size= GetIntRangeSize( range_mut );
		halt if( size != 8s );
	}
	halt if( GetCharRangeSize( "wtf?" ) != 4s ); // Can implicitly convert string literal to range
	{ // Can modify array content, using range.
		var [ i32, 5 ] mut arr= zero_init;
		IntRangeMut(arr).back()= 42;
		halt if( arr[4u] != 42 );
	}
	{ // Subrange.
		var [ i32, 5 ] arr= zero_init;
		var IntRangeImut range(arr);
		halt if( range.size() != 5s );
		halt if( range.subrange( 1s, 5s ).size() != 4s );
		halt if( range.subrange( 2s, 3s ).size() != 1s );
		halt if( range.subrange( 0s, 0s ).size() != 0s );
	}
	{ // Can modify array content, using subrange range.
		var [ i32, 4 ] mut arr= zero_init;
		{
			auto range= IntRangeMut(arr).subrange( 1s, 3s );
			range.front()= 45;
			range.back()= 66;
		}
		halt if( arr[1u] != 45 );
		halt if( arr[2u] != 66 );
	}
	{
		// "continue" should work properly.
		var [ i32, 7 ] mut arr[ 31, -7, -8, 22, 7, 6, -4 ];
		var i32 mut res= 0, mut iterations= 0;
		var IntRangeImut range(arr);
		foreach( x : range )
		{
			if( x < 0 ) { continue; }
			res+= x;
			++iterations;
		}
		halt if( res != 31 + 22 + 7 + 6 );
		halt if( iterations != 4 );
	}
	{ // swap
		var [ i32, 5 ] mut arr[ 0, 1, 2, 3, 4 ];
		{
			var IntRangeMut range( arr );
			range.swap( 2s, 4s );
		}
		halt if( arr[0] != 0 );
		halt if( arr[1] != 1 );
		halt if( arr[2] != 4 );
		halt if( arr[3] != 3 );
		halt if( arr[4] != 2 );
	}
	{ // swap
		var [ i32, 4 ] mut arr[ 33, 11, 55, 78 ];
		{
			var IntRangeMut range( arr );
			range.swap( 0s, 3s );
		}
		halt if( arr[0] != 78 );
		halt if( arr[1] != 11 );
		halt if( arr[2] != 55 );
		halt if( arr[3] != 33 );
	}
	{ // swap
		var [ i32, 1 ] mut arr[ 654 ];
		{
			var IntRangeMut range( arr );
			range.swap( 0s, 0s );
		}
		halt if( arr[0] != 654 );
	}
	{ // ==
		var [ i32, 3 ] v0[ 11, 22, 33 ];
		var [ i32, 3 ] v1[ 11, 22, 33 ];
		var [ i32, 3 ] v2[ 0 , 22, 33 ];
		var [ i32, 3 ] v3[ 11,  0, 33 ];
		var [ i32, 3 ] v4[ 11, 22,  0 ];
		var [ i32, 2 ] v5[ 11, 22 ];
		var [ i32, 0 ] v6[];

		static_assert( typeinfo</IntRangeImut/>.is_equality_comparable );
		var IntRangeImut r0(v0), r1(v1), r2(v2), r3(v3), r4(v4), r5(v5), r6(v6);

		halt if( r0 != r0 );
		halt if( r1 != r1 );
		halt if( r2 != r2 );
		halt if( r3 != r3 );
		halt if( r4 != r4 );
		halt if( r5 != r5 );
		halt if( r6 != r6 );

		halt if( r0 != r1 );
		halt if( r1 != r0 );

		halt if( r0 == r2 );
		halt if( r0 == r3 );
		halt if( r0 == r4 );
		halt if( r0 == r5 );
		halt if( r0 == r6 );

		// == for array and range must work too.
		halt if( r0 != v0 );
		halt if( r1 != v1 );
		halt if( v0 != r1 );
		halt if( v1 != r0 );

		halt if( v4 == r6 );
		halt if( r5 == v3 );
	}
	{ // starts_with
		var [ i32, 4 ] a0[ 76, 31356, -64, 46 ];
		var [ i32, 1 ] a1[ 76 ];
		var [ i32, 2 ] a2[ 76, 31356 ];
		var [ i32, 3 ] a3[ 76, 31356, -64 ];
		var [ i32, 1 ] a4[ 31356 ];
		var [ i32, 1 ] a5[ -64 ];
		var [ i32, 1 ] a6[ 46 ];
		var [ i32, 2 ] a7[ -64, 46 ];
		var [ i32, 2 ] a8[ 31356, -64 ];
		var [ i32, 5 ] a9[ 76, 31356, -64, 46, 7543 ];

		var IntRangeImut r(a0);
		halt if( !r.starts_with( IntRangeImut() ) );
		halt if( !r.starts_with( a1 ) );
		halt if( !r.starts_with( a2 ) );
		halt if( !r.starts_with( a3 ) );
		halt if( !r.starts_with( a0 ) );
		halt if( r.starts_with( a4 ) );
		halt if( r.starts_with( a5 ) );
		halt if( r.starts_with( a6 ) );
		halt if( r.starts_with( a7 ) );
		halt if( r.starts_with( a8 ) );
		halt if( r.starts_with( a9 ) );
		halt if( !IntRangeImut().starts_with( IntRangeImut() ) );
		halt if( IntRangeImut().starts_with( a1 ) );
	}
	{ // ends_with
		var [ i32, 3 ] a0[ 55, 66, 77 ];
		var [ i32, 1 ] a1[ 77 ];
		var [ i32, 2 ] a2[ 66, 77 ];
		var [ i32, 1 ] a3[ 66 ];
		var [ i32, 1 ] a4[ 55 ];
		var [ i32, 4 ] a5[ 44, 55, 66, 77 ];

		var IntRangeImut r(a0);
		halt if( !r.ends_with( IntRangeImut() ) );
		halt if( !r.ends_with( a1 ) );
		halt if( !r.ends_with( a2 ) );
		halt if( !r.ends_with( a0 ) );
		halt if( r.ends_with( a3 ) );
		halt if( r.ends_with( a4 ) );
		halt if( r.ends_with( a5 ) );
		halt if( !IntRangeImut().ends_with( IntRangeImut() ) );
		halt if( IntRangeImut().ends_with( a1 ) );
	}

	// Create range for single element.
	{
		var i32 mut x= 0;
		{
			var IntRangeMut range= ust::single_element_array_view( x );
			halt if( range.size() != 1s );
			range[0s]= 66;
		}
		halt if ( x != 66 );
	}
	{
		var i32 x= 778;
		var IntRangeImut range= ust::single_element_array_view( x );
		halt if( range.size() != 1s );
		halt if( range[0s] != 778 );
	}

	// Empty range
	{
		var IntRangeMut r_mut= ust::empty_range;
		var IntRangeImut r_imut= ust::empty_range;
		halt if( !r_mut.empty() );
		halt if( !r_imut.empty() );
	}

	// Reverse
	{
		var [ i32, 4 ] mut a[ 1, 2, 3, 4 ]; // Even number of elements
		IntRangeMut(a).reverse();
		halt if( a != ust::make_array( 4, 3, 2, 1 ) );
	}
	{
		var [ i32, 5 ] mut a[ 1, 2, 3, 4, 5 ]; // Odd number of elements
		IntRangeMut(a).reverse();
		halt if( a != ust::make_array( 5, 4, 3, 2, 1 ) );
	}
	{
		var [ i32, 0 ] mut a[]; // Empty array
		IntRangeMut(a).reverse();
		halt if( a != ust::make_array</i32/>() );
	}
	{ // random_access_range for a type with references inside.
		var [ ust::array_view_imut</ char8 />, 4 ] mut strings_array[ "lol", "foo", "qwerty", "123ABC!" ];

		{
			var ust::array_view_mut</ ust::array_view_imut</ char8 /> /> mut strings_array_view= strings_array;
			strings_array_view.drop_front();

			foreach( &mut view : strings_array_view )
			{
				view.drop_back();
			}
		}

		halt if( strings_array[0] != "lol" ); // Remains unchanged.
		halt if( strings_array[1] != "fo" );
		halt if( strings_array[2] != "qwert" );
		halt if( strings_array[3] != "123ABC" );
	}
	{
		var [ [ i32, 3 ], 4 ] mut matrix[ [ 1, 2, 3 ], [ 10, 20, 30 ], [ 100, 200, 300 ], [ 1000, 2000, 3000 ] ];
		var [ i32, 3 ] mut single_row[ 111, 222, 333 ];

		{
			var [ ust::array_view_mut</ [ i32, 3 ] />, 2 ] rows_array[ ust::array_view_mut</ [ i32, 3 ] />( matrix ), ust::single_element_array_view(single_row) ];
			halt if( rows_array[0].size() != 4s );
			halt if( rows_array[1].size() != 1s );

			var ust::array_view_imut</ ust::array_view_mut</ [ i32, 3 ] /> /> rows_view= rows_array;

			foreach( &row_group : rows_view )
			{
				foreach( &mut row : row_group )
				{
					foreach( &mut el : row )
					{
						el*= 3;
					}
				}
			}
		}
		halt if( matrix[0][0] != 3 );
		halt if( matrix[0][1] != 6 );
		halt if( matrix[0][2] != 9 );
		halt if( matrix[1][0] != 30 );
		halt if( matrix[1][1] != 60 );
		halt if( matrix[1][2] != 90 );
		halt if( matrix[2][0] != 300 );
		halt if( matrix[2][1] != 600 );
		halt if( matrix[2][2] != 900 );
		halt if( matrix[3][0] != 3000 );
		halt if( matrix[3][1] != 6000 );
		halt if( matrix[3][2] != 9000 );
		halt if( single_row[0] != 333 );
		halt if( single_row[1] != 666 );
		halt if( single_row[2] != 999 );
	}
	{ // Reverse iterator.
		var [ i32, 4 ] arr[ 5, 7, 9, 11 ];
		var [ i32, 4 ] mut dst= zero_init;
		var size_type mut index= 0s;
		foreach( el : IntRangeImut(arr).iter_reverse() )
		{
			dst[index] = el;
			++index;
		}

		halt if( dst[0] != 11 );
		halt if( dst[1] != 9 );
		halt if( dst[2] != 7 );
		halt if( dst[3] != 5 );
	}
	{ // Reverse mutable iterator.
		var [ i32, 3 ] mut arr[ 55, 66, 77 ];
		var [ i32, 3 ] mut dst= zero_init;
		var size_type mut index= 0s;
		foreach( &mut el : IntRangeMut(arr).iter_reverse() )
		{
			dst[index] = el;
			++index;
			el *= -2;
		}

		halt if( arr[0] != -110 );
		halt if( arr[1] != -132 );
		halt if( arr[2] != -154 );

		halt if( dst[0] != 77 );
		halt if( dst[1] != 66 );
		halt if( dst[2] != 55 );
	}
	{ // Reverse iterator for empty range.
		var [ i32, 0 ] arr[];
		foreach( el : IntRangeImut(arr).iter_reverse() )
		{
			// Should never enter this loop.
			halt;
		}
	}
	// to_byte8_range - immutable range of byte8-sized elements.
	{
		var [ char8, 6 ] arr= "qwerty";
		var ust::array_view_imut</char8/> range= arr;

		var ust::array_view_imut</byte8/> byte8_range= range.to_byte8_range();

		halt if( byte8_range.size() != range.size() );
		halt if( byte8_range[0s] != byte8('q') );
		halt if( byte8_range[1s] != byte8('w') );
		halt if( byte8_range[2s] != byte8('e') );
		halt if( byte8_range[3s] != byte8('r') );
		halt if( byte8_range[4s] != byte8('t') );
		halt if( byte8_range[5s] != byte8('y') );
	}
	// to_byte8_range - immutable range of byte32-sized elements.
	{
		var [ u32, 3 ] arr[ 67444u, 233u, 744519774u ];

		var ust::array_view_imut</u32/> range= arr;

		var ust::array_view_imut</byte8/> byte8_range= range.to_byte8_range();

		// TODO - adopt this for big-ending targets.

		halt if( byte8_range.size() != range.size() * typeinfo</u32/>.size_of );

		halt if( byte8_range[0s] != byte8(0x74u8) );
		halt if( byte8_range[1s] != byte8(0x07u8) );
		halt if( byte8_range[2s] != byte8(0x01u8) );
		halt if( byte8_range[3s] != byte8(0x00u8) );

		halt if( byte8_range[4s] != byte8(0xe9u8) );
		halt if( byte8_range[5s] != byte8(0x00u8) );
		halt if( byte8_range[6s] != byte8(0x00u8) );
		halt if( byte8_range[7s] != byte8(0x00u8) );

		halt if( byte8_range[ 8s] != byte8(0x5Eu8) );
		halt if( byte8_range[ 9s] != byte8(0x78u8) );
		halt if( byte8_range[10s] != byte8(0x60u8) );
		halt if( byte8_range[11s] != byte8(0x2Cu8) );
	}
	// to_byte8_range - immutable range of arrays.
	{
		var[ [ char8, 5 ], 3 ] arr[ "abcde", "fghij", "klmno" ];

		var ust::array_view_imut</ [ char8, 5 ] /> range= arr;

		var ust::array_view_imut</byte8/> byte8_range= range.to_byte8_range();

		halt if( byte8_range.size() != 15s );
		halt if( byte8_range[ 0s] != byte8( 'a' ) );
		halt if( byte8_range[ 1s] != byte8( 'b' ) );
		halt if( byte8_range[ 2s] != byte8( 'c' ) );
		halt if( byte8_range[ 3s] != byte8( 'd' ) );
		halt if( byte8_range[ 4s] != byte8( 'e' ) );
		halt if( byte8_range[ 5s] != byte8( 'f' ) );
		halt if( byte8_range[ 6s] != byte8( 'g' ) );
		halt if( byte8_range[ 7s] != byte8( 'h' ) );
		halt if( byte8_range[ 8s] != byte8( 'i' ) );
		halt if( byte8_range[ 9s] != byte8( 'j' ) );
		halt if( byte8_range[10s] != byte8( 'k' ) );
		halt if( byte8_range[11s] != byte8( 'l' ) );
		halt if( byte8_range[12s] != byte8( 'm' ) );
		halt if( byte8_range[13s] != byte8( 'n' ) );
		halt if( byte8_range[14s] != byte8( 'o' ) );
	}
	// to_byte8_range - immutable range of floats.
	{
		var [ f32, 2 ] arr[ 17.5f, -261.2f ];

		var ust::array_view_imut</ f32 /> range= arr;

		var ust::array_view_imut</byte8/> byte8_range= range.to_byte8_range();

		// TODO - adopt this for big-ending targets.

		halt if( byte8_range.size() != 8s );
		halt if( byte8_range[0s] != byte8( 0x00u8 ) );
		halt if( byte8_range[1s] != byte8( 0x00u8 ) );
		halt if( byte8_range[2s] != byte8( 0x8Cu8 ) );
		halt if( byte8_range[3s] != byte8( 0x41u8 ) );
		halt if( byte8_range[4s] != byte8( 0x9Au8 ) );
		halt if( byte8_range[5s] != byte8( 0x99u8 ) );
		halt if( byte8_range[6s] != byte8( 0x82u8 ) );
		halt if( byte8_range[7s] != byte8( 0xC3u8 ) );
	}
	// to_byte8_range - immutable range of raw pointers.
	{
		var i32 mut x= 0, mut y= 0;
		var [ $(i32), 2 ] arr[ $<(x), $<(y) ];

		var ust::array_view_imut</ $(i32) /> range= arr;

		var ust::array_view_imut</byte8/> byte8_range= range.to_byte8_range();

		halt if( byte8_range.size() != range.size() * typeinfo</ $(i32) />.size_of );

		var [ $(i32), 2 ] mut arr_copy= zero_init;

		unsafe( ust::memory_copy( ust::ptr_cast_to_byte8( $<(arr_copy) ), byte8_range.data(), byte8_range.size() ) );

		halt if( arr_copy != arr );
	}
	// to_byte8_range - immutable range of raw enum values.
	{
		var [ SomeEnum, 4 ] arr[ SomeEnum::EThree, SomeEnum::ETwo, SomeEnum::EOne, SomeEnum::EZero ];

		var ust::array_view_imut</ SomeEnum /> range= arr;

		var ust::array_view_imut</byte8/> byte8_range= range.to_byte8_range();

		halt if( byte8_range.size() != range.size() );
		halt if( byte8_range[0s] != byte8(3u8) );
		halt if( byte8_range[1s] != byte8(2u8) );
		halt if( byte8_range[2s] != byte8(1u8) );
		halt if( byte8_range[3s] != byte8(0u8) );
	}
	// to_byte8_range - mutable range of byte8-sized elements.
	{
		var [ char8, 3 ] mut arr= zero_init;

		{
			var ust::array_view_mut</byte8/> byte8_range= ust::array_view_mut</char8/>(arr).to_byte8_range();

			halt if( byte8_range.size() != 3s );

			byte8_range[0s]= byte8( '7' );
			byte8_range[1s]= byte8( 'q' );
			byte8_range[2s]= byte8( '_' );
		}

		halt if( arr != "7q_" );
	}
	// to_byte8_range - mutable range of u16-sized elements.
	{
		var [ u16, 3 ] mut arr= zero_init;

		{
			var ust::array_view_mut</byte8/> byte8_range= ust::array_view_mut</u16/>(arr).to_byte8_range();

			halt if( byte8_range.size() != 6s );

			byte8_range[0s]= byte8( 0x34u8 );
			byte8_range[1s]= byte8( 0x12u8 );
			byte8_range[2s]= byte8( 0x78u8 );
			byte8_range[3s]= byte8( 0x56u8 );
			byte8_range[4s]= byte8( 0xabu8 );
			byte8_range[5s]= byte8( 0x90u8 );
		}

		halt if( arr[0] != 0x1234u16 );
		halt if( arr[1] != 0x5678u16 );
		halt if( arr[2] != 0x90abu16 );
	}
	// to_byte8_range - mutable range of char8 array elements.
	{
		var [ [ char8, 3 ], 2 ] mut arr= zero_init;

		{
			var ust::array_view_mut</byte8/> byte8_range= ust::array_view_mut</ [ char8, 3 ] />(arr).to_byte8_range();

			halt if( byte8_range.size() != 6s );

			byte8_range[0s]= byte8( '0' );
			byte8_range[1s]= byte8( '1' );
			byte8_range[2s]= byte8( '2' );
			byte8_range[3s]= byte8( '3' );
			byte8_range[4s]= byte8( '4' );
			byte8_range[5s]= byte8( '5' );
		}

		halt if( arr[0] != "012" );
		halt if( arr[1] != "345" );
	}
	// to_byte8_range - immutable range of tuples.
	{
		type ElementType= tup[ u32, f32, [ char8, 4 ] ];
		var [ ElementType, 2 ] arr[ [ 16734u, 0.25f, "SPQR" ], [ 1674578423u, -572.23f, "r! 6" ] ];

		var ust::array_view_imut</byte8/> byte8_range= ust::array_view_imut</ElementType/>(arr).to_byte8_range();

		halt if( byte8_range.size() != 24s );

		halt if( byte8_range[ 0s] != byte8( 0x5Eu8 ) );
		halt if( byte8_range[ 1s] != byte8( 0x41u8 ) );
		halt if( byte8_range[ 2s] != byte8( 0x00u8 ) );
		halt if( byte8_range[ 3s] != byte8( 0x00u8 ) );
		halt if( byte8_range[ 4s] != byte8( 0x00u8 ) );
		halt if( byte8_range[ 5s] != byte8( 0x00u8 ) );
		halt if( byte8_range[ 6s] != byte8( 0x80u8 ) );
		halt if( byte8_range[ 7s] != byte8( 0x3Eu8 ) );
		halt if( byte8_range[ 8s] != byte8( 'S' ) );
		halt if( byte8_range[ 9s] != byte8( 'P' ) );
		halt if( byte8_range[10s] != byte8( 'Q' ) );
		halt if( byte8_range[11s] != byte8( 'R' ) );
		halt if( byte8_range[12s] != byte8( 0xF7u8 ) );
		halt if( byte8_range[13s] != byte8( 0x09u8 ) );
		halt if( byte8_range[14s] != byte8( 0xD0u8 ) );
		halt if( byte8_range[15s] != byte8( 0x63u8 ) );
		halt if( byte8_range[16s] != byte8( 0xB8u8 ) );
		halt if( byte8_range[17s] != byte8( 0x0Eu8 ) );
		halt if( byte8_range[18s] != byte8( 0x0Fu8 ) );
		halt if( byte8_range[19s] != byte8( 0xC4u8 ) );
		halt if( byte8_range[20s] != byte8( 'r' ) );
		halt if( byte8_range[21s] != byte8( '!' ) );
		halt if( byte8_range[22s] != byte8( ' ' ) );
		halt if( byte8_range[23s] != byte8( '6' ) );
	}
	// to_byte8_range - mutable range of tuples.
	{
		type ElementType= tup[ u16, char8, i8 ];
		var [ ElementType, 2 ] mut arr= zero_init;

		{
			var ust::array_view_mut</byte8/> byte8_range= ust::array_view_mut</ElementType/>(arr).to_byte8_range();

			halt if( byte8_range.size() != 8s );

			byte8_range[0s]= byte8( 0x12u8 );
			byte8_range[1s]= byte8( 0x34u8 );
			byte8_range[2s]= byte8( 0x56u8 );
			byte8_range[3s]= byte8( 0x78u8 );
			byte8_range[4s]= byte8( 0x9Au8 );
			byte8_range[5s]= byte8( 0xBCu8 );
			byte8_range[6s]= byte8( 0xDEu8 );
			byte8_range[7s]= byte8( 0xF0u8 );
		}

		halt if( arr[0][0] != 0x3412u16 );
		halt if( arr[0][1] != char8( 0x56 ) );
		halt if( arr[0][2] != 0x78i8 );
		halt if( arr[1][0] != 0xBC9Au16 );
		halt if( arr[1][1] != char8( 0xDE ) );
		halt if( arr[1][2] != i8( 0xF0u8 ) );
	}
	// to_byte8_range - immutable range of structs.
	{
		var [ StructWithTrivialLayout, 2 ] arr[ { .x= 16734u, .y= 0.25f, .z= "SPQR" }, { .x= 1674578423u, .y= -572.23f, .z= "r! 6" } ];

		var ust::array_view_imut</byte8/> byte8_range= ust::array_view_imut</StructWithTrivialLayout/>(arr).to_byte8_range();

		halt if( byte8_range.size() != 24s );

		halt if( byte8_range[ 0s] != byte8( 0x5Eu8 ) );
		halt if( byte8_range[ 1s] != byte8( 0x41u8 ) );
		halt if( byte8_range[ 2s] != byte8( 0x00u8 ) );
		halt if( byte8_range[ 3s] != byte8( 0x00u8 ) );
		halt if( byte8_range[ 4s] != byte8( 0x00u8 ) );
		halt if( byte8_range[ 5s] != byte8( 0x00u8 ) );
		halt if( byte8_range[ 6s] != byte8( 0x80u8 ) );
		halt if( byte8_range[ 7s] != byte8( 0x3Eu8 ) );
		halt if( byte8_range[ 8s] != byte8( 'S' ) );
		halt if( byte8_range[ 9s] != byte8( 'P' ) );
		halt if( byte8_range[10s] != byte8( 'Q' ) );
		halt if( byte8_range[11s] != byte8( 'R' ) );
		halt if( byte8_range[12s] != byte8( 0xF7u8 ) );
		halt if( byte8_range[13s] != byte8( 0x09u8 ) );
		halt if( byte8_range[14s] != byte8( 0xD0u8 ) );
		halt if( byte8_range[15s] != byte8( 0x63u8 ) );
		halt if( byte8_range[16s] != byte8( 0xB8u8 ) );
		halt if( byte8_range[17s] != byte8( 0x0Eu8 ) );
		halt if( byte8_range[18s] != byte8( 0x0Fu8 ) );
		halt if( byte8_range[19s] != byte8( 0xC4u8 ) );
		halt if( byte8_range[20s] != byte8( 'r' ) );
		halt if( byte8_range[21s] != byte8( '!' ) );
		halt if( byte8_range[22s] != byte8( ' ' ) );
		halt if( byte8_range[23s] != byte8( '6' ) );
	}
	{	// to_byte8_range - immutable range of structs containing a reference field.
		var i32 x= 1, y= 2, z= 3;
		var [ $(i32), 3 ] ptr_arr[ unsafe( $<( cast_mut(x) ) ), unsafe( $<( cast_mut(y) ) ), unsafe( $<( cast_mut(z) ) ) ];
		var [ StructWithRef, 3 ] arr[ { .r=x }, { .r= y }, { .r= z } ];

		var ust::array_view_imut</byte8/> byte8_range_of_pointers= ust::array_view_imut</ $(i32) />(ptr_arr).to_byte8_range();
		var ust::array_view_imut</byte8/> byte8_range= ust::array_view_imut</StructWithRef/>(arr).to_byte8_range();

		// Range of structs with references inside should be equal to range of raw pointers (pointing to the same values).

		halt if( byte8_range_of_pointers.size() != 3s * typeinfo</ $(i32) />.size_of );
		halt if( byte8_range.size() != 3s * typeinfo</ $(i32) />.size_of );

		foreach( pair : byte8_range_of_pointers.iter().zip( byte8_range.iter() ) )
		{
			halt if( pair.first != pair.second );
		}
	}
	{ // "copy_from".
		var [ i32, 3 ] mut a0= zero_init;
		var [ i32, 2 ] a1[ 1, 2 ];
		var [ i32, 4 ] a2[ 3, 4, 5, 6 ];
		var [ i32, 3 ] a3[ 7, 8, 9 ];

		var ust::array_view_mut</i32/> a0_range(a0);

		// Copy from range with size less than needed.
		a0_range.copy_from( ust::array_view_imut</i32/>( a1 ) );
		halt if( a0_range[0s] != 1 );
		halt if( a0_range[1s] != 2 );
		halt if( a0_range[2s] != 0 );

		// Copy from range with size greater than needed.
		a0_range.copy_from( ust::array_view_imut</i32/>( a2 ) );
		halt if( a0_range[0s] != 3 );
		halt if( a0_range[1s] != 4 );
		halt if( a0_range[2s] != 5 );

		// Copy from range with equal size.
		a0_range.copy_from( ust::array_view_imut</i32/>( a3 ) );
		halt if( a0_range[0s] != 7 );
		halt if( a0_range[1s] != 8 );
		halt if( a0_range[2s] != 9 );
	}
	{ // "copy_from" for mutable source.
		var [ f32, 3 ] mut a0= zero_init;
		var [ f32, 2 ] mut a1[ 1.0f, 2.0f ];
		var [ f32, 4 ] mut a2[ 3.0f, 4.0f, 5.0f, 6.0f ];
		var [ f32, 3 ] mut a3[ 7.0f, 8.0f, 9.0f ];

		var ust::array_view_mut</f32/> a0_range(a0);

		// Copy from range with size less than needed.
		a0_range.copy_from( ust::array_view_mut</f32/>( a1 ) );
		halt if( a0_range[0s] != 1.0f );
		halt if( a0_range[1s] != 2.0f );
		halt if( a0_range[2s] != 0.0f );

		// Copy from range with size greater than needed.
		a0_range.copy_from( ust::array_view_mut</f32/>( a2 ) );
		halt if( a0_range[0s] != 3.0f );
		halt if( a0_range[1s] != 4.0f );
		halt if( a0_range[2s] != 5.0f );

		// Copy from range with equal size.
		a0_range.copy_from( ust::array_view_mut</f32/>( a3 ) );
		halt if( a0_range[0s] != 7.0f );
		halt if( a0_range[1s] != 8.0f );
		halt if( a0_range[2s] != 9.0f );

		// Source is unchanged.
		halt if( a1[0] != 1.0f );
		halt if( a1[1] != 2.0f );
		halt if( a2[0] != 3.0f );
		halt if( a2[1] != 4.0f );
		halt if( a2[2] != 5.0f );
		halt if( a2[3] != 6.0f );
		halt if( a3[0] != 7.0f );
		halt if( a3[1] != 8.0f );
		halt if( a3[2] != 9.0f );
	}
	{ // "copy_from" for strings.
		var [ ust::string8, 3 ] mut a0[ "zero", "zero", "zero" ];
		var [ ust::string8, 2 ] a1[ "one", "two" ];
		var [ ust::string8, 4 ] a2[ "three", "four", "five", "six" ];
		var [ ust::string8, 3 ] a3[ "seven", "eight", "nine" ];

		var ust::array_view_mut</ust::string8/> a0_range(a0);

		// Copy from range with size less than needed.
		a0_range.copy_from( ust::array_view_imut</ust::string8/>( a1 ) );
		halt if( a0_range[0s] != "one" );
		halt if( a0_range[1s] != "two" );
		halt if( a0_range[2s] != "zero" );

		// Copy from range with size greater than needed.
		a0_range.copy_from( ust::array_view_imut</ust::string8/>( a2 ) );
		halt if( a0_range[0s] != "three" );
		halt if( a0_range[1s] != "four" );
		halt if( a0_range[2s] != "five" );

		// Copy from range with equal size.
		a0_range.copy_from( ust::array_view_imut</ust::string8/>( a3 ) );
		halt if( a0_range[0s] != "seven" );
		halt if( a0_range[1s] != "eight" );
		halt if( a0_range[2s] != "nine" );
	}

	return 0;
}
