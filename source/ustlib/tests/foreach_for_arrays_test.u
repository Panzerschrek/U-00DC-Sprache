//##success_test
import "../imports/helpers.u"
import "../imports/ustlib_macros.u"

fn nomangle main() call_conv( "C" ) : i32
{
	{
		var [ i32, 4 ] arr[ 4, 8, 15, 16 ];
		var i32 mut sum= 0;
		foreach( x : arr ) { sum+= x; }
		halt if( sum != 4 + 8 + 15 + 16 );
	}
	{
		var [ i32, 3 ] mut arr[ -54, 65, 895 ];
		var size_type mut iterations(0);
		foreach( &mut x : arr )
		{
			x*= -2;
			++iterations;
		}
		halt if( iterations != 3s );
		halt if( arr[0u] != 108 );
		halt if( arr[1u] != -130 );
		halt if( arr[2u] != -1790 );
	}
	{
		var [ i32, 0 ] arr[];
		var size_type mut iterations(0);
		foreach( x : arr ) { ust::ignore_unused(x); ++iterations; }
		halt if( iterations != 0s );
	}
	{
		// "continue" should work properly.
		var [ i32, 7 ] mut arr[ 31, -7, -8, 22, 7, 6, -4 ];
		var i32 mut res= 0, mut iterations= 0;
		foreach( x : arr )
		{
			if( x < 0 ) { continue; }
			res+= x;
			++iterations;
		}
		halt if( res != 31 + 22 + 7 + 6 );
		halt if( iterations != 4 );
	}

	return 0;
}
