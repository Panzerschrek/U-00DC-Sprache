import "../container_utils.u"
import "../memory.u"
import "../pointer.u"

namespace ust
{

// Mark as "shared", because counter operations are not thread-safe.
template</ type T />
class shared_ptr_final_base shared
{
	// Forbid shared pointers for structs with references inside.
	static_assert( typeinfo</T/>.references_tags_count == size_type(0) );

	type hasher= shared_ptr_final_base_hasher;

public:
	fn constructor()= default;

	fn constructor( T mut value )
	{
		unsafe
		{
			ptr_= storage_ptr( cast_ref_unsafe</storage/>( memory_allocate( typeinfo</storage/>.size_of ) ) );
			move_unsafe( ptr_.get_ref().value, move(value) );
			ptr_.get_ref().counter= 1u;
		}
	}

	fn constructor( mut this, this_type &imut other )
	{
		this= other;
	}

	fn destructor()
	{
		reset();
	}

	op=( mut this, this_type &imut other )
	{
		reset();
		unsafe
		{
			if( !other.ptr_.is_null() )
			{
				ptr_= other.ptr_;
				++ptr_.get_ref().counter;
			}
		}
	}

	fn reset( mut this )
	{
		if( empty() ) { return; }
		unsafe
		{
			--ptr_.get_ref().counter;
			if( ptr_.get_ref().counter == 0u )
			{
				call_destructor( ptr_.get_ref().value );
				memory_free( ptr_.get_ref() );
			}
			ptr_= storage_ptr();
		}
	}

	fn reset( mut this, T mut value )
	{
		this= this_type( move(value) );
	}

	fn empty( this ) : bool
	{
		return ptr_.is_null();
	}

	fn get_ref_unsafe( this ) unsafe : T &imut
	{
		unsafe
		{
			return ptr_.get_ref().value;
		}
	}

	op==( this_type& l, this_type& r ) : bool
	{
		return l.ptr_ == r.ptr_;
	}

	op!=( this_type& l, this_type& r ) : bool
	{
		return l.ptr_ != r.ptr_;
	}

private:
	type this_type= shared_ptr_final_base</T/>;

	struct storage
	{
		u32 counter;
		T value;
	}
	type storage_ptr= raw_ptr_mut</storage/>;

private:
	storage_ptr ptr_;
}

struct shared_ptr_final_base_hasher
{
	template</type T/>
	fn hash( shared_ptr_final_base</T/>& ptr ) : size_type
	{
		unsafe
		{
			return ust::ref_to_int( ptr.get_ref_unsafe() );
		}
	}
}

struct shared_ptr_final_hasher
{
	template</type PtrT/>
	fn hash( PtrT& ptr ) : size_type
	{
		unsafe{ return shared_ptr_final_base_hasher::hash( ptr.get_impl() ); }
	}
}

} // namespace ust
