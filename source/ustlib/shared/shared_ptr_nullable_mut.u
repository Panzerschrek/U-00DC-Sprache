import "shared_ptr_base.u"
import "shared_ptr_mut.u"

namespace ust
{

template</ type T />
class shared_ptr_nullable_mut
{
public:
	fn constructor()= default;

	fn constructor( T mut value )
	( impl_( move(value) ) )
	{}

	fn conversion_constructor( shared_ptr_mut</T/> & ptr )
	{
		unsafe{  impl_= shared_ptr_base</ T />( ptr.get_impl() );  }
	}

	fn constructor( mut this, this_type &imut other )= default;
	op=( mut this, this_type &imut other )= default;

	fn reset( mut this )
	{
		impl_.reset();
	}

	fn reset( mut this, T mut value )
	{
		impl_.reset( move(value) );
	}

	fn empty( this ) : bool
	{
		return impl_.empty();
	}

	fn lock_mut ( this ) : shared_ptr_impl::lock_mut </T/>'this'
	{
		return impl_.lock_mut ();
	}

	fn lock_imut( this ) : shared_ptr_impl::lock_imut</T/>'this'
	{
		return impl_.lock_imut();
	}

public: // Methods for internal usage
	fn constructor( shared_ptr_base</ T /> mut impl ) unsafe
	( impl_(move(impl)) )
	{}

	fn get_impl( this ) unsafe : shared_ptr_base</ T />
	{
		return impl_;
	}

private:
	type this_type= shared_ptr_nullable_mut</T/>;

private:
	shared_ptr_base</ T /> impl_;
}

template</ type T />
fn to_non_nullable( shared_ptr_nullable_mut</T/> & val ) : shared_ptr_mut</T/>
{
	halt if( val.empty() );
	unsafe
	{
		return shared_ptr_mut</T/>( val.get_impl() );
	}
}

} // namespace ust
