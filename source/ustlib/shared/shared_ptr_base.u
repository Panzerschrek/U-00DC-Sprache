import "../container_utils.u"
import "../memory.u"
import "../polymorph.u"
import "../type_traits.u"

namespace ust
{

template</ type T, bool is_nullable, bool is_mutable />
class shared_ptr_base shared
{
	// Forbid shared pointers for structs with references inside.
	static_assert( typeinfo</T/>.references_tags_count == size_type(0) );

	type hasher= shared_ptr_base_hasher;

public:
	// Default constructor. Exists only for nullable shared_ptr.
	fn enable_if(is_nullable) constructor()= default;

	// Construct with value.
	fn constructor( T mut value )
	{
		unsafe
		{
			value_= $<( cast_ref_unsafe</T/>( memory_allocate( typeinfo</T/>.size_of ) ) );
			move_into_uninitialized( $>(value_), move(value) );

			counter_= $<( cast_ref_unsafe</ shared_ptr_impl::counter />( memory_allocate( typeinfo</ shared_ptr_impl::counter />.size_of ) ) );
			$>(counter_).use_count_total= 1u;
			$>(counter_).use_count_strong= 1u;
			$>(counter_).lock_count= 0;
		}
	}

	// Constructor for nullable ptr from non-nullable ptr.
	fn enable_if( is_nullable ) conversion_constructor( mut this, shared_ptr_base</T, false, is_mutable/>& ptr )
	{
		unsafe
		{
			value_= ptr.get_value_ptr();
			counter_= ptr.get_counter_ptr();
			// It's almost impossible to get counter owerflow here - so, ignore such possibility.
			++$>(counter_).use_count_total;
			++$>(counter_).use_count_strong;
		}
	}

	// Constructor for immutable ptr from mutable ptr.
	fn enable_if( !is_mutable ) conversion_constructor( mut this, shared_ptr_base</T, is_nullable, true/>& ptr )
	{
		if( !ptr.empty() )
		{
			unsafe
			{
				value_= ptr.get_value_ptr();
				counter_= ptr.get_counter_ptr();
				// It's almost impossible to get counter owerflow here - so, ignore such possibility.
				++$>(counter_).use_count_total;
				++$>(counter_).use_count_strong;
			}
		}
	}

	// Constructor for immutable nullable ptr from mutable non-nullable ptr.
	fn enable_if( !is_mutable && is_nullable ) conversion_constructor( mut this, shared_ptr_base</T, false, true/>& ptr )
	{
		unsafe
		{
			value_= ptr.get_value_ptr();
			counter_= ptr.get_counter_ptr();
			// It's almost impossible to get counter owerflow here - so, ignore such possibility.
			++$>(counter_).use_count_total;
			++$>(counter_).use_count_strong;
		}
	}

	// Copy constructor.
	fn constructor( mut this, this_type &imut other )
	{
		if( !other.empty() )
		{
			unsafe
			{
				value_= other.value_;
				counter_= other.counter_;
				// It's almost impossible to get counter owerflow here - so, ignore such possibility.
				++$>(counter_).use_count_total;
				++$>(counter_).use_count_strong;
			}
		}
	}

	// Construct from shared_ptr of compatible type.
	template</ type U />
	fn enable_if( !is_same_type</T, U/>() && is_ancestor_for</T, U/>() )
	conversion_constructor( mut this, shared_ptr_base</U, is_nullable, is_mutable/>& ptr )
	{
		if( !ptr.empty() )
		{
			unsafe
			{
				value_= $<( cast_ref</T/>( $>(ptr.get_value_ptr() ) ) );
				counter_= ptr.get_counter_ptr();
				// It's almost impossible to get counter owerflow here - so, ignore such possibility.
				++$>(counter_).use_count_total;
				++$>(counter_).use_count_strong;
			}
		}
	}

	fn destructor()
	{
		if( empty() ) { return; }
		unsafe
		{
			--$>(counter_).use_count_strong;
			if( $>(counter_).use_count_strong == 0u )
			{
				call_destructor( $>(value_) );
				memory_free( polymorph_restore_original_ref( $>(value_) ) );
			}

			--$>(counter_).use_count_total;
			if( $>(counter_).use_count_total == 0u )
			{
				memory_free( cast_ref_unsafe</void/>( $>(counter_) ) );
			}
		}
	}

	// Copy-assignment operator.
	op=( mut this, this_type &imut other )
	{
		// Call copy constructor for other and move-assign copy to this.
		this= this_type(other);
	}

	fn enable_if(is_nullable) reset( mut this )
	{
		// Move value ot out "this", call destructor for it, construct default value in place of "this".
		take(this);
	}

	fn reset( mut this, T mut value )
	{
		this= this_type( move(value) );
	}

	fn empty( this ) : bool
	{
		static_if( is_nullable )
		{
			return value_ == nullptr</T/>();
		}
		else
		{
			return false;
		}
	}

	// Checked conversion to non-nullable from nullable.
	fn enable_if(is_nullable) try_to_non_nullable( this ) : shared_ptr_base</T, false, is_mutable/>
	{
		halt if(empty());

		unsafe
		{
			// It's almost impossible to get counter owerflow here - so, ignore such possibility.
			++$>(counter_).use_count_total;
			++$>(counter_).use_count_strong;

			// Just copy contents (a pait of pointers). This works fine since layeout of different shared_ptr_base classes is same.
			var shared_ptr_base</T, false, is_mutable/> mut res= uninitialized;

			static_assert( typeinfo</this_type/>.size_of  == typeinfo</ shared_ptr_base</T, false, is_mutable/> />.size_of  );
			static_assert( typeinfo</this_type/>.align_of == typeinfo</ shared_ptr_base</T, false, is_mutable/> />.align_of );

			memory_copy_aligned( typeinfo</this_type/>.align_of, cast_ref_unsafe</void/>(res), cast_ref_unsafe</void/>(this), typeinfo</this_type/>.size_of );
			return move(res);
		}
	}

	// Lock operations.

	fn enable_if(is_nullable && is_mutable) try_lock_mut( this ) : lock_mut_t 'this'
	{
		halt if( empty() );
		unsafe{  return lock_mut_unchecked();  }
	}

	fn enable_if(is_nullable) try_lock_imut( this ) : lock_imut_t'this'
	{
		halt if( empty() );
		unsafe{  return lock_imut_unchecked();  }
	}

	fn enable_if(!is_nullable && is_mutable) lock_mut( this ) : lock_mut_t 'this'
	{
		unsafe{  return lock_mut_unchecked();  }
	}

	fn enable_if(!is_nullable) lock_imut( this ) : lock_imut_t'this'
	{
		unsafe{  return lock_imut_unchecked();  }
	}

	fn enable_if(is_mutable) lock_mut_unchecked( this ) unsafe : lock_mut_t 'this'
	{
		unsafe{  return lock_mut_t ( $>(value_), counter_ );  }
	}

	fn lock_imut_unchecked( this ) unsafe : lock_imut_t'this'
	{
		unsafe{  return lock_imut_t( $>(value_), counter_ );  }
	}

	// Compare.

	op==( this_type& l, this_type& r ) : bool
	{
		return l.value_ == r.value_;
	}

	op!=( this_type& l, this_type& r ) : bool
	{
		return l.value_ != r.value_;
	}

public: // Methods for internal usage.

	fn get_value_ptr( this ) unsafe : $(T)
	{
		return value_;
	}

	fn get_counter_ptr( this ) unsafe : $(shared_ptr_impl::counter)
	{
		return counter_;
	}

	// Counter must be incremented before this constructor call.
	fn constructor( $(T) value_ptr, $(shared_ptr_impl::counter) counter_ptr ) unsafe
	( value_(value_ptr), counter_(counter_ptr) )
	{}

private:
	type this_type= shared_ptr_base</ T, is_nullable, is_mutable />;
	type value_ptr= $(T);
	type counter_ptr= $(shared_ptr_impl::counter);
	type lock_mut_t = shared_ptr_impl::lock_mut </T/>;
	type lock_imut_t= shared_ptr_impl::lock_imut</T/>;

private:
	value_ptr value_= zero_init;
	counter_ptr counter_= zero_init;
}

struct shared_ptr_base_hasher
{
	template</type T, bool is_nullable, bool is_mutable/>
	fn hash( shared_ptr_base</T, is_nullable, is_mutable/>& ptr ) : size_type
	{
		unsafe
		{
			return ust::ref_to_int( cast_ref_unsafe</void/>( $>( ptr.get_value_ptr() ) ) );
		}
	}
}

struct shared_ptr_hasher
{
	template</type PtrT/>
	fn hash( PtrT& ptr ) : size_type
	{
		unsafe{ return shared_ptr_base_hasher::hash( ptr.get_impl() ); }
	}
}

template</ type T, bool is_mutable />
class weak_ptr_base shared
{
public:
	fn constructor()= default;

	// Construct from nullable shared_ptr_base.
	fn constructor( shared_ptr_type& ptr )
	{
		unsafe
		{
			value_= ptr.get_value_ptr();
			counter_= ptr.get_counter_ptr();
			if( counter_ != nullptr</shared_ptr_impl::counter/>() )
			{
				// It's almost impossible to get counter owerflow here - so, ignore such possibility.
				++$>(counter_).use_count_total;
			}
		}
	}

	// Construct from non-nullable shared_ptr_base.
	fn constructor( shared_ptr_base</ T, false, is_mutable />& ptr )
	{
		unsafe
		{
			value_= ptr.get_value_ptr();
			counter_= ptr.get_counter_ptr();
			// It's almost impossible to get counter owerflow here - so, ignore such possibility.
			++$>(counter_).use_count_total;
		}
	}

	// Construct from weak_ptr of compatible type.
	template</ type U />
	fn enable_if( !is_same_type</T, U/>() && is_ancestor_for</T, U/>() )
	conversion_constructor( mut this, weak_ptr_base</U, is_mutable/>& ptr )
	{
		unsafe
		{
			if( ptr.get_value_ptr() != nullptr</U/>() )
			{
				value_= $<( cast_ref</T/>( $>( ptr.get_value_ptr() ) ) ); // Convert reference here.
				counter_= ptr.get_counter_ptr();
				// It's almost impossible to get counter owerflow here - so, ignore such possibility.
				++$>(counter_).use_count_total;
			}
		}
	}

	fn constructor( mut this, this_type &imut other )
	{
		this= other;
	}

	fn destructor()
	{
		reset();
	}

	op=( mut this, this_type &imut other )
	{
		reset();
		unsafe
		{
			if( other.counter_ != nullptr</shared_ptr_impl::counter/>() )
			{
				value_= other.value_;
				counter_= other.counter_;
				// It's almost impossible to get counter owerflow here - so, ignore such possibility.
				++$>(counter_).use_count_total;
			}
		}
	}

	fn reset( mut this )
	{
		unsafe
		{
			if( counter_ != nullptr</shared_ptr_impl::counter/>() )
			{
				--$>(counter_).use_count_total;
				if( $>(counter_).use_count_total == 0u )
				{
					memory_free( cast_ref_unsafe</void/>( $>(counter_) ) );
					value_= nullptr</T/>();
				}
				counter_= nullptr</shared_ptr_impl::counter/>();
			}
		}
	}

	fn lock( this ) : shared_ptr_type
	{
		unsafe
		{
			if( counter_ == nullptr</shared_ptr_impl::counter/>() || $>(counter_).use_count_strong == 0u )
			{
				return shared_ptr_type();
			}
			// It's almost impossible to get counter owerflow here - so, ignore such possibility.
			++$>(counter_).use_count_total;
			++$>(counter_).use_count_strong;
			return shared_ptr_type( value_, counter_ );
		}
	}

public: // Methods for internal usage.
	fn get_value_ptr( this ) unsafe : $(T)
	{
		return value_;
	}

	fn get_counter_ptr( this ) unsafe : $(shared_ptr_impl::counter)
	{
		return counter_;
	}

private:
	type this_type= weak_ptr_base</ T, is_mutable />;
	type shared_ptr_type= shared_ptr_base</ T, true, is_mutable />;
	type value_ptr= $(T);
	type counter_ptr= $(shared_ptr_impl::counter);
	type lock_mut_t = shared_ptr_impl::lock_mut </T/>;
	type lock_imut_t= shared_ptr_impl::lock_imut</T/>;

private:
	value_ptr value_= zero_init;
	counter_ptr counter_= zero_init;
}

namespace shared_ptr_impl
{

struct counter
{
	u32 use_count_total;
	u32 use_count_strong; // always <= use_count_total
	i32 lock_count; // 0 - not locked, positive value - locked as imut, -1 - locked as mut.
}

template</ type T />
class lock_mut
{
public:
	fn constructor( mut this'a', T &'b  mut value, $(counter) counter ) ' a <- b ' unsafe
		( value_(value), counter_(counter) )
	{
		unsafe
		{
			halt if( $>(counter_).lock_count != 0 ); // halt if have any locks
			$>(counter_).lock_count= -1;
		}
	}

	fn destructor()
	{
		unsafe{  $>(counter_).lock_count= 0;  }
	}

	// Mark reference by "this" tag, instead of inner tag, for prevention of "lock" destruction.
	fn deref( mut this ) : T &'this  mut
	{
		unsafe
		{
			return $>( $<(value_) );
		}
	}

private:
	T & mut value_;
	$(counter) counter_= zero_init;
}

template</ type T />
class lock_imut
{
public:
	fn constructor( mut this'a', T &'b imut value, $(counter) counter ) ' a <- b ' unsafe
		( value_(value), counter_(counter) )
	{
		unsafe
		{
			halt if( $>(counter_).lock_count < 0 ); // halt if have mutable locks
			// It's almost impossible to get counter owerflow here - so, ignore such possibility.
			++$>(counter_).lock_count;
		}
	}

	fn destructor()
	{
		unsafe{  --$>(counter_).lock_count;  }
	}

	// Mark reference by "this" tag, instead of inner tag, for prevention of "lock" destruction.
	fn deref( this ) : T &'this imut
	{
		unsafe
		{
			return $>($<(cast_mut(value_)));
		}
	}

private:
	T &imut value_;
	$(counter) counter_= zero_init;
}

} // namespace shared_ptr_impl

} // namespace ust
