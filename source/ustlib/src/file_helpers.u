import "../imports/file_helpers.u"

namespace ust
{

fn read_file_to_byte8_vector( filesystem_path_view path ) : filesystem_result</ vector</byte8/> />
{
	result_unwrap_or_return( mut f : file_readable::open( path ) );
	result_unwrap_or_return( s : f.get_size() );

	if( is_size_too_large_to_allocate(s) )
	{
		return io_error::out_of_memory;
	}

	unsafe // Because of uninitialized.
	{
		var vector</byte8/> mut res;

		// It's assumed that compiler can optimize-out filling container memory with uninitialized value.
		// So, using such approach we prevent unnecessary filling of memory, which will be soon overwritten.
		var byte8 filler= uninitialized;
		res.resize( size_type(s), filler );

		result_unwrap_or_return( v : f.read_exact( res ) );
		ignore_unused(v);

		return res;
	}
}

fn read_file_to_string( filesystem_path_view path ) : filesystem_result</ string8 />
{
	result_unwrap_or_return( mut f : file_readable::open( path ) );
	result_unwrap_or_return( s : f.get_size() );

	if( is_size_too_large_to_allocate(s) )
	{
		return io_error::out_of_memory;
	}

	unsafe // Because of uninitialized.
	{
		var string8 mut res;

		// It's assumed that compiler can optimize-out filling container memory with uninitialized value.
		// So, using such approach we prevent unnecessary filling of memory, which will be soon overwritten.
		var char8 filler= uninitialized;
		res.resize( size_type(s), filler );

		result_unwrap_or_return( v : f.read_exact( cast_range_to_bytes_range( res.range() ) ) );
		ignore_unused(v);

		return res;
	}
}

fn write_byte8_range_to_file( filesystem_path_view path, array_view_imut</byte8/> range ) : filesystem_result</void/>
{
	result_unwrap_or_return( mut f : file_writeable::create( path ) );

	return f.write_all( range );
}

fn write_string_view_to_file( filesystem_path_view path, string_view8 range ) : filesystem_result</void/>
{
	return write_byte8_range_to_file( path, cast_range_to_bytes_range(range) );
}

fn is_size_too_large_to_allocate( u64 size_bytes ) : bool
{
	// Allow allocating only less than half of the possible address space.
	var size_type constexpr limit= 1s << ( typeinfo</size_type/>.size_of * 8s - 1s );
	return size_bytes >= u64(limit);
}

// It's safe to cast between ranges of chars and bytes.
// TODO - make such conversion functions public.
template</bool is_mutable/>
fn cast_range_to_bytes_range( random_access_range</char8, is_mutable/> r ) : random_access_range</byte8, is_mutable/>
{
	return unsafe( random_access_range</byte8, is_mutable/>( ptr_cast_to_byte8( r.data() ), r.size() ) );
}

} // namespace ust
