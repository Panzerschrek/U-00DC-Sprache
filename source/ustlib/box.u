import "container_utils.u"
import "hash.u"
import "polymorph.u"
import "type_traits.u"

namespace ust
{

template</ type T />
class box
{
public:
	type hasher= box_hasher;

public:
	fn constructor()= delete;

	fn constructor( mut this, T mut val )
	{
		unsafe
		{
			ptr_= $<( cast_ref_unsafe</T/>( memory_allocate( typeinfo</T/>.size_of ) ) );
			move_unsafe</T/>( $>(ptr_), move(val) );
		}
	}

	template</ type U />
	fn enable_if( !is_same_type</T, U/>() && is_ancestor_for</T, U/>() )
	conversion_constructor( mut this, box</U/> mut val )
	{
		unsafe
		{
			ptr_= $<( cast_ref</T/>(val.get_ref()) );
			move_into_nowhere( move(val) );
		}
	}

	// box is NOT copyable, because copying required memory allocation. Copying must be done manually.

	fn destructor()
	{
		unsafe
		{
			call_destructor( $>(ptr_) );
			memory_free( polymorph_restore_original_ref( $>(ptr_) ) );
		}
	}

	fn get_ref( mut this ) : T &mut
	{
		unsafe{  return get_ref_unsafe();  }
	}

	fn get_ref( imut this ) : T &imut
	{
		unsafe{  return get_ref_unsafe();  }
	}

	fn get_ref_unsafe( mut this ) unsafe : T &mut
	{
		unsafe{  return $>(ptr_);  }
	}

	fn get_ref_unsafe( imut this ) unsafe : T &imut
	{
		unsafe{  return $>(ptr_);  }
	}

private:
	fn check_type()
	{
		static_assert( typeinfo</T/>.references_tags_count == 0s );
	}

	type element_type_for_shared_check= T;

private:
	$(T) ptr_= zero_init;
}

struct box_hasher
{
	template</ type T />
	fn hash( box</T/>& b ) : size_type
	{
		return ust::default_hasher::hash(b.get_ref());
	}
}

} // namespace ust
